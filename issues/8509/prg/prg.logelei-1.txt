

;prg.logelei-1.prg ==0801==
    1 rem *********************************
    2 rem *                               *
    3 rem *      logeleien fuer zwei      *
    4 rem *           variable            *
    5 rem *                               *
    6 rem *  heimo ponnath  hamburg 1985  *
    7 rem *                               *
    8 rem *********************************
    9 rem
   10 printchr$(147):poke53280,0:poke53281,0:poke646,5:goto30
   15 rem **** up cursor setzen ****
   20 poke211,s:poke214,z:sys58640:return
   25 rem **** hp-1: funktioneneingabe ***
   30 s=11:z=5:gosub20:poke646,7:print"logeleien fuer zwei"
   35 s=16:z=6:gosub20:poke646,2:print"variable"
   40 s=6:z=10:gosub20:poke646,5:print"im programm ist die funktion:"
   45 k=1:gosub100:k=0:s=0:z=15:gosub20:print"c="l$
   50 s=6:z=20:gosub20
   55 printchr$(18)"a"chr$(146)"lte oder "chr$(18)"n"chr$(146)"eue funktion ?"
   60 geta$:ifa$<>"a"anda$<>"n"then60
   65 ifa$="a"then95
   70 s=0:z=22:gosub20:print"c(a,b)=";:inputl$:poke781,22:sys59903
   75 gosub20:print"c="l$:fori=0to500:next:poke646,0
   80 printchr$(147)chr$(17):print"100l$="chr$(34)l$chr$(34)
   85 print"110deffnl(a)="l$:print"120deffnm(b)="l$:print"run95"chr$(19);
   90 poke631,13:poke632,13:poke633,13:poke634,13:poke198,4:end
   94 rem **** hp-2: berechnungen ****
   95 poke646,5:printchr$(147)
  100 l$="not(a and(not b))"
  110 deffnl(a)=not(a and(not b))
  120 deffnm(b)=not(a and(not b))
  130 ifk=1thenreturn
  140 s=5:z=5:gosub20:poke646,2:print"hier ist die wahrheitstabelle:"
  145 print:print:print:poke646,1:printtab(5)"a"tab(10)"b"tab(25)"ergibt c"
  150 printtab(4)"--------------------------------":poke646,5:print
  155 fora=-1to0:forb=-1to0:printtab(5)atab(10)btab(27)fnm(b)
  160 nextb:nexta
  165 print:poke646,1:printtab(4)"--------------------------------":poke646,5
  170 s=0:z=20:gosub20:print"c="l$

