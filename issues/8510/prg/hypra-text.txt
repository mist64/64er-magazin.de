

;hypra-text.prg ==0801==
;inkl. Fehlerteufelchen 11/1985
    0 goto10
    1 ifpeek(198)>0thengeta$:return
    2 gosub30600:goto1
    3 print"{down}{down}{down}{down} + Bitte warten + Ich  a r b e i t e  + ":return
    4 poke908,0:poke918,30:poke920,11:poke917,36:poke924,0:a$="":return
    5 sys50176,a$,a$:f=peek(913):return
    9 goto30001
   10 goto 22000
   11 gosub31000:poke650,128:poke2,184
   12 gosub40000
   14 print:l$=a6$
   15 kl=1:printchr$(14)
   17 dima$(600):an=0:max=600
   26 print"{clr}";pt$;"{down}"
   27 goto100
   30 a$=left$(a$,len(a$)-1):return
   31 ifright$(a$,1)=" "thengosub30:goto31
   32 return
   40 a$=left$(a$(t),len(a$(t))-1):return
   70 rem ** unterprogramm **
   71 print:print"Geben sie ein,von welcher Zeilennr      (0=Anfang)"
   72 inputv:ifv=0thenv=1
   73 print"Geben sie ein,bis zu welcher Zeilennr   (0=Ende)"
   74 inputb:ifb>maxthenprint"Zu gross !":goto74
   75 ifb=0thenb=an
   79 return
   80 print"{home}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}":return
   90 printleft$(a6$,mb):return
   95 gosub80:fori=1to4:gosub90:next:gosub80:return
  100 print"{clr}";pt$;:close2:close15:close5:poke198,0:ba=0:dr=0:br=0:sp=0
  101 ifan/max>0.7thenb1$="{rvon}"+str$(int((an/max)*100))+" % VOLL !!!{rvof}":x=fre(9)
  102 ifan/max>0.7thenb2$="{rvon}[5] = Speichern{rvof}":b3$="{rvon}ACHTUNG{rvof}":sp=1
  103 v$(50)=d$:v$(47)=ba$:v$(46)=str$(rr):v$(45)=str$(rr+rr*(mw/100))
  104 v$(44)=str$(rr*(mw/100))
  105 poke54296,0:ifpeek(1022)=200thenam=2
  107 print"{home}";pt$;
  109 poke53265,27
  110 print"{down}  Datum: ";d$
  111 gosub300:print"{home}{down}{down}{down}"
  112 print" {SHIFT--} {rvon}System    {rvof} :";b1$
  113 print" {SHIFT--} {rvon}Empfehlung{rvof} :";b2$
  114 print" {SHIFT--} {rvon}          {rvof} :";b3$
  115 print"{down} {SHIFT--} Text name  :";t1$
  116 print" {SHIFT--} Platz      :";max-an;"Zeilen frei"
  120 rem ** menu **
  125 print"{down}{down}{rght}{rght}{rght}-1-  Text erstellen"
  130 print"{rght}{rght}{rght}-2-  Text aendern"
  132 print"{rght}{rght}{rght}-3-  Profiprint laden"
  134 print"{rght}{rght}{rght}-4-  Bildschirmausgabe"
  135 print"{rght}{rght}{rght}-5-  Arbeiten mit Diskette "
  136 print"{rght}{rght}{rght}-6-  ";fu$
  137 print"{rght}{rght}{rght}-7-  Arbeiten mit Variablen"
  145 print"{down}{down}{rght}{rght}Die Funktionstasten"
  147 print"{rght}{rght}f1= Eingabe f3=Bildschirmausgabe"
  148 print"{rght}{rght}f5= Ende   ";
  149 ifsp=1thengosub30601
  ;vor Fehlerteufelchen 11/1985
  ;150 geta$:ifa$=""then105
  150 geta$:ifa$=""then150
  154 ifa$="{f3}"then7000
  155 ifa$="{f1}"then1015
  156 ifa$="{f5}"then8000
  197 ifval(a$)<1orval(a$)>8then150
  199 x=val(a$):onxgoto1000,2000,56100,7100,5000,20000,9700
  200 goto 100
  260 return
  300 rem ** bs aufbau **
  301 ifba=1thenreturn
  310 a1$=" {CBM-A}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{CBM-S}"
  320 a2$=" {CBM-Q}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{CBM-W}"
  330 a3$=" {CBM-Z}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{CBM-X}"
  340 a4$=" {SHIFT--}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{SHIFT--}"
  350 print"{home}{down}{down}":printa1$:printa4$:printa4$:printa4$:printa2$
  360 printa4$:printa4$:printa2$
  370 fort=1to9:printa4$:next:printa2$
  380 printa4$:printa4$:printa4$:printa3$;
  499 ba=1:return
 1000 print"{clr}";pt$
 1001 print"{home}{down}{down}{rvon} * * Textprogramm     * *  Eingabe  * * {down}{rvof}{down}{down}";:ta=0:ta$=""
 1003 print"{rvon}=1=  Eingabe des Textes"
 1004 print"=2= Eingabe des Textnames"
 1005 print"=3= Eingabe ueber Maske"
 1007 print"=4= Text loeschen"
 1008 print"=5= Den Text ausfuellen":print"=6= Eingabe Tabulatorzeile"
 1009 gosub1
 1010 ifa$="0"ora$="{f1}"then100
 1011 b1$="Aendern [2]":b2$="Speichern [5]":t=val(a$)
 1013 ift=0then1009
 1014 ontgoto1015,1850,33000,1300,9400,1830,100
 1015 print"{clr}";pt$:print"{home}{down}{down}{rvon} * *  Textprogramm    * *  Eingabe  * * {down}{rvof}";:ta=0
 1016 b1$="Aendern   [2]"
 1017 b2$="Speichern [5]"
 1018 b3$="Ansehen   [f3]"
 1020 a=an:goto2014
 1040 ifk1$="{rvon}TASTEN{rvof} f7=HELP           f5=Zeile hoch"then1050
 1046 k1$="{rvon}TASTEN{rvof} f7=HELP           f5=Zeile hoch"
 1047 k2$="Home = Tabulator    RETURN=Zeile runter"
 1048 k3$="crsr hoch = Wort vor    :crsr => rechts"
 1049 k4$="crsr runter=Wort zurueck:crsr <= links":return
 1050 k1$="{rvon}Funktionst.{rvof} f1=Ende : f2=Einfuegen"
 1051 k2$="f3= Zeilen Loeschen : f4=Block copieren"
 1052 k3$="f5=Zeile hoch       : f6=Seite +/-"
 1053 k4$="{rvon}f7=HELP   {rvof}          : f8=Block laden":return
 1100 fori=g8tomax
 1110 gosub1400
 1120 iflen(a$(i+1))<m8thenreturn
 1130 next:return
 1300 gosub1301:goto1000
 1301 print"{clr}";pt$;"{down}{rvon}          Text loeschen                 "
 1302 print"{down}Bist du sicher? [j/(n)] ":gosub1:ifa$<>"j"then100
 1305 print"{down}{down}":fort=1tomax:print"{up}";t:a$(t)="":next
 1310 an=0:return
 1400 b=len(a$(i)):fora=m8to1step-1
 1410 ifmid$(a$(i),a,1)=" "ormid$(a$(i),a,1)="{$a0}"then1430
 1420 next:print"Zeile zu lang:fehler":return
 1430 a$(i+1)=right$(a$(i),b-a)+" "+a$(i+1):a$(i)=left$(a$(i),a)
 1450 return
 1460 print"{clr}":fort=0toan:printa$(t):next:print"{up}{up}"
 1470 return
 1830 print"{clr}";pt$
 1831 input"Maximale Zeilelaenge (39)";m8:gosub1832:goto1000
 1832 m8=m8+2:ifm8<10thenm8=39
 1833 print"{clr}";pt$:print"Eingabe Tabulatorzeile":print"!=Start +=Tab.Stop -=Nicht
 1834 t7$=left$(t7$,len(t7$)-1)
 1835 a$=left$(t7$,m8):gosub9:t7$=a$
 1840 m8=m8-2:t7$=left$(t7$+"----------------------------------------",m8-1)
 1841 fort=1tolen(t7$):ifmid$(t7$,t,1)="!"thencb=t
 1842 next:t7$=t7$+"!":poke917,m8
 1845 return
 1850 print"{clr}{down}{down}{down}{down}Textname":a$=t1$:gosub9:t1$=a$:goto1000
 2000 print"{clr}";pt$
 2001 print"{home}{down}{down}{rvon} + + Textprogramm     + +  Aendern  + + {down}{rvof}";:print"{down}{rvon}Aendern [1]{rvof}"
 2002 print"{down}{down}  >0< Menu":print"{down}  >1< Text aendern"
 2003 print"{down}  >2< Wort tausch"
 2005 print"{down}  >3< Text loeschen{down}{down}"
 2006 gosub1
 2008 ifa$="0"ora$="{f1}"then100
 2009 b1$="Speichern [5]":b2$="Ansehen   [f3]":b3$="Hilfe     [f7]"
 2010 t=val(a$):ift<1ort>3then2006
 2011 ontgoto2012,2900,1300
 2012 gosub80:print" 0 = Ende : 1 = Anfang"
 2013 input"{down}Ab Zeile";a:a=a-1:ifa=-1thena=an
 2014 gosub1046
 2020 a=a+1:ifa>anthenan=a
 2030 ifa<1thena=1
 2031 y=t:a$=a$(a)
 2040 ee=1:a$=a$(a):g8=a:gosub30000
 2041 ifu9=1thena$(a+1)=left$(a6$,cb-1)+r$+" "+a$(a+1):u9=2
 2050 a$(a)=a$:iflen(a$(a))>m8then2040
 2060 ifa$(a)=">"thena$(a)=""
 2061 pp=a:iff=3then2600
 2062 iff=7thengosub1040:goto2030
 2063 ifin=1andf<>0thenin=0:gosub2560:iff=1thenf=0
 2064 iff=1thenee=0:goto100
 2065 iff=6then2200
 2070 ifin=1thengosub2350
 2071 iff=2then2500
 2072 iff=4then2800
 2073 iff=8then2100
 2075 iff=5thena=a-1:goto2030
 2080 a$=a$(a):y=t:goto2020
 2100 print"{home}{down}{down}{rvon} * * Textprogramm    * * Block laden * *"
 2110 gosub95:gosub4:poke920,22
 2120 print"Name des Blockes":gosub5:iff=1then2020
 2121 a$=">"+ba$+"??????"+a$
 2125 close2:close5:open2,fa,15:open5,fa,2,a$+",s,r":gosub34000:ifen<>0then2100
 2130 input#5,z:gosub2530
 2140 fort=1toz:gosub34100:gosub34000:ifa$=">"thena$=""
 2150 a$(pp+t)=a$:next:close2:close5:a=a+z:gosub2560:goto2020
 2200 gosub95
 2201 print"f5= Seite + "
 2202 print"f6= Seite -    "
 2203 print"{down}Bitte waehlen"
 2210 gosub1:ifa$="{f1}"thena=a-1:goto2020
 2220 ifa$="{f5}"thena=a+10:goto2020
 2230 ifa$="{f6}"thena=a-12:goto2020
 2240 goto2210
 2300 z=1:gosub2530
 2320 a$(a+1)=right$(a$(a),len(a$(a))-peek(925)):cc=999:in=1
 2330 a$(a)=left$(a$(a),peek(925))
 2340 ifleft$(a$(a+1),1)=" "thena$(a+1)=right$(a$(a+1),len(a$(a+1))-1):goto2340
 2345 a$(a)=a$(a)+"{$a0}":a=a-1:goto2360
 2350 z=1:gosub2530
 2360 print"{home}{down}{down}{rvon}!!! Textprogramm !!! Aufschiebemodus !!!":return
 2500 print"{home}{down}{down}{rvon} * * Textprogramm    * * Aufschieben * *":gosub2510:goto2020
 2510 gosub95
 2511 print"f3 = X - Zeilen  f5 = bis [f1]"
 2512 gosub1:ifa$="{f1}"then2560
 2513 ifa$="{f5}"then2300
 2520 print"Wieviele Zeilen":inputz:pp=pp-1:a=a-1
 2530 foro=an+ztopp+zstep-1
 2540 a$(o)=a$(o-z):next
 2550 foro=pp+1topp+z:a$(o)="":next:an=an+z
 2560 print"{home}{down}{down}{rvon} + Textprogramm + Eingabe / Aendern +  ":return
 2600 print"{home}{down}{down}{rvon} * * Textprogramm    * * Loeschen   * * "
 2610 gosub95
 2620 print"Wieviele Zeilen":inputz
 2630 foro=pptoan+z
 2640 a$(o)=a$(o+z):next
 2650 foro=an-ztoan:a$(o)="":next:an=an-z:gosub2560:a=a-1:goto2020
 2800 print"{home}{down}{down}{rvon} * * Textprogramm    * * Copieren    * *":z=0:z1=0
 2810 gosub95:print"Markieren sie das untere Ende mit [f3]"
 2820 print"[f1] = Ende   :  [f3] = Markieren"
 2825 print"[f5] = hoch   :  [f7] = runter"
 2829 z=1
 2830 ifz+pp<1orz+pp>maxthenz=1
 2831 g9=pp+z-6:gosub30705:poke781,11:poke782,0:sys58636
 2832 print"{rvon}";a$(pp+z);"{rvof}"
 2833 gosub1:ifa$="{f1}"then2030
 2834 ifa$="{f5}"thenz=z-1
 2835 ifa$="{f7}"thenz=z+1
 2836 ifa$="{f3}"then2850
 2840 goto2830
 2850 ifz1=0thenz1=z:gosub80:print"Markieren sie wohin der Block soll    ":goto2830
 2860 z2=pp+z-1
 2881 g9=pp+z-6:gosub30705:poke781,11:poke782,0:sys58636
 2886 fort=pptopp+z1:a$(((t+1)-pp)+z2)=a$(t):next
 2887 ifan<((t+1)-pp)+z2thenan=((t+1)-pp)+z2
 2890 goto7000
 2900 print"{clr}{down}{down}{down}Nach welchem Wort soll gesucht werden":a$=""
 2901 gosub9:c$=a$:iff<>0then100
 2902 print"{clr}{down}{down}{down}Welches Wort soll dafuer eingesetzt     werden":gosub9:b$=a$
 2903 print"{clr}";pt$;"{down}     (j) = Aendern  (n)=Lassen{down}{down}{down}"
 2910 l1=len(c$):l2=len(b$):l3=l2-l1
 2920 fort=1toan+1:x=0:g=1:cc=0:gosub30400
 2921 ifcc=999thengosub2960:cc=0
 2925 x=len(a$(t)):ifx=0thennext:goto100
 2930 forg=1tox
 2935 ifmid$(a$(t),g,l1)<>c$thennext:next:goto100
 2939 print"{home}{down}{down}{down}{down}{down}{down}";a6$;a6$;a6$
 2940 print"{home}{down}{down}{down}{down}{down}{down}";right$("   "+str$(t),3);" ";left$(a$(t),g-1)+"{rvon}"+c$+"{rvof}";
 2941 printright$(a$(t),len(a$(t))-g-len(c$)+1)
 2942 gosub1:ifa$="n"then2950
 2945 a$=left$(a$(t),g-1)+b$+right$(a$(t),x-(g+l1)+1):x=x+l3:gosub30400:a$(t)=a$
 2946 ifcc=999thengosub2960                           
 2950 next:next:goto100
 2960 a$(t)=a$:a$(t+1)=r$+" "+a$(t+1):iflen(a$(t+1))<m8thenreturn
 2970 z=1:pp=t+1:gosub2530:a$=a$(t+1):gosub30400:a$(t+1)=a$:a$(t+2)=r$:return
 4000 print"{clr}";pt$
 4001 print"{rvon} + Variablen aus der Dateiverwaltung  + "
 4002 print"{down}{down}-1- Nach Nummer"
 4011 print"{down}-2- Nach Namen"
 4020 gosub1:ifa$="{f1}"then100
 4030 ifa$="1"theninput"{down}Nummer";y:goto4499
 4040 print"{clr}";pt$:print"{down}{down}{down}Name":gosub4:gosub5:b$=a$:iff=1then100
 4050 gosub4100:iff<>999then4499
 4060 goto4230
 4100 close2:open2,fa,15:open5,fa,2,"ad,s,r":gosub34000:ifer<>0then100
 4110 gosub34100:input#5,a
 4120 ifleft$(a$,len(b$))=b$thenclose2:close5:y=a:return
 4150 if(st and 64)=64thenclose2:close5:goto4200
 4160 goto4110
 4200 f=999:print"{clr}";pt$
 4210 print"{down}{down}Datensatz ";b$
 4220 print"nicht vorhanden.":return
 4230 print"{down}{down}Bitte [f1] druecken":gosub1:goto100
 4499 gosub4500:goto4560
 4500 close2:close5
 4501 hb=int(y/256):lb=y-hb*256
 4510 open2,fa,15:open5,fa,2,"asalem,l,"+chr$(254)
 4520 print#2,"p"+chr$(2)+chr$(lb)+chr$(hb)+chr$(1)
 4522 input#2,en
 4525 ifen<>0thenprint"{rvon}ERROR Falsche Diskette !!!":return
 4530 b=20:gosub4700:gosub4750:v$(1)=b$
 4531 b=20:gosub4700:gosub4750:v$(2)=b$
 4532 b=20:gosub4700:gosub4750:v$(3)=b$
 4533 b=4:gosub4700:v$(4)=b$
 4534 b=20:gosub4700:gosub4750:v$(4)=v$(4)+" "+b$
 4536 b=16:gosub4700:v$(5)=b$
 4537 b=2:gosub4700:v$(6)=b$+".":gosub4700:v$(6)=v$(6)+b$+".":b=4:gosub4700
 4538 v$(6)=v$(6)+b$:b=10:gosub4700:v$(7)=b$
 4539 b=2:gosub4700:v$(8)=b$
 4542 b=10:gosub4700:v$(9)=b$
 4544 b=10:gosub4700:v$(10)=b$
 4546 b=20:gosub4700:gosub4750:v$(11)=b$
 4548 b=20:gosub4700:v$(12)=b$
 4549 b=20:gosub4700:gosub4750:v$(13)=b$
 4550 b=20:gosub4700:gosub4750:v$(14)=b$
 4555 close2:close5:return
 4560 print"{clr}";pt$:print"Datensatz ";v$(2)
 4561 ifba$<>v$(8)andpr=0thenv$(7)="passwort geschuetzt"
 4565 print:fort=1to14:printv$(t):next:print"{down}Bitte [f1] druecken":gosub1:goto100
 4599 goto100
 4700 poke917,b-1:poke915,5:sys50182,b$
 4740 ifright$(b$,1)=" "thenb$=left$(b$,len(b$)-1):goto4740
 4749 return
 4750 a$=left$(b$,1):ifasc(a$)<65orasc(a$)>90thenreturn
 4760 b$=right$(b$,len(b$)-1):b$=chr$(asc(a$)+32)+b$:return
 5000 goto5500
 5001 print"{clr}";pt$:print"{down}{down}Laden von Diskette"
 5010 print"{down}{down}   -1- Text loeschen und neuen                 Text laden"
 5020 print"{down}   -2- Hinter alten Text laden"
 5021 print"{down}   -3- Listings laden"
 5025 print"{down}Befehl";
 5030 gosub1
 5040 ifa$="{f1}"then100
 5041 b1$="Ansehen [f3]":b2$="Aendern [2]"
 5042 ifa$="1"ora$="3"thengosub1305:goto5050
 5043 ifa$="2"then5050
 5045 goto5030
 5050 print"-";a$;"-":i=0:ifa$="3"theni=999
 5060 print"{clr}{down}{down}{down}{down}{down}Name der Datei":a$="":gosub9:t1$=a$:b$=a$:iff=1then100
 5061 ifleft$(a$,1)<>"@"then5066
 5062 a$=right$(a$,len(a$)-1):t=val(a$):a$=v$(30+t)
 5063 ift>borb=0then5060
 5064 goto5100
 5066 print"{home}{down}{down}{down}{down}{down}Aufzeichnungsdatum:":a$="??????":gosub9
 5070 ifpr<>1thena$=">"+ba$+a$+b$
 5071 ifpr=1thena$=b$
 5100 rem
 5102 open2,fa,15,"i0"
 5103 gosub34000
 5104 open5,fa,5,"0:"+a$+",s,r"
 5105 ax=0:gosub34000:ifen=62then100
 5106 ifi=999then5200
 5107 input#5,ax:fort=1toax
 5110 rem
 5111 gosub34100:a$(t+an)=a$:ifa$(t+an)=">"thena$(t+an)=" "
 5112 gosub34000
 5113 geta$:ifa$="{f1}"thenclose2:close5:goto100
 5114 next
 5115 an=an+ax
 5120 close2:close5:goto100
 5200 rem
 5210 an=an+1
 5211 geta$:ifa$="{f1}"thenclose2:close5:goto100
 5220 gosub34100:a$(an)=a$:ifst<>64then5210
 5230 close2:close5:goto100
 5400 print"{clr}";pt$:print"{down}{down}Vergleichen von Diskette"
 5401 a$=t1$:print"Name":gosub9:iff=1then100
 5402 open2,fa,15,"i0":gosub70
 5403 gosub34000:ifpr<>1thena$=">"+ba$+"??????"+a$
 5404 open5,fa,5,"0:"+a$+",s,r"
 5405 gosub34000:ifen<>0then100
 5407 input#5,ax:ifb<>0andax<>b-vthenprint"Zeilenanzahl falsch"
 5408 fort=1toax
 5410 gosub34100:ifa$=">"thena$=" "
 5412 printa$:poke198,0:wait198,1:geta$:ifa$="{f1}"then5420
 5413 gosub34000
 5415 next
 5420 close2:close5:print"Taste druecken":gosub1:goto100
 5500 print"{clr}";pt$
 5501 print"{rvon} + Menu Disketten-Operationen         + "
 5502 print"{down} laden     [4]"
 5503 print" speichern [5]"
 5510 rem
 5520 print"{down}  -0- Menu"
 5521 print"  -1- Text vergleichen"
 5525 print"  -2- Text umbenennen"
 5527 print"  -3- Text loeschen"
 5529 print"  -4- Text laden"
 5530 print"  -5- Text speichern"
 5532 print"  -6- Inhaltsverzeichnis"
 5534 ifpr=1thenprint"  -7- Disk formatieren"
 5536 ifpr=1thenprint"  -8- Disk Name + ID aendern"
 5550 gosub1
 5551 ifa$="0"ora$="{f1}"then100
 5552 ifval(a$)=0then5500
 5553 onval(a$)goto5400,5650,5700,5001,6000,5800,5600,5900
 5555 return
 5600 print"{clr}";pt$:pa$="":ifpr=0then100
 5610 print"{rvon} D i s k e t t e  F o r m a t i e r e n "
 5630 a$="Disketten Name":gosub9:b$=a$:iff=1then100
 5631 print"{home}{down}{down}{down}{down}Schuetzen?":a$="n":gosub9:ifa$="j"theninput"Passwort";pa$
 5633 ifa$="j"andlen(pa$)<10thenpa$=pa$+chr$(0):goto5633
 5634 a$="id"
 5635 print"{home}{down}{down}{down}{down}Disketten ID  ":gosub9:id$=a$:a$=left$(b$+"................",16)
 5640 id$=left$(id$+"te",2)
 5641 gosub3
 5642 open2,fa,15,"n:"+a$+","+id$:close2:ifpa$=""thengoto100
 5643 open15,fa,15,pr$:open2,fa,2,"#"
 5644 print#15,"b-r 2 0 18 0":print#15,"b-p 2 164"
 5645 print#2,chr$(0)+chr$(0)+chr$(0):print#15,"b-p 2 180":print#2,pa$
 5646 print#15,"b-p 2 2":print#2,"o";
 5647 print#15,"b-w 2 0 18 0":close2:close15:goto100
 5650 print"{clr}";pt$:print"{rvon}  +  T e x t   u m b e n e n n e n  +   "
 5651 print"{home}{down}{down}{down}{down}Alter Name":a$="":gosub9:iff=1then100
 5652 b$=a$:a$="ttmmjj":print"{home}{down}{down}{down}{down}Datum":gosub9
 5655 c$=a$:print"{home}{down}{down}{down}{down}Neuer Name":gosub9
 5656 ifpr=1thengosub3:a$=">"+a$:b$=">"+b$
 5657 ifpr=0thengosub3:a$=">"+ba$+da$+a$:b$=">"+ba$+c$+b$
 5660 open2,8,15,pr$
 5670 print#2,"r:"+a$+"="+b$:gosub34000:close2:goto100
 5700 print"{clr}";pt$:print"{rvon}    +  T e x t   l o e s c h e n  +     "
 5715 print"{home}{down}{down}{down}{down}Name":gosub9:b$=a$:iff=1then100
 5716 a$="??????":print"{home}{down}{down}{down}{down}Datum      ":gosub9:gosub3
 5717 ifpr<>1thena$=">"+ba$+a$+b$
 5718 ifpr=1thena$=">"+b$
 5719 open2,fa,15,pr$
 5720 print#2,"s0:"+a$:close2:goto100
 5800 close15:close2:open15,fa,15,"i":print"{clr}";pt$
 5801 print"{clr}";pt$:print"{rvon}  + Inhaltsverzeichnis der Diskette  +  {down}{down}{down}"
 5810 open2,fa,2,"#"
 5811 print#15,"b-r 2 0 18 0":print#15,"b-p 2 165"
 5812 get#2,a$:ifa$<>""then5816
 5813 print#15,"b-p 2 180":pa$="":fort=1to10:get#2,a$:pa$=pa$+a$:next
 5814 print"Die Diskette ist geschuetzt: Geben Sie  das Passwort ein"
 5815 inputa$:ifa$<>pa$thenclose2:close15:goto100
 5816 b$="":x=999:gosub5910:x=0:b$="{f3}"
 5817 ifa$(2)="inhalt"thenan=an+2:a$(an)=id$+" "+n$:an=an+1
 5820 t=18:s=1:b=0
 5821 gosub5898
 5830 print#15,"u1 2 0";t;s
 5840 v=0
 5841 get#2,x$:x$=x$+chr$(0):t=asc(x$)
 5842 get#2,x$:x$=x$+chr$(0):s=asc(x$)
 5845 forx=0to7:print#15,"b-p 2";x*32+2
 5846 get#2,x$,a$:z=asc(x$+chr$(0)):ifz<>8*16+1andpr=0 then5855
 5847 gosub5890
 5848 ifb=10thenb=0:print"{down}Bitte f3 druecken{down}":wait198,1:getb$:gosub5898
 5849 ifb$="{f1}"then100
 5850 x$="{wht}seq ":ifz<>0andpr=1thengosub5870
 5851 ifpr=0andz<>0thenprint"{wht}   ";b,"{left}";ff$:v$(30+b)=c$
 5852 ifpr=1thenprint"{wht}  ";x$,ff$
 5855 nextx
 5856 ift<>0then5830
 5860 print"{down}{down}Bitte f1 druecken":gosub1
 5865 goto100
 5870 ifz=130thenx$="prg "
 5871 ifz=129thenx$="seq "
 5872 ifz=194thenx$="prg<"
 5873 ifz=131thenx$="usr "
 5874 ifz=193thenx$="seq<"
 5875 ifz=195thenx$="usr<"
 5880 ifa$(1)="p"andz<>130andz<>194thenreturn
 5881 ifa$(2)<>"inhalt"thenreturn
 5885 an=an+1:a$(an)=id$+" "+x$+ff$:return
 5888 goto100
 5890 ifpr=1then5897
 5891 get#2,a$,a$:ifa$<>">"thenz=0:return
 5892 get#2,a$,b$:ff$=a$+b$:ifff$<>ba$andba$<>"--"thenz=0:return
 5893 c$=">"+a$+b$:get#2,a$,b$:ff$=ff$+"  "+a$+b$+".":c$=c$+a$+b$
 5894 get#2,a$,b$:ff$=ff$+a$+b$+".":c$=c$+a$+b$
 5895 get#2,a$,b$:ff$=ff$+a$+b$+"  {$a0}":c$=c$+a$+b$
 5896 fory=1to7:get#2,a$:c$=c$+a$:ff$=ff$+a$:nexty:b=b+1:return
 5897 ff$="":get#2,a$:fory=0to15:get#2,a$:ff$=ff$+a$:next:b=b+1:return
 5898 print"{clr}";pt$:print"Disk: ";n$;"  ID:";id$;"{down}"
 5899 print"    Nr.  Wer  Datum     Name{down}":return
 5900 ifpr=0then100
 5901 print"{clr}";pt$:print"{rvon}Aendern des Disknamens  oder der Disk ID"
 5902 print"{down}Die zu Aendernde Diskette einlegen und":print"  [f3]  druecken"
 5903 poke198,0:gosub1:ifa$<>"{f3}"then100
 5908 b$=""
 5909 open15,8,15,"i":print#15,pr$:open2,8,2,"#"
 5910 print#15,"b-r 2 0 18 0":print#15,"b-p 2 144":fort=1to20:get#2,a$:b$=b$+a$:next
 5920 n$=left$(b$,16):id$=right$(b$,2)
 5921 ifx=999thenreturn
 5925 print"Name  ";n$:input"{up}Name";n$
 5927 print"ID    ";id$:input"{up}ID  ";id$
 5930 n$=n$+"{$a0}{$a0}{$a0}{$a0}{$a0}{$a0}{$a0}{$a0}{$a0}{$a0}{$a0}{$a0}{$a0}{$a0}{$a0}{$a0}":id$=id$+"++":n$=left$(n$,16):id$=left$(id$,2)
 5940 print#15,"b-p 2 144":print#2,n$;id$;:print#15,"b-w 2 0 18 0"
 5945 close2:close15
 5950 input"Passwort";pa$:ifpa$="0"then100
 5960 iflen(pa$)<10thenpa$=pa$+chr$(0):goto5960
 5970 goto5643
 6000 print"{clr}";pt$:print"{rvon}+ + T e x t    A u f z e i c h n e n + +"
 6001 print"{down}Textname: ";t1$
 6002 print"{down}{down} [f1]=Ende   [=] = Textname{down}"
 6004 a$="=":gosub9:iff=1then100
 6005 ifa$="="thena$=t1$:ift1$=""thenprint"Name fehlt":goto6004
 6006 t1$=a$:b$=">"+ba$+"??????"+a$
 6007 print"{down}Alle Vorgaenger loeschen (j/n)?":gosub1
 6008 ifa$="j"thenopen2,8,15,"s:"+b$:close2
 6009 b1$="Drucken [3]":b2$="Ende [f5]"
 6015 a$=t1$:gosub70
 6016 t1$=a$:open2,fa,15,"i0":a$=">"+ba$+da$+a$:print#2,pr$
 6017 gosub34000
 6018 open5,fa,5,"@0:"+a$+",s,w"
 6021 gosub34000
 6022 ax=b-v:print#5,str$(ax)
 6024 fort=vtob:ifa$(t)=""ora$(t)=" "thena$(t)=">"
 6030 print#5,a$(t):ifa$(t)=">"thena$(t)=""
 6031 gosub34000
 6032 next:close2:close5:goto100
 7000 t=0:x=0
 7002 ifx<0thenx=0
 7003 gosub7030:fort=1to20
 7004 print"{rvon}";right$(str$(t+x*20),1);"{rvof}";a$(t+x*20)
 7006 nextt
 7007 print"{down}f1=Ende   f3=Seite +   f5=Seite - "
 7008 b1$="Aendern   [2]"
 7009 b2$="Speichern [5]":b3$="Hilfe     [f7]"
 7010 gosub1
 7011 ifa$="{f1}"then100
 7012 ifa$="{f5}"thenx=x-1:ifx>=0then7002
 7013 ifa$="{f3}"thenx=x+1:ifx*20<anthen7002
 7014 ifx*20>=anthenx=0:goto7002
 7015 ifx<0thenx=int(an/20):goto7002
 7020 goto7010
 7030 print"{clr}{rvon} +T e x t  6 4 +Bildschirm+Zeile        "
 7031 print"{home}{rvon} +T e x t  6 4 +Bildschirm+Zeile";x*20;"-"
 7032 return
 7100 print"{clr}";pt$:print"{rvon} + +      Bildschirmausgabe         + + "
 7105 input"{down}{down}Ab Zeile";x:ifx<1orx>anthenx=1
 7106 x=int(x/20):goto7002
 8000 print"{clr}";pt$
 8002 g=0
 8006 print"{down} -1- Neustart des Programms"
 8007 print"{down} -2- Info ueber Speicherplatz"
 8008 print"{down} -3- Ton aus"
 8009 print"{down} -4- Ton an"
 8010 gosub1
 8011 ifa$="1"then10000
 8013 ifa$="0"ora$="{f1}"then100
 8014 ifa$="1"thenrun
;vor Fehlerteufelchen 11/1985
;8015 ifa$="2"thenprintfre(9);"Zeichen frei":print"{down} Taste druecken":gosub1:goto1
 8015 ifa$="2"thenprintfre(9);"Zeichen frei":print"{down} Taste druecken":gosub1:goto26
 8016 ifa$="{home}"theninput"code";a$:ifa$="susi"then55555
 8017 ifa$="{CTRL-P}"and(ba$="--"orba$="of")thenpr=1:print"{down}{down}{down}Prog ok":gosub1:goto100
 8018 ifa$="{CTRL-O}"thenpr=0:print"{down}{down}{down}Programmierer aus":gosub1:goto100
 8019 ifa$="3"thent9%=1:print"{down}Ton aus":goto100
 8020 ifa$="4"thent9%=0
 8021 ifa$="{wht}"andpr=1thenend
 8099 goto100
 9400 rem*** fill ***
 9401 print"{clr}";pt$:print"{home}{down}{down}{rvon} * * Textprogramm * * Ausfuellen  * *   "
 9407 f2=0:print"Neben den Text [j/n]":gosub1:ifa$="j"thenf2=1
 9408 ifa$="{f1}"then100
 9409 iff2=1thenprint"Links/Rechts [l/r]":gosub1:g=1:ifa$="r"theng=2
 9410 gosub70
 9421 input"Schrittweite";s:ifs<=0then9421
 9429 print"{clr}{down}{down}{down}{down}Was ?"
 9430 a$="":gosub9:iff=1then100
 9440 ifb>anthenan=b
 9450 fort=vtobsteps
 9452 iff2=0thena$(t)=a$
 9453 iff2=1andg=1thena$(t)=a$+a$(t)
 9454 iff2=1andg=2thena$(t)=a$(t)+a$
 9455 next:goto7000
 9689 return
 9700 print"{clr}{rvon}";pt$;"{down}"
 9710 print"{down}  1  Variablen zeigen"
 9712 print"{down}  2  Variablen eingeben"
 9713 print"{down}  3  Variablen an"
 9714 print"{down}  4  Variablen aus"
 9715 print"{down}  5  Variablen zu Text"
 9717 print"{down}  6  Variablen als Text 1-50"
 9718 print"{down}  7  Text als Variablen 1-50"
 9719 print"{down}  8  Variablen aus Dateiverwaltung"
 9750 gosub1
 9755 ifa$="0"ora$="{f1}"then100
 9760 onval(a$)goto9800,9900,9780,9785,9850,9790,9795,4000
 9770 goto9750
 9780 v9=1:print"Variablen an":fort=1to999:next:goto9700
 9785 v9=0:print"Variablen aus":fort=1to999:next:goto100
 9790 fort=1to50:a$(t)=v$(t):an=50:next:goto100
 9795 fort=1to50:v$(t)=a$(t):next:goto100
 9800 print"{clr}";pt$
 9801 fort=1to50:printt,v$(t)
 9810 ift/20=int(t/20)thengosub1:print"{clr}";pt$
 9820 next:gosub1:goto100
 9850 gosub3:v9=1
 9860 fort=1toan:a$=a$(t):gosub30300:a$(t)=a$:next:v9=0:goto100
 9900 print"{clr}";pt$:print"{down}      + + Variablen EINGABE + +{down}{down}{down}{down}"
 9901 input"Variablen Nr.";a:ifa>40then9900
 9910 a$="":gosub9:v$(a)=a$
 9920 print:print"Noch eine? [j/n]"
 9930 gosub1
 9940 ifa$="n"then100
 9950 goto9900
10000 print"{clr}";pt$:b$=a$
10010 print"{down}{down}ACHTUNG !!! Text unbedingt SPEICHERN"
10020 print"{down}Ist der Text schon gespeichert? [j/n]"
10030 gosub1:ifa$<>"j"then6000
;vor Fehlerteufelchen 11/1985
;10040 a$=b$:poke930,0:goto8012
10040 a$=b$:poke930,0:goto8013
20000 print"{clr}";pt$:print"{down}{rvon} + + Textprogramm + + R E C H N E N  + +"
20009 print"{down} [e]= Ende"
20010 print"{down} [c]= Speicher loeschen"
20011 print"{down} [m]= Mehrwertsteuer eingeben"
20013 print"{down} [+] [-] [*] [:] = Rechenoperationen"
20015 rr=int((rr*100)+0.5)/100
20019 print"{down}{down}{rvon}";rr;"{rvof}";
20020 gosub1
20030 ifa$="c"thenrr=0:goto20000
20031 ifa$="e"ora$="{f1}"then100
20032 ifa$="m"theninput"Steuer in %   14{left}{left}{left}{left}";mw:goto20000
20034 ifa$<>"+"anda$<>"-"anda$<>"*"anda$<>":"then20020
20040 printa$;"  ";:inputx
20050 ifa$=":"andx=0thenprint"Fehler... nicht durch 0 dividieren":goto20020
20060 ifa$=":"thenrr=rr/x
20061 ifa$="*"thenrr=rr*x
20062 ifa$="+"thenrr=rr+x
20063 ifa$="-"thenrr=rr-x
20064 goto20000
22000 ifpeek(1022)=0thengosub22100
22001 ifpeek(1022)=0thenpoke1022,1:load"zeichen b",8,1
22002 ifpeek(1022)=250thenpoke1022,240:load"zeichen din",8,1
22003 ifpeek(1022)=1thenpoke53272,11:poke56576,peek(56576)and252:poke648,192
22004 ifpeek(1022)=240thenpoke53272,11:poke56576,peek(56576)and252:poke648,192
22005 ifpeek(1022)=1thenpoke1022,2:load"zeichen d",8,1
22006 print"{clr}":ifpeek(1022)=240thenpoke1022,199:load"zeichen ddin",8,1
22007 ifpeek(1022)=2thenpoke1022,3:load"profip v1.0",8,1
22008 ifpeek(1022)=199thenpoke1022,200:load"profip v1.0",8,1
22009 ifpeek(1022)=111thenpoke53272,11:poke56576,peek(56576)and252:poke648,192:end
22010 goto11
22100 printchr$(14);"{clr}{rvon} Textverarbeitungsprogramm      O.Fendt "
22105 print"{down}{down}Was fuer eine Tastaturbelegung?"
22106 input"{down}{down} [din] - [cbm]{rght}{rght}{rght}din{left}{left}{left}{left}{left}";a$
22107 ifa$="din"thenpoke1022,250
22108 gosub3:return
30000 g9=g8-6:gosub30700:xt=0:iflen(a$)>m8thena$=left$(a$,m8)
30001 poke922,am:cf=1:ifu9=2thenu9=0:cp=len(r$)+cb:goto30020
30002 ifcc=999thencp=len(a$(g8)):cc=0:goto30020
30010 cc=0:x=0:cp=cb
30020 ifcp=0thencp=1
30021 poke908,cp-1:poke918,m8-(m8/5):poke920,11:poke917,m8:poke924,0
30040 sys 50176,a$,a$ :f=peek(913):ifa$="> "thena$=""
30045 gosub31
30053 iff=9thengosub30550:goto30020
30054 iff=15thena$="":a$(g8)="":goto30000
30055 iff=11thengosub30400:a$(g8)=a$
30056 iff=20thencc=999:f=5
30057 iff=16thenf=0:ifright$(a$,1)<>"{CBM-F}"thena$=a$+"{CBM-F}"
30099 gosub30300:gosub30400:print:return
30300 rem ** variablen test **
30301 ifv9=0thenreturn
30310 forb=1tolen(a$)-2
30320 ifmid$(a$,b,1)<>"@"thennext:return
;vor Fehlerteufelchen 11/1985
;30330 x=val(mid$("00"+a$,b+1,2)):ifx>50thenx=0
30330 x=val(mid$(a$,b+1,2)):ifx>50thenx=0
30340 a$=left$(a$,b-1)+v$(x)+right$(a$,len(a$)-b-2):goto30310
30350 next:return
30400 iflen(a$)<m8thenreturn
30401 iflen(a$)>80then30440
30403 cc=999:fori=m8to1step-1
30410 ifmid$(a$,i,1)<>" "thennexti:goto30440
30420 r$=right$(a$,len(a$)-i):a$=left$(a$,i-1):u9=1
30421 ifright$(r$,1)=" "thenr$=left$(r$,len(r$)-1):goto30421
30430 iflen(a$)>=m8thennext:goto30440
30431 return
30440 gosub95:print"{down}>> Zeile zu lang << Fehler !!":poke198,0
30441 a$=left$(a$,m8-1)
30442 gosub30601:ifpeek(198)<1then30442
30443 poke198,0:return
30550 ifcp+1>len(t7$)orxt=1thenreturn
30560 x=0:forz=cp+1tolen(t7$):ifmid$(t7$,z,1)="+"andx=0thenx=1:cp=z
30570 next:ifx=0thenxt=1
30580 iflen(a$)<cpthena$=a$+"{$a0}":goto30580
30590 return
30600 rem ** error system **
30601 ift9%=1thenreturn
30602 poke53280,1
30603 poke54296,15:poke54277,25:poke54278,4*16+4
30610 poke54273,29:poke54272,69:poke54276,17
30619 fort=1to200:next
30620 poke53280,0:fort=1to200:next
30621 poke54276,0:poke54277,0:poke54278,0:return
30700 rem   ** fenster **
30701 print"{home}";pt$:print"{down}{down}";t7$
30702 gosub80:gosub30750
30705 print"{home}":fori=0to10:poke781,i+5:poke782,0:sys58636
30710 ifg9+i<=0thengosub90:goto30740
30720 v=mb-len(a$(i+g9)):ifv<0thenv=0
30730 printa$(i+g9)+left$(a6$,v)
30740 next:gosub90:return
30750 printk1$;left$(a6$,(mb+2)-len(k1$))
30751 printk2$;left$(a6$,(mb)-len(k2$))
30752 printk3$;left$(a6$,(mb)-len(k3$))
30753 printk4$;left$(a6$,(mb)-len(k4$)):return
31000 rem *****************************
31001 rem *****************************
31002 rem **     initialisierung     **
31003 rem *****************************
31004 rem *****************************
31006 pt$="{wht}{rvon}Hypra-text V1.0          by Oliver Fendt{rvof}":me=100:print"{clr}";pt$
31009 fu$="Rechnen":mw=14
31010 dimv$(50)
31016 poke53281,0:poke53280,0:ifm8<=7thenm8=36
31019 t7$="!----+----+----+----+----+----+----------"
31020 gosub46000:b1$=str$(m8)+" Zeichen Modus"
31048 a6$="                                                                   "
31060 b1$="Laden       [5]"
31061 b2$="Eingabe     [f1]"
31062 b3$="Hilfe       [f7]"
31063 gosub1840:pr$="m-w"+chr$(1)+chr$(1)+chr$(1)+"a"
31150 print"{clr}";pt$:print"{down}{down}Guten Tag,";v$(1)
31160 print"{down}Bitte geben Sie das Datum ein."
31199 return
33000 rem *** formblatt ***
33001 print"{clr}";pt$:print"{home}{down}{down} + M a s k e  ausfuellen  + Von Menu 1 +":a=cb
33004 b1$="Speichern [5]":b2$="Aendern [2]"
33010 forpq=1toan
33020 a$=a$(pq):a$(pq)=a$
33021 iflen(a$)=0thennext:goto33099
33022 forpx=1tolen(a$):l=0
33025 ifmid$(a$,px,1)="{CBM-@}"thencb=px:goto33050:ifcc=4andc8=0then100
33027 next:g9=pq-6:gosub30700:next:goto33099
33050 forpx=cbtolen(a$):ifmid$(a$,px,1)="{CBM-@}"thennext
33060 a$=left$(a$(pq),cb)+left$(a6$,px-cb-1)
33061 x=len(a$(pq))-px+1:ifx>0thena$=a$+right$(" "+a$(pq),x)
33062 a$(pq)=a$
33070 g8=pq:gosub30000:a$(pq)=a$:iff=1then100
33072 goto33022
33099 cb=a:goto100
33200 return
34000 rem *****************************
34001 rem *                           *
34002 rem *  floppy control           *
34003 rem *                           *
34010 rem *****************************
34020 input#2,en,em$,et,es:ifen=0thenreturn
34050 print"{home}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}";en;"  ";em$
34060 print"{down}{down}Taste druecken"
34070 gosub1
34080 ifen=62orxy=999thenreturn
34090 print"Kontrollieren und nach dem beheben      [f1] druecken"
34091 gosub1:ifa$<>"{f1}"then34091
34092 print#2,"i":goto100
34100 poke915,5:poke908,0:poke917,m8:sys50182,a$
34110 gosub31:return
34500 fort=20767+8500 to65000
34510 ifpeek(t)<>0thennext
34520 x=t
34530 printpeek(x)+peek(x+1)*256
34540 x=x+2
34550 ifpeek(x)=0thenx=x+3:print:goto34530
34560 printchr$(peek(x));:x=x+1:goto34550
40000 print"{home}{down}{down}{down}{down}{down}{down}{down}{down}Datum:":a$="------":gosub9:da$=a$
40010 ifda$="------"then40000
40020 d$=left$(da$,2)+"."+mid$(da$,3,2)+".19"+right$(da$,2):return
43000 rem
43040 am=1
43060 return
46000 print"{clr}";pt$;chr$(14):m8=39:cb=1:mb=39:t9%=1
46001 print"{down}Parameterdiskette einlegen{down}"
46002 ifpeek(930)<>0thenba$=chr$(peek(930))+chr$(peek(931)):fa=peek(932):goto46070
46003 print"Floppy  Adr.:  8":gosub1:fa=val(a$):ifa$=chr$(13)thenfa=8
46004 iffa<8orfa>9then46003
46005 t9%=1:print"{down}{down}Name         ":gosub4:gosub5:b$=a$:t=0:print
46006 ifval(b$)<>0theny=val(b$):gosub4500:goto46010
46007 gosub4100:iff<>999thengosub4500
46010 iff=999oren<>0thenprint"{down}Benutzer nicht registriert":gosub1:goto46000
46013 ifpeek(1022)=200thenam=2
46050 m8=39
46053 ifv$(7)="-"thenprint"Keine Zugriffsberechtigung":gosub1:goto46000
46055 ift>2thenprint:print"{wht}Passwort falsch":gosub1:goto46000
46056 print"{home}{down}{down}{down}{down}{down}{down}{down}{down}Codewort{blk}":gosub4:gosub5:print"{wht}":ifa$<>v$(7)thent=t+1:goto46055
46058 ba$=v$(8)
46060 poke930,asc(left$(ba$,1))
46061 poke931,asc(right$(ba$,1))
46062 poke932,fa
46070 xy=0:return
55555 close15:open15,8,15,"i":close15
55556 print"{clr}{down}{down}{down}{down}ich speichere": save"@:pt"+da$,8
55557 poke2,0
55560 print"{home}{down}{down}{down}{down}{down}{down}{down}ist gespeichert":end
56100 print"{clr}";pt$
56101 print"{down}{down} {rvon}ACHTUNG !!!{rvof}"
56102 print"{down} Nur wenn der Text schon gespeichert"
56103 print" ist, dann die Hypra-text Diskette"
56104 print" einlegen, und [f8] druecken !!!"
56106 gosub1:ifa$<>"{f8}"then100
56107 print"{clr}";pt$:print"{down}{down}Ich lade nun Profiprint"
56108 print"{down}  Bitte warten ..."
56109 poke2,0:load"profiprint",8

