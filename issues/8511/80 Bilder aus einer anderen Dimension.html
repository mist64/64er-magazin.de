<!DOCTYPE html>
<html lang="de">

<head>
    <title>Bilder aus einer anderen Dimension</title>
    <meta charset="UTF-8">
    <link rel="stylesheet" href="../style.css">
    <meta name="author" content="Gerhard Pehland, og">
    <meta name="64er.issue" content="11/85">
    <meta name="64er.pages" content="80-84">
    <meta name="64er.head1" content="Grafik">
    <meta name="64er.head2" content="C 64">
    <meta name="64er.toc_title" content="<b>Mathematische Kunst: Apfelmännchen — Bilder aus einer anderen Dimension</b>">
    <meta name="64er.toc_category" content="Listings zum Abtippen">
    <meta name="64er.index_title" content="Bilder aus einer anderen Dimension (Apfelmännchen)">
    <meta name="64er.index_category" content="Listings zum Abtippen|Grafik">
    <meta name="64er.id" content="apfelmännchen">
</head>

<body>
    <article>
        <h1>Bilder aus einer anderen Dimension</h1>
        <p class="intro">Kunst aus dem Computer ist auf Großrechnern im Kommen. Daß es auch mit dem C 64 geht, zeigt dieses Programm.</p>

        <p>Mit Hilfe einer einfachen Formel lassen sich die erstaunlichsten Grafiken erzeugen. Das Apfelmännchen, wie man diese Grafiken auch bezeichnet, wurde zuerst von dem Mathematiker Mandelbrot entdeckt. Das Apfelmännchen stellt einen Ausschnitt aus der Ebene der komplexen Zahlen dar. Die mathematischen Grundlagen dazu werden in Bild 1 erläutert.</p>

        <p class="noindent"><strong>Hinweise zum Abtippen.</strong> Tippen Sie das Maschinenprogramm (Listing 1) mit dem MSE ein und speichern Sie es unter dem Namen »APFELROUTINEN«. Dann geben Sie das Basic-Programm (Listing 2) mit dem Checksummer ein und speichern es ebenfalls. Die REM-Zahlen können weggelassen werden, da sie im Programm nicht angesprungen werden. Nach dem Starten des Basic-Programms wird das Maschinenprogramm automatisch nachgeladen. Außerdem werden die Zeiger für den Beginn der Variablen verändert. Wenn man eine Grafik geladen hat und erneut das Basic-Programm speichert, wird die Grafik daran angehängt.</p>

        <h3>Die Bedienung des Programms</h3>

        <p>Vom Menü aus lassen sich folgende Unterpunkte anwählen:</p>

        <ul class="plain">
            <li><strong>Neue Werte</strong>: Hier können Sie die Grenzen für Realteil (linker/rechter Rand) und Imaginärteil (oberer/unterer Rand) der Zahlen (siehe Bild 1) eingeben, die Sie darstellen wollen. Das Ausgangsbild aller Apfelmännchen finden Sie mit den Parametern: linker Rand —0.7, rechter Rand 2.1, unterer Rand —1, oberer Rand 1. Die maximale Tiefe bestimmt, nach wievielen Iterationen (Wiederholungen) abgebrochen wird. Es sind Werte von 5 bis 254 zugelassen. Je größer die Tiefe, desto mehr Details treten am Rand der Figur zutage. Beachten Sie aber, daß eine große Tiefe auch eine lange Rechenzeit bedeutet. Nach Eingabe der Tiefe werden Sie gefragt, ob die fertige Grafik auf Diskette gespeichert werden soll — zum Beispiel, wenn Sie den Computer über Nacht rechnen lassen. Falls Ihnen die Werte nicht gefallen sollten, können Sie jetzt noch mit dem Linkspfeil ins Menü zurück. Wenn Sie die Grafik speichern lassen, muß ein File-Namen eingegeben werden. Nach der letzten Eingabe beginnt der Aufbau des Bildes. Das Programm kann dann nur noch durch RUN/STOP-RESTORE abgebrochen werden. Natürlich kann eine abgebrochene Berechnung nach einem RUN gespeichert werden, zum Beispiel um später einen Ausschnitt zu berechnen. Denn statt die Grenzen als Zahlen einzugeben, können Sie auch einen Ausschnitt aus einem bereits bestehenden Bild berechnen lassen.</li>
            <li><strong>Ausschnitt</strong>: Am linken oberen Rand erscheint ein weißes Eck, mit dem Sie die linke obere Ecke des neuen Bildes markieren. Gesteuert wird es mit den Cursortasten, und fixiert mit der Space-Taste. Mit dem rechten unteren Eck wird genauso verfahren. Der Ausschnitt sollte ungefähr die Form des Bildschirms haben, da sonst die Grafik stark verzerrt wird. Mit der Eingabe der maximalen Tiefe geht es dann weiter wie bei der Eingabe von neuen Werten.</li>
            <li><strong>Bild ansehen</strong>: Unter diesem Menüpunkt können Sie sich eine fertige Grafik ansehen. Mit den Funktionstasten können Sie die Farben verändern. Zurück ins Menü geht es mit dem Linkspfeil.</li>
            <li><strong>Speichern</strong>: Ein fertiges Bild kann auf Diskette gespeichert werden. Dabei wird an den Filenamen die Endung ».pic« angehängt. Zusätzlich werden die Parameter des Bildes mit auf Diskette abgelegt.</li>
            <li><strong>Laden</strong>: Bilder von Diskette können unter Angabe des Namens von Diskette geladen werden. Die Endung ».pic« ist dabei nicht mitanzugeben. Nach Beendigung des Ladevorgangs werden die dem Ausschnitt entsprechenden Parameter angezeigt.</li>
            <li><strong>Directory</strong>: Nach Aufruf dieser Funktion wird ein Verzeichnis aller Bilder, die mit ».pic« enden, auf dem Bildschirm gelistet. Effekt: Durch zyklisches Vertauschen der Farbwerte entsteht der Eindruck eines bewegten Bildes. Auch hier können Sie die Farben mit den Funktionstasten verändern. Ins Menü zurück kommt man wieder mit dem Linkspfeil.</li>
        </ul>

        <h3>Programmbeschreibung</h3>

        <p>Die wesentlichen Routinen sind im Maschinenprogramm enthalten, das den Bereich von $C800 bis $CBlE belegt. Es gibt folgende Einsprungadressen:</p>

        <table class="plain">
            <tr class="strong"><td>Adresse</td><td>Variable</td><td>Routine</td><td>Funktion der Routine</td></tr>
            <tr><td>51200</td><td></td><td>USRIN</td><td>übergibt Werte an Maschinenprogramm</td></tr>
            <tr><td>51207</td><td></td><td>USROUT</td><td>übergibt Werte an Basic</td></tr>
            <tr><td>51214</td><td>M1</td><td>MULTION</td><td>Grafik einschalten</td></tr>
            <tr><td>51217</td><td>MO</td><td>MULTIOFF</td><td>Grafik ausschalten</td></tr>
            <tr><td>51220</td><td>SC</td><td>SETCOL</td><td>Farben setzen</td></tr>
            <tr><td>51223</td><td>BG</td><td>BEGIN</td><td>Berechnen der Grafik</td></tr>
            <tr><td>51226</td><td>SA</td><td>SAVEPIC</td><td>Bild speichern</td></tr>
            <tr><td>51229</td><td>L0</td><td>LOADPIC</td><td>Bild laden</td></tr>
            <tr><td>51232</td><td>CL</td><td>CLRSCR</td><td>Grafikbildschirm löschen</td></tr>
        </table>

        <p>Die Adressen der Variablen, die im Assemblerprogramm verwendet werden, können Sie dem Source-Code (Listing 3) entnehmen. Für die Arithmetik des Berechnungsteils werden die Routinen des Basic-Interpreters benutzt. Der Berechnungsteil des Maschinenprogramms folgt dem Algorithmus des Struktogramms (Bild 2). Das Basic-Programm enthält folgende Unterprogramme:</p>

        <figure>
            <img src="80-2.png" alt="">
            <figcaption>Bild 2. Struktogramm des Berechnungs-Algorithmus</figcaption>
        </figure>

        <table class="plain">
            <tr class="strong"><td>Ab Zeile</td><td>Funktion</td></tr>
            <tr><td>1000</td><td>Eingabe neuer Werte</td></tr>
            <tr><td>2000</td><td>Ausschnitt</td></tr>
            <tr><td>3000</td><td>Grafik ansehen</td></tr>
            <tr><td>4000</td><td>Bild speichern</td></tr>
            <tr><td>5000</td><td>Bild laden</td></tr>
            <tr><td>5500</td><td>Directory</td></tr>
            <tr><td>7000</td><td>Bewegungseffekt</td></tr>
        </table>

        <p>Die Übergabe der Werte für die Ränder an das Assemblerprogramm erfolgt über die USR-Funktion. Dabei werden die Zahlen von Basic aus in den Fließkomma-Akku geschrieben. Eine kurze Unterroutine speichert die Zahlen dann an ihre richtige Adresse. Dazu wird von Basic aus direkt in das Maschinenprogramm die jeweilige Adresse gePOKEt (Variable XI, XA, Y1, YA). Auf ähnliche Weise werden auch die Parameter eines geladenen Bildes an Basic übergeben.</p>

        <h3>Die Demo-Bilder</h3>

        <p>Die Demo-Bilder sind auf der Programmservice-Diskette enthalten. Sie können diese laden und gleich mit der Erstellung eines Ausschnitts beginnen. Wir hoffen, daß die Beispielbilder Sie ein wenig auf den Geschmack gebracht haben und wünschen Ihnen viel Spaß auf Ihrer Entdeckungsfahrt durch die unendlichen Weiten der Mathematik.</p>

        <address class="author">(Gerhard Pehland/og)</address>

        <figure>
            <p>Eine komplexe Zahl besteht aus zwei Zahlen, dem Real- und dem Imaginärteil. Bei diesem Programm werden die beiden Anteile durch die X- und Y-Koordinate auf dem Grafikbildschirm repräsentiert. Das heißt die Koordinaten jedes Punktes auf dem Bildschirm stehen für eine komplexe Zahl, die im folgenden »C« genannt wird.</p>

            <p class="noindent">Wie entstehen nun diese fantastischen Grafiken?</p>

            <p>Es beginnt mit der komplexen Zahl 0. Davon wird die Zahl C abgezogen. Es ergibt sich eine neue komplexe Zahl, die jetzt quadriert wird. Vom Ergebnis wird wieder C abgezogen, dann quadriert und so weiter. Dabei entsteht eine Folge von Zahlen, die eine merkwürdige Eigenschaft hat: Für manche Werte von C ergibt sich, daß die Elemente der Folge immer kleiner werden, bei anderen Werten steigt sie schnell an. Dazwischen gibt es aber Werte, bei denen die Folge lange unschlüssig hin und her pendelt, bis sie steigt oder fällt. Die Farbe eines Punktes auf dem Bildschirm wird bestimmt, indem man den dazugehörigen Wert C in die Folge einsetzt und so lange immer wieder abzieht und quadriert, bis die Elemente der Folge eine gewisse Grenze überschreiten. Aus der Anzahl der Iterationen ergibt sich die Farbe des entsprechenden Punktes. Natürlich muß man die Berechnung irgendwann beenden. Das geschieht zum einen, wenn die Folge das Grenzkriterium überschritten hat, oder wenn die maximale Tiefe erreicht ist. Führt man diese Berechnungen für alle Punkte des Bildschirms durch, entstehen typische Grafiken (siehe Seite 169). Das Struktogramm (Bild 2) zeigt Ihnen den genauen Ablauf und die verwendeten Formeln. Die Bedeutung der Variablen können Sie dem Source-Code (Listing 3) entnehmen. Ein typisches Beispiel ist das Ausgangsbild mit den oben angegebenen Werten. In der Bildschirmmitte liegen die Werte von C, bei denen die Folge sofort sinkt. Ganz außen steigt die Folge schnell an. Interessant ist die Grenzschicht dazwischen. Hier gibt es die bizarrsten Muster, die Sie mit »Ausschnitt« (siehe Text) herausvergrößern können. Bei der Vergrößerung zeigt sich eine weitere Eigenschaft dieser Bilder: Am Rande des großen Apfelmännchens sitzen nämlich noch unzählige weitere, kleinere Männchen, auf denen noch kleinere Figuren sitzen. Diese Eigenschaft wird Selbstähnlichkeit genannt. Aber auch andere Muster tauchen immer wieder auf, mal größer, mal kleiner.</p>

            <p>Zum Schluß noch ein Wort zur Rechenzeit: Obwohl die Berechnungsroutine in Assembler geschrieben ist, muß man auf die fertige Grafik manchmal bis zu 8 (acht!) Stunden warten, da ja für 32 000 Punkte bis zu 250 mal quadriert, subtrahiert und verglichen wird.</p>

            <figcaption>Bild 1. Die mathematischen Grundlagen</figcaption>
        </figure>

        <p class="intro">Man glaubt es kaum, was man aus dem C 64 herausholen kann. Bestes Beispiel sind diese Grafiken, die Sie natürlich auch selbst erstellen können. Das Programm dazu finden Sie in dieser Ausgabe.</p>

        <figure>
            <img src="169-1.png" alt="">
            <figcaption>Bild 1. Die Urform aller Apfelmännchen</figcaption>
        </figure>
        <figure>
            <img src="169-2.png" alt="">
            <figcaption>Bild 2. Das ist keine Verkleinerung, sondern ein Ausschnitt aus der Spitze des Apfelmännchens.</figcaption>
        </figure>
        <figure>
            <img src="169-3.png" alt="">
            <figcaption>Bild 3. Ganz rechts, in Bild 2 nur als schwarzer Punkt zu sehen, tritt die Selbstähnlichkeit dieser Grafiken deutlich zu Tage.</figcaption>
        </figure>
        <figure>
            <img src="169-4.png" alt="">
            <figcaption>Bild 4. In einem ganz anderen Bereich am Rand der Ausgangsform findet man spiralförmige Strukturen.</figcaption>
        </figure>
        <figure>
            <img src="169-5.png" alt="">
            <figcaption>Bild 5. Ungefähr fünf Stunden Berechnungszeit sind keine Seltenheit. Probieren Sie doch mal die Werte 0.763; 0.768; 0.0999; 0.103 mit einer Tiefe von 254.</figcaption>
        </figure>
        <figure>
            <img src="169-6.png" alt="">
            <figcaption>Bild 6. Farblich abgestimmt kann man die Bilder durchaus als Kunstwerke der Mathematik betrachten.</figcaption>
        </figure>

        <figure>
            <pre data-filename="apfelroutinen.prg" data-name="Apfelmännchen (Maschinenprogramm)" data-mse=mse1></pre>
            <figcaption>Listing 1. Maschinenprogramm zu »Apfelmännchen«. Bitte mit dem MSE eingeben.</figcaption>
        </figure>
        <div class="binary_download" data-filename="apfelroutinen.prg" data-name="Apfelmännchen (Maschinenprogramm)"></div>
        <figure>
            <pre data-filename="apfelmann" data-name="Apfelmännchen (Basic-Teil)"></pre>
            <figcaption>Listing 2. Den Basic-Teil geben Sie mit dem Checksummer ein. Beachten Sie dazu die Hinweise auf Seite 54.</figcaption>
        </figure>

        <figure>
            <pre>        .OPT P1
 ;*** SYSTEMADRESSEN ***
VIC      =    53248   ;VIDEOCHIP
FAC      =    97      ;FLIESSKOMMA-AKKU
ARG      =    105     ;FLIESSKOMMA-AKKU #2
CHROUT   =    $FFD2   ;EIN ZEICHEN AUSGEBEN
DIV      =    $BB12   ;FAC=ARG/FAC
SUB      =    $B853   ;FAC=ARG-FAC
BYTENFAC =    $B3A2   ;BYTE IN Y NACH FAC
FACNARG  =    $BC0C   ;ARG=FAC
ZEIGSUB  =    $B850   ;FAC=(A/Y)-FAC
ZEIGADD  =    $B867   ;FAC=(A/Y)+FAC
ZEIGMULT =    $BA28   ;FAC=(A/Y)*FAC
KONNARG  =    $BA8C   ;ARG=(A/Y)
KONNFAC  =    $BBA2   ;FAC=(A/Y)
FACNKON  =    $BBD4   ;(X/Y)=FAC
CHSIGN   =    $BFB4   ;FAC=-FAC
FILEPAR  =    $FFBA   ;FILEPARAMETER SETZEN
FILENAM  =    $FFBD   ;FILENAME UEBERGEBEN
SAVE     =    $FFD8   ;SPEICHERN
LOAD     =    $FFD5   ;LADEN
 ;*** VARIABLE ***
ZEIG1    =    $A4     ;UNIVERSALZEIGER
ZEIG2    =    $A6
ZEILE    =    $CF00   ;AKTUELLE ZEILE
SPALTE   =    $CF01   ;AKTUELLE SPALTE
BYTE     =    $CF02   ;ENTHAELT IMMER 4 PUNKTE
POS      =    $CF03   ;ZAEHLER FUER 4 PUNKTE (0 BIS 3)
TIEFE    =    $CF04   ;AKTUELLE TIEFE
TIEFEMAX =    $3F54   ;MAXIMALE TIEFE
DX       =    $CF90   ;SCHRITTWEISE REALTEIL
DY       =    $CF30   ;SCHRITTWEISE IMAGINAERTEIL
CX       =    $CF40   ;REALTEIL AKTUELLER BILDSCHIRMPUNKT
CY       =    $CF60   ;DTO. IMAGINAER
XQUAD    =    87      ;XWERT*XWERT
YQUAD    =    247     ;YWERT*YWERT
XWERT    =    $CF70   ;MOMENTANER WERT DER FOLGE REALTEIL
YWERT    =    $CF80   ;DTO. IMAGINAER
XMAX     =    $3F45   ;RECHTER RAND
XMIN     =    $3F40   ;LINKER RAND
YMAX     =    $3F4F   ;OBERER RAND
YMIN     =    $3F4A   ;UNTERER RAND
FILENAME =    $CFA0   ;VON BASIC GEPOKED
FARBTAB  =    $CE00   ;TABELLE TIEFE MOD 3 +1
 ;
 ;*** PROGRAMM ***
         *=   $C800
USRIN    LDX  #<XMIN  ;UEBERGABE DER REALZAHLEN VON BASIC
         LDY  #>XMIN
         JMP  FACNKON
USROUT   LDA  #<XMIN  ;REALZAHLEN AN BASIC ZURUECK
         LDY  #>XMIN
         JMP  KONNFAC
 ;SPRUNGTABELLE
         JMP  MULTION
         JMP  MULTIOFF
         JMP  SETCOL
         JMP  BEGIN
         JMP  SAVEPIC
         JMP  LOADPIC
         JMP  CLRSCR
COL0     .BYT 0       ;FARBEN
COL1     .BYT 7
COL2     .BYT 10
COL3     .BYT 2
CLRSCR   LDA  #0      ;GRAFIK LOESCHEN
         STA  ZEIG1
         LDA  #$20
         STA  ZEIG1+1
         LDY  #0
L1       LDA  #0
         STA  (ZEIG1),Y
         INY
         BNE  L1
         INC  ZEIG+1
         LDA  #$40
         CMP  ZEIG1+1
         BNE  L1
         RTS


SETCOL   LDA  #0      ;FARBEN SETZEN
         STA  53280
         LDA  COL0
         STA  53281
         LDA  COL1
         ASL  A
         ASL  A
         ASL  A
         ASL  A
         ORA  COL2
         LDX  #0
         STX  ZEIG1
         STX  ZEIG2
         LDX  #4
         STX  ZEIG1+1
         LDX  #$D8
         STX  ZEIG2+1
WAIT     LDX  VIC+18  ;WARTEN AUF STRAHLDURCHLAUF
         CPX  #58
         BNE  WAIT
         LDX  VIC+17
         BMI  WAIT
         LDY  #0
         LDX  #8
L2       STA  (ZEIG1),Y
         PHA
         LDA  COL3
         STA  (ZEIG2),Y
         PLA
         INY
         BNE  L2
         INC  ZEIG1+1
         INC  ZEIG2+1
         CPX  ZEIG1+1
         BNE  L2
         RTS


MULTION  LDA  VIC+17  ;MULTICOLOUR EIN
         ORA  #%00100000
         STA  VIC+17
         LDA  VIC+24
         ORA  #$00001000
         STA  VIC+24
         LDA  VIC+22
         ORA  #$00010000
         STA  VIC+22
         JSR  SETCOL
         RTS
SETBYTE  LDA  ZEILE   ;4 PUNKTE (=1 BYTE) SETZEN
         AND  #%11111000
         LSR  A
         LSR  A
         TAX
         LDA  ZEILTAB,X
         STA  ZEIG1
         INX
         LDA  ZEILTAB,X
         STA  ZEIG1+1
         LDA  ZEILE
         AND  #%00000111
         CLC
         ADC  ZEIG1
         STA  ZEIG1
         BCC  SETBYT1
         INC  ZEIG1+1
SETBYT1  LDA  SPALTE
         ASL  A
         ASL  A
         ASL  A
         BCC  SETBYT3
         INC  ZEIG1+1
SETBYT3  CLC
         ADC  ZEIG1
         STA  ZEIG1
         BCC  SETBYT2
         INC  ZEIG1+1
SETBYT2  LDY  #0
         LDA  BYTE
         STA  (ZEIG1),Y
         RTS

FACSARG  LDA  ARG+5;VORZEICHENVERGLEICH VON FAC UND ARG
         EOR  FAC+5
         STA  ARG+6
         LDA  FAC
         RTS


MAKETAB  LDX  #0      ;FARBTABELLE ANLEGEN
         LDY  #1
L40      TYA
         STA  FARBTAB,X
         INY
         CPY  #4
         BNE  MT1
         LDY  #1
MT1      INX
         BNE  L40
         RTS


MULTIOFF LDA  VIC+22  ;MULTICOLOUR AUS
         AND  #%11101111
         STA  VIC+22
         LDA  VIC+17
         AND  #%11011111
         STA  VIC+17
         LDA  VIC+24
         AND  #%11110111
         STA  VIC+24
         LDA  #12
         STA  53281
         LDA  #11
         STA  53280
         LDA  #147
         JSR  CHROUT
         RTS

APFEL    SEI          ;BERECHNUNG DER GRAFIK
         JSR  MAKETAB
         LDA  #<XMIN  ;DX=(XMAX-XMIN)/159
         LDY  #>XMIN
         JSR  KONNFAC
         LDA  #<XMAX
         LDY  #>XMAX
         JSR  ZEIGSUB
         JSR  FACNARG
         LDY  #159
         JSR  BYTENFAC
         JSR  FACSARG
         JSR  DIV
         LDX  #<DX
         LDY  #>DX
         JSR  FACNKON
         LDA  #<YMIN  ;DY=(YMAX-YMIN)/199
         LDY  #>YMIN
         JSR  KONNFAC
         LDA  #<YMAX
         LDY  #>YMAX
         JSR  ZEIGSUB
         JSR  FACNARG
         LDY  #199
         JSR  BYTENFAC
         JSR  FACSARG
         JSR  DIV
         LDX  #<DY
         LDY  #>DY
         JSR  FACNKON
         LDY  #4      ;CX=XMIN CY=YMAX
L13      LDA  XMIN,Y
         STA  CX,Y
         LDA  YMAX,Y
         STA  CY,Y
         DEY
         BPL  L13
         LDA  #0
         STA  ZEILE
         STA  SPALTE
         STA  POS
ITERAT   LDA  #0
         STA  BYTE
ITERAT1  LDA  #0
         STA  TIEFE
         LDY  #4
L15      STA  XWERT,Y
         STA  YWERT,Y
         DEY
         BPL  L15
ITER1    INC  TIEFE
         LDA  TIEFEMAX
         CMP  TIEFE   ;"MAXIMALE TIEFE ?
         BCS  ITER4
         LDA  #0      ;PUNKTFARBE
         JMP  PLOT
ITER4    LDA  #<XWERT ;XQUAD= XWERT * XWERT
         LDY  #>XWERT
         JSR  KONNFAC
         LDA  #<XWERT
         LDY  #>WERT
         JSR  ZEIGMULT
         LDX  #XQUAD
         LDY  #0
         JSR  FACNKON
         LDA  #<YWERT ;YQUAD = YWERT * YWERT
         LDY  #>YWERT
         JSR  KONNFAC
         LDA  #<YWERT
         LDY  #>YWERT
         JSR  ZEIGMULT
         LDX  #YQUAD
         LDY  #0
         JSR  FACNKON
         LDA  #XQUAD
         LDY  #0      ;SUMME XQUAD+YQUAD
         JSR  ZEIGADD
         LDA  FAC
         CMP  #$84    ;"SUMME GROESSER 8 ?
         BCC  ITER3
         LDX  TIEFE
         LDA  FARBTAB,X ;PUNKTFARBE
         JMP  PLOT
ITER3    LDA  #<YWERT ;YWERT=XWERT*YWERT*2-CY
         LDY  #>YWERT
         JSR  KONNFAC
         LDA  #<XWERT
         LDY  #>XWERT
         JSR  ZEIGMULT
         LDA  FAC
         BEQ  ITER2
         INC  FAC
ITER2    LDA  #<CY
         LDY  #>CY
         JSR  ZEIGSUB
         JSR  CHSIGN
         LDX  #<YWERT
         LDY  #>YWERT
         JSR  FACNKON
         LDY  #0      ;XWERT=XQUAD-YQUAD-CX
         LDA  #YQUAD
         JSR  KONNFAC
         LDY  #0
         LDA  #XQUAD
         JSR  ZEIGSUB
         LDA  #<CX
         LDY  #>CX
         JSR  ZEIGSUB
         JSR  CHSIGN
         LDX  #<XWERT
         LDY  #>XWERT
         JSR  FACNKON
         JMP  ITER1
PLOT     ASL  BYTE
         ASL  BYTE
         ORA  BYTE
         STA  BYTE
         LDA  #<CX      ;CX=CX+DX
         LDY  #>CX
         JSR  KONNFAC
         LDA  #<DX
         LDY  #>DX
         JSR  ZEIGADD
         LDX  #<CX
         LDY  #>CX
         JSR  FACNKON
         INC  POS
         LDA  POS
         AND  #3
         BEQ  APFEL1
         JMP  ITERAT1
APFEL1   STA  POS
         JSR  SETBYTE
         INC  SPALTE
         LDA  SPALTE
         CMP  #40
         BEQ  APFEL2
         JMP  ITERAT
APFEL2   LDA  #0
         STA  SPALTE
         LDY  #5      ;CX=XMIN
L30      LDA  XMIN,Y
         STA  CX,Y
         DEY
         BPL  L30
         INC  ZEILE
         LDA  ZEILE
         CMP  #200
         BEQ  ENDAPFEL
         LDA  #<DY      ;CY=CY-DY
         LDY  #>DY
         JSR  KONNFAC
         LDA  #<CY
         LDY  #>CY
         JSR  ZEIGSUB
         LDX  #<CY
         LDY  #>CY
         JSR  FACNKON
         JMP  ITERAT
ENDAPFEL CLI
         RTS

BEGIN    JSR  MULTION
         JSR  APFEL
         JMP  MULTIOFF

SAVEPIC  LDX  #8      ;SPEICHERN
         JSR  FILEPAR
         LDA  FILENAME+16
         LDX  #<FILENAME
         LDY  #>FILENAME
         JSR  FILENAM
         LDA  #0
         STA  ZEIG1
         LDA  #$20
         STA  ZEIG1+1
         LDA  #ZEIG1
         LDX  #$56
         LDY  #$3F
         JMP  SAVE

LOADPIC  LDX  #8      ;LADEN
         LDY  #1
         JSR  FILEPAR
         LDA  FILENAME+16
         LDX  #<FILENAME
         LDY  #>FILENAME
         JSR  FILENAM
         LDA  #0
         JMP  LOAD
         RTS

 ;TABELLE DER ZEILENANFAENGE
ZEILTAB  .WOR 8192,8512,8832,9152,9472,9792
         .WOR 10112,10432,10752,11072,11392,11712
         .WOR 12032,12352,$3180,12992,13312,13632
         .WOR 13952,14272,14592,14912,15232,15552
         .WOR 15872</pre>
            <figcaption>Listing 3. Das Source-Code-Listing des Apfelmännchens</figcaption>
        </figure>

        <div class="binary_download" data-filename="apfelmann.pic.prg" data-name="apfelmann.pic"></div>
    </article>
</body>

</html>