<!DOCTYPE html>
<html lang="de">

<head>
    <title>XXX</title>
    <meta charset="UTF-8">
    <link rel="stylesheet" href="../style.css">
    <meta name="author" content="Bernd Weißbecker, gk">
    <meta name="64er.issue" content="8/84">
    <meta name="64er.pages" content="66-67,106-111">
    <meta name="64er.toc_category" content="XXX">
    <meta name="64er.id" content="XXX">
</head>

<body>
    <article>
        <h1>Listing des Monats: Castle of Doom</h1>

        <p class="intro">Castle of Doom ist ein Adventure, das es in sich hat. Man kann nicht nur drei verschiedene Versionen auswählen, es ist zudem gar nicht einfach, bis ans Ende durchzustehen. Dazu brauchen Sie Geduld, eine gehörige Portion Phantasie und – viel Zeit.</p>

        <figure>
            <img src="66-1x.png" alt="">
            <figcaption>Bild 1. Die Burg des Grauens</figcaption>
        </figure>
        <figure>
            <img src="66-2x.png" alt="">
            <figcaption>Bild 2. Welcher Weg ist richtig</figcaption>
        </figure>
        <figure>
            <img src="66-3.png" alt="">
            <figcaption>Bild 3. Beißen Sie sich die Zähne aus: Es ist nicht einfach, das Spiel durchzustehen</figcaption>
        </figure>

        <p>Zunächst einige wesentliche Informationen zum Ablauf des Spiels:</p>

        <p>Am Anfang des Spiels kann man sich für eine von drei verschiedenen Versionen entscheiden. Bei allen Versionen ist das »Spielfeld« gleich, doch das Ziel, das es zu erreichen gilt, ist jeweils ein anderes. Die Kommandos müssen jeweils aus zwei Worten bestehen, die durch Leerraum getrennt werden. Beide Worte können beliebig abgekürzt werden, doch man muß darauf achten, daß keine Mißverständnisse auftreten.</p>

        <p>Das Programm beinhaltet eine kurze Anleitung, in der die wichtigsten Punkte der Befehlseingabe nochmals erwähnt werden. Zudem sind alle Verben, die der Computer versteht, aufgeführt. Wird dennoch ein Wort benutzt, das der Computer nicht kennt, so gibt er an, welches der eingegebenen Worte ihm unbekannt ist.</p>

        <p>Auf dem Bildschirm sind immer zu sehen:</p>

        <ul>
            <li>eine Grafik, die den Ort zeigt, an dem man sich befindet,</li>
            <li>eine Liste der Gegenstände, die sich an diesem Ort befinden,</li>
            <li>eine Liste der Gegenstände, die man besitzt.</li>
        </ul>

        <address class="author">(Bernd Weißbecker/gk)</address>

        <aside>
            <img src="66-4.png" class="inline">
            <p>Der Autor Bernhard Weißbecker wurde am 29.10.1963 in Gelnhausen geboren. Er machte sein Abitur, und ist seit Mitte 1983 bei der Bundeswehr. Wie er zur Computerei kam, erzählt er selbst:</p>

            <p>Meine ersten Erfahrungen mit Computern machte ich in der Schule. Dort gab es einen Computerraum, der mit einigen PETs bestückt war. Am Anfang zogen mich Spiele wie »Space Invaders« dorthin, doch bald erwarb ich auch erste Basic-Kenntnisse. Mein Beitritt zu einem Computer-Kurs, der nachmittags auf freiwilliger Basis ablief, folgte. Dieser Kurs endete leider nach einem halben Jahr schon wieder. Zudem zwang mich das nahende Abitur, meine Aktivitäten auf andere Gebiete zu verlagern.</p>

            <p>Nun bin ich seit zehn Monaten bei der Bundeswehr und seit sieben Monaten stolzer Besitzer eines Commodore 64. Da ich mich besonders für Abenteuerspiele interessiere, wollte ich unbedingt auch selbst einmal ein solches Spiel schreiben.</p>

            <p>An den Abenteuerspielen, die gelegentlich in Computerzeitungen abgedruckt sind, ärgerte mich immer, daß man schon beim Abtippen des Programms unfreiwillig Informationen erhält, die das Lösen des Abenteuerspiels dann oft wesentlich vereinfachen. Und mit einem Abenteuerspiel, das man einmal gelöst hat, kann man nicht mehr viel anfangen. Deshalb habe ich bei meinem Adventure drei verschiedene Versionen zur Auswahl gestellt, für die jeweils ein anderer Lösungsweg gefunden werden muß. Zudem macht es die Art der Befehlsauswertung relativ schwer, aus dem Listing zu Informationen über den Lösungsweg zu kommen.</p>
        </aside>

        <h3>Erklärung der Programmzeilen für Castle of Doom:</h3>
        <p>Zur Erläuterung des Programms sind hier die verschiedenen Abschnitte des Programms aufgeführt:</p>

        <table>
            <tr>
                <td>1</td>
                <td>Sprung zur Befehlsausführung</td>
            </tr>
            <tr>
                <td>5-95</td>
                <td>DATAs einlesen, Erklärung?</td>
            </tr>
            <tr>
                <td>100—160</td>
                <td>Version auswählen</td>
            </tr>
            <tr>
                <td>165-176</td>
                <td>Zauberwörter bestimmen</td>
            </tr>
            <tr>
                <td>179—740</td>
                <td>Bilder zeichnen</td>
            </tr>
            <tr>
                <td>748—860</td>
                <td>Kommando auswerten</td>
            </tr>
            <tr>
                <td>870-940</td>
                <td>Bewegen</td>
            </tr>
            <tr>
                <td>950-980</td>
                <td>Sprung zur Befehlsausführung berechnen und ausführen</td>
            </tr>
            <tr>
                <td>999—5382</td>
                <td>Befehlsausführung</td>
            </tr>
            <tr>
                <td>6500—6528</td>
                <td>Befehl »sage«</td>
            </tr>
            <tr>
                <td>7000—7065</td>
                <td>Besitz + Gegenstände anzeigen</td>
            </tr>
            <tr>
                <td>7100—7125</td>
                <td>Befehl »nimm«</td>
            </tr>
            <tr>
                <td>7200—7415</td>
                <td>Befehl »wirf«</td>
            </tr>
            <tr>
                <td>7500-7560</td>
                <td>Erklärung</td>
            </tr>
            <tr>
                <td>7600-7720</td>
                <td>verloren</td>
            </tr>
            <tr>
                <td>7800-7880</td>
                <td>gewonnen</td>
            </tr>
            <tr>
                <td>8000—8110</td>
                <td>DATAs</td>
            </tr>
        </table>

        <p>Da die Kommandoauswertung recht ungewöhnlich ausgeführt wird, folgt hier noch eine Beschreibung der betreffenden Programmzeilen:</p>

        <table>
            <tr>
                <td>755</td>
                <td>Kommandoeingabe</td>
            </tr>
            <tr>
                <td>760-785</td>
                <td>Zerlegen in Verb/Hauptwort</td>
            </tr>
            <tr>
                <td>795—810</td>
                <td>Vergleich mit der Liste aller bekannten Wörter<br>
                    Zuordnung:<br>
                    X = Nummer des Verbs (1—14)<br>
                    Y = Nummer des Hauptworts (1—21)</td>
            </tr>
            <tr>
                <td>830-845</td>
                <td>Untersuchen, ob eine sinnvolle Kombination von Verb und Hauptwort vorliegt</td>
            </tr>
            <tr>
                <td>960—980</td>
                <td>Aus X und Y wird die Zeilennummer berechnet, in der die Antwort auf das eingegebene Kommando steht. Die berechnete Zeilennummer wird in ihre Ziffern zerlegt, und diese werden in den Programmspeicher hinter das GOTO in Zeile 1 gePOKEd. Anschließend wird ein GOTO 1 aus-geführt.</td>
            </tr>
        </table>

        <h3>Variablen-Definition zum Listing des Monats:</h3>

        <p>Zum Abschluß hier noch die wichtigsten Variablen:</p>

        <table>
            <tr>
                <td>V$(1...14)</td>
                <td>Liste Verben</td>
            </tr>
            <tr>
                <td>H$(1...21)</td>
                <td>Liste Hauptwörter</td>
            </tr>
            <tr>
                <td>RI$(1...19)</td>
                <td>mögliche Richtungen</td>
            </tr>
            <tr>
                <td>ZI$(1...19)</td>
                <td>Nachbarfelder</td>
            </tr>
            <tr>
                <td>G$(1...12)</td>
                <td>Liste Gegenstände</td>
            </tr>
            <tr>
                <td>G(1..12)</td>
                <td>Ort der Gegenstände</td>
            </tr>
            <tr>
                <td>P</td>
                <td>Position</td>
            </tr>
            <tr>
                <td>V</td>
                <td>Version</td>
            </tr>
            <tr>
                <td>Z1$,Z2$</td>
                <td>Zauberwörter</td>
            </tr>
            <tr>
                <td>ZA</td>
                <td>= 0,1,2 Zauberwort gesagt?</td>
            </tr>
            <tr>
                <td>K$</td>
                <td>Kommando</td>
            </tr>
            <tr>
                <td>V$</td>
                <td>Verb</td>
            </tr>
            <tr>
                <td>H$</td>
                <td>Hauptwort</td>
            </tr>
            <tr>
                <td>KB$(1...9)</td>
                <td>mögliche Kombinationen</td>
            </tr>
            <tr>
                <td>MO</td>
                <td>= 1 Monster besiegt</td>
            </tr>
            <tr>
                <td>GE</td>
                <td>= 1 Geist besiegt</td>
            </tr>
            <tr>
                <td>MR</td>
                <td>= 1 Monster anwesend</td>
            </tr>
            <tr>
                <td>GS</td>
                <td>= 1 Geist anwesend</td>
            </tr>
            <tr>
                <td>ZB</td>
                <td>= 1 Zauberer anwesend</td>
            </tr>
            <tr>
                <td>GG</td>
                <td>Anzahl Gegenstände</td>
            </tr>
        </table>

        <figure>
            <pre>
0 GOTO5
1 GOTO!!!!
2 REM
3 REM ZEILEN 0 UND 1 NICHT AENDERN !!!!
4 REM
5 DIMV$(14),H$(21),RI$(19),ZI$(19),G$(12),G(12)
6 L$="{RVON}                        "
7 CL$="{CBM-G}                      {CBM-M}"
8 EF$="                                       "
10 POKE53280,14:POKE53281,14:POKE53272,23:P=15:QQ=54272:VI=53248
15 POKEVI+21,0:PRINT"{CLR}"SPC(12)"{BLK}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}CASTLE{$A0}OF{$A0}DOOM":REM CASTLE OF DOOM
20 PRINT"{DOWN}{DOWN}{WHT}    EIN ADVENTURE VON B.WEISSBECKER"
25 PRINTSPC(11)"{DOWN}{DOWN}* BITTE WARTEN *"
30 FORI=1TO14:READV$(I)
35 IFI<10THENREADKB$(I)
40 NEXT
45 FORI=1TO21:READH$(I):NEXT
50 FORI=1TO19:READRI$(I),ZI$(I):NEXT
55 FORI=1TO12:G$(I)=H$(I):READG(I):NEXT
60 FORI=1TO4:READZX$(I),ZY$(I):NEXT
65 FORI=13TO15
70 FORJ=0TO62:READK:POKE64*I+J,K:NEXT:NEXT
85 PRINT"{UP}  MOECHTEN SIE EINE ERKLAERUNG ? (J/N)"
90 GETA$:IFA$="J"THEN7500
95 IFA$<>"N"THEN90
100 PRINT"{CLR}{BLK}"SPC(10)"{DOWN}CASTLE{$A0}OF{$A0}DOOM"
105 PRINT"{DOWN}{DOWN}{CYN} BEI DIESEM ADVENTURE KOENNEN SIE ZWI-"
110 PRINT"{DOWN} SCHEN DREI VERSIONEN WAEHLEN :":PRINT"{DOWN}  WOLLEN SIE :"
115 PRINT"{DOWN}  1 - EINE VERZAUBERTE PRINZESSIN":PRINTSPC(6)"{DOWN}RETTEN"
120 PRINT"{DOWN}  2 - EINEN BOESEN ZAUBERER BESIEGEN"
125 PRINT"{DOWN}  3 - EINEN SCHATZ SUCHEN"
130 PRINT"{DOWN}{DOWN} BITTE GEBEN SIE DIE ENTSPRECHENDE"
135 PRINT"{DOWN} KENNZIFFER EIN."
140 POKE198,0:WAIT198,1
145 GETI$:IFASC(I$)<49ORASC(I$)>51THEN140
150 V=VAL(I$):IFV<>2THEN160
155 RI$(13)="W-O":ZI$(13)="1214":RI$(9)="W-O-R":ZI$(9)="081018"
157 RI$(8)="N-S-O":ZI$(8)="051209"
160 IFV=1THENG(7)=1
165 FORI=1TO4:J(I)=1+4*RND(TI):NEXT
170 Z1$=ZX$(J(1))+ZY$(J(2))
175 Z2$=ZX$(J(3))+ZY$(J(4))
176 IFZ1$=Z2$THEN165
177 TI$="000000"
179 REM **** BILDER ZEICHNEN ****
180 PRINT"{CLR}{BLK}";:POKE53272,21:POKEVI+21,0:POKEVI+23,0:POKEVI+29,0
185 FORI=1TO20:PRINTSPC(8)CL$:NEXT
190 FORI=1824TO1863:POKEI,99:POKEI+QQ,0:NEXT
195 IFP>11THEN415
197 POKE53280,5:POKE53281,5
200 PRINT"{HOME}";:FORI=1TO4:PRINTSPC(8+I)"M"SPC(22-2*I)"N":NEXT
205 PRINTSPC(13)"{UP}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}"
210 FORI=1TO10:PRINTSPC(12)"{CBM-M}"SPC(14)"{CBM-G}":NEXT
215 PRINTSPC(13)"{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{UP}"
220 FORI=1TO5:PRINTSPC(13-I)"N"SPC(12+2*I)"M":NEXT
225 FORI=1TOLEN(RI$(P))STEP2:A$=MID$(RI$(P),I,1)
230 IFA$="N"THEN250
235 IFA$="W"THEN260
240 IFA$="O"THEN265
245 NEXT:GOTO270
250 FORJ=1402TO1562STEP40:FORK=0TO3:POKEJ+K,160:POKEJ+K+QQ,0:NEXT:NEXT
255 POKE1402,254:POKE1405,252:GOTO245
260 FORJ=1552TO1752STEP40:POKEJ,160:POKEQQ+J,0:NEXT:POKE1752,105:GOTO245
265 FORJ=1575TO1775STEP40:POKEJ,160:POKEQQ+J,0:NEXT:POKE1775,95:GOTO245
270 ONPGOTO275,740,300,315,740,325,345,740,355,375,395
275 ONVGOTO740,280,285
280 POKE2040,15:POKEVI+39,0:POKEVI,150:POKEVI+1,140:POKEVI+29,1:POKEVI+23,1
282 POKEVI+21,1:ZB=1:GOTO740
285 PRINT"{HOME}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}"SPC(13)"{CBM-P}{CBM-P}{CBM-P}{CBM-P}":PRINTSPC(12)"N   N{CBM-H}"
290 PRINTSPC(11)"N   N {CBM-H}":PRINTSPC(11)"O{CBM-Y}{CBM-Y}P  {CBM-H}":PRINTSPC(11)"{CBM-H}$${CBM-N} N"
295 PRINTSPC(11)"L{CBM-P}{CBM-P}{SHIFT-@}N SCHATZ":FORI=1TO2000:NEXT:GOTO7830
300 IFLEFT$(RI$(3),1)="N"THEN740
305 FORJ=1402TO1562STEP40:FORK=0TO3
310 POKEJ+K,160:POKEJ+K+QQ,06:NEXT:NEXT:PRINTSPC(16)"{UP}{UP}{UP}{UP}{BLK}SPIEGEL":GOTO740
315 PRINT"{HOME}{DOWN}{DOWN}"SPC(13)"{CBM-*}{RVON}    {RVOF}{SHIFT-POUND}":PRINTSPC(14)"{CBM-*}{RVON}  {RVOF}{SHIFT-POUND}":PRINTSPC(15)"{CBM-H}{CBM-N}"
320 FORI=1TO10:PRINTSPC(15)"OP":NEXT:GOTO740
325 PRINT"{HOME}{BRN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}"SPC(13)"{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}":PRINTSPC(13)"L{CBM-P}{CBM-P}{CBM-P}{SHIFT-@}"
330 PRINTSPC(12)"N    N{CBM-H}":PRINTSPC(11)"N    NN{CBM-H}":PRINTSPC(10)"N    NN{CBM-Y}"
335 PRINTSPC(9)"O{CBM-Y}{CBM-Y}{CBM-Y}PNN":PRINTSPC(9)"{CBM-H}   {CBM-N}N":PRINTSPC(9)"L{CBM-P}{CBM-P}{CBM-P}{SHIFT-@}  SCHLAFZIMMER"
340 GOTO740
345 IFGE=1THEN740
350 POKE2040,13:POKEVI+39,1:POKEVI,150:POKEVI+1,150:POKEVI+23,1:POKEVI+29,1
351 POKEVI+21,1:IFG(12)<>0THENFORI=1TO2000:NEXT:GOTO7630
352 GS=1:GOTO740
355 PRINT"{HOME}{BLK}{DOWN}"SPC(11)"BURGHOF / BRUNNEN":PRINTSPC(13)"{DOWN}OLOLOLOP{SHIFT-@}P{SHIFT-@}P{SHIFT-@}P"
360 PRINTSPC(17)"{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{CBM-@}{CBM-P}{CBM-O}{CBM-O}{CBM-P}{CBM-@}":PRINTSPC(16)"{RVON}        "
365 PRINTSPC(16)"{CBM-H}{CBM-T}{CBM-Y}{CBM-U}{CBM-U}{CBM-Y}{CBM-T}{CBM-N}":PRINTSPC(16)"{CBM-H}      {CBM-N}":PRINTSPC(16)"{CBM-Y}EDCCDE{CBM-Y}"
370 GOTO740
375 POKE214,14:PRINT
380 IFFA=1THENPRINTSPC(23)"{DOWN}{CBM-*}{RVON}   {CBM-*}":PRINTSPC(24)"{CBM-*}{RVON}   {CBM-*}":GOTO740
385 PRINTSPC(23)"{CBM-P}{CBM-P}{CBM-P}{CBM-P}":PRINTSPC(23)"M   M"
390 PRINTSPC(16)"QUADRAT M   M":PRINTSPC(25)"{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}":GOTO740
395 PRINT"{HOME}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}"SPC(23)"{CBM-P}{CBM-P}{CBM-P}{CBM-P}":PRINTSPC(22)"{CBM-N}M   M":PRINTSPC(22)"{CBM-N} OP{CBM-Y}P"
400 PRINTSPC(15)"L{SHIFT-@}     {CBM-N} {CBM-H}{CBM-N}.{CBM-N}":PRINTSPC(15)"L{SHIFT-@}     {CBM-N} {CBM-H}{CBM-N} {CBM-N}"
405 PRINTSPC(14)"{RVON}{SHIFT-POUND}  {CBM-*}{RVOF}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}ML{SHIFT-@}{CBM-P}{SHIFT-@}":PRINTSPC(13)"{RVON}{SHIFT-POUND}    {CBM-*}"
410 PRINTSPC(21)"SCHRANK":GOTO740
415 IFP>14THEN445
417 POKE53280,8:POKE53281,8
420 PRINT"{HOME}";:FORI=1TO6:PRINTSPC(8)"{BLU}"L$:NEXT
425 FORI=55504TO55514STEP2:POKEI,7:NEXT:FORI=55517TO55527STEP2:POKEI,7:NEXT
430 FORI=1TO11:PRINT"{YEL}"SPC(8)L$:NEXT:FORI=1TO3:PRINT"{BRN}"SPC(8)L$:NEXT
435 IFLEFT$(RI$(P),1)<>"N"THEN740
440 POKE214,11:PRINT"{BLK}":FORI=1TO5:PRINTSPC(18)"{RVON}    ":NEXT
442 GOTO740
445 POKE53280,14:POKE53281,14:ONP-14GOTO555,450,485,500,540
450 PRINT"{HOME}{YEL}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}"SPC(20)"{CBM-D}":PRINTSPC(18)"{RVON}   "
460 PRINTSPC(17)"{BRN}{CBM-D}{RVON} N {CBM-C}":PRINTSPC(16)"{RVON}{SHIFT-POUND} N   {CBM-C}"
465 PRINTSPC(15)"{CBM-D}{RVON} (      {CBM-C}":PRINTSPC(14)"{CBM-I}{RVON}{CBM-V}  M       {CBM-C}"
470 PRINTSPC(13)"{RVON}{SHIFT-POUND}     M       {CBM-C}"
475 FORI=1TO7:PRINTSPC(8)L$:NEXT
480 PRINTSPC(9)"{UP}{UP}{UP}{UP}{UP}{UP}{RVON}BERG"
481 IFLEN(RI$(16))=5THENPRINTSPC(9)"{DOWN}{RVON}STEILER WEG NACH OBEN"
482 GOTO740
485 PRINT"{HOME}"SPC(9)"{DOWN}{DOWN}{DOWN}{DOWN}STEILER BERGHANG,":PRINTSPC(9)"{DOWN}HOEHLE"
490 PRINTSPC(21)"{DOWN}{DOWN}{DOWN}{CBM-I}":PRINTSPC(19)"{CBM-D}{RVON}   {CBM-V}{CBM-C}{RVOF}{CBM-F}"
495 FORI=1TO6:PRINTSPC(19)"{RVON}       ":NEXT:GOTO740
500 PRINT"{HOME}";:FORI=1TO19:PRINTSPC(8)L$:NEXT
505 PRINT"{HOME}"SPC(9)"{RVON}{DOWN}IM BRUNNEN ..."
510 IFV<>3THEN740
515 IFMO=1THENPRINT"{DOWN}":GOTO530
520 POKE2040,14:POKEVI+39,13:POKEVI,200:POKEVI+1,150:POKEVI+23,1:POKEVI+29,1
525 PRINTSPC(9)"{RVON}{DOWN}MONSTER":POKEVI+21,1:MR=1
530 PRINTSPC(9)"{RVON}{DOWN}TUER{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}":FORI=1TO7:PRINTSPC(16)"     ":NEXT
535 IFRI$(18)="H"THENPOKE1601,114
537 GOTO740
540 PRINT"{HOME}";:FORI=1TO20:PRINTSPC(8)L$:NEXT
545 PRINT"{HOME}"SPC(9)"{RVON}{DOWN}IN DER HOEHLE ..."
550 PRINTSPC(9)"{RVON}{DOWN}STOCKDUNKEL HIER ...":GOTO740
555 PRINT"{HOME}{RED}{DOWN}"SPC(13)"O{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}P":PRINTSPC(13)"{CBM-H} B A S A R {CBM-N}"
560 PRINTSPC(13)"L{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{SHIFT-@}"
565 IFXX=1THEN655
570 PRINTSPC(9)"{BLK}{DOWN}EIN   BEFREUNDETER"
575 PRINTSPC(9)"{DOWN}HAENDLER IST BEREIT,":PRINTSPC(9)"{DOWN}IHNEN ZWEI DER FOLGEN-"
580 PRINTSPC(9)"{DOWN}DEN GEGENSTAENDE ZU":PRINTSPC(9)"{DOWN}UEBERLASSEN :"
585 PRINTSPC(9)"{DOWN}{RVON}1{RVOF} GLASKUGEL {RVON}2{RVOF} KAEFIG"
590 PRINTSPC(9)"{DOWN}{RVON}3{RVOF} AMULETT   {RVON}4{RVOF} BUCH"
595 PRINT"{DOWN}{DOWN}{DOWN}{DOWN}  BITTE ENTSPRECHENDE NUMMERN EINGEBEN"
600 GETI$:IFI$=""THEN600
605 IFASC(I$)<49ORASC(I$)>52THEN600
610 ONVAL(I$)GOSUB635,640,645,650
615 GETJ$:IFJ$=""ORJ$=I$THEN615
620 IFASC(J$)<49ORASC(J$)>52THEN615
625 ONVAL(J$)GOSUB635,640,645,650
630 XX=1:GG=GG+2:GOSUB7000:GOTO655
635 POKE1633,49:G(1)=0:RETURN
640 POKE1645,50:G(2)=0:RETURN
645 POKE1713,51:G(3)=0:RETURN
650 POKE1725,52:G(4)=0:RETURN
655 YY=0:FORI=1TO4:IFG(I)=0THENYY=YY+1
657 NEXT:IFYY<>2THEN710
660 PRINT"{HOME}{BLK}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}"SPC(9)"WOLLEN SIE DIE GEGEN-"
665 PRINTSPC(9)"{DOWN}STAENDE, DIE SIE HIER":PRINTSPC(9)"{DOWN}ERHALTEN HABEN, NOCH  "
670 PRINTSPC(9)"{DOWN}EINMAL UMTAUSCHEN ?":PRINTSPC(9)"{DOWN}        (J/N)"
675 PRINT:PRINTSPC(8)CL$:PRINT:PRINTSPC(8)CL$:GOSUB860
680 GETI$:IFI$="N"THEN710
685 IFI$<>"J"THEN680
690 FORI=1TO4:G(I)=20:NEXT:GG=GG-2
695 PRINT"{HOME}{DOWN}{DOWN}{DOWN}{DOWN}":FORI=1TO9:PRINTSPC(8)CL$:NEXT
700 PRINT"{HOME}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}"SPC(12)"WAEHLEN SIE !":PRINT"{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}"
705 GOTO585
710 PRINT"{HOME}{DOWN}{DOWN}{DOWN}{DOWN}{BLK}":FORI=1TO9:PRINTSPC(8)CL$:NEXT
715 PRINT"{HOME}{DOWN}{DOWN}{DOWN}":FORI=1TO5:PRINTSPC(13)"{CBM-J}"SPC(11)"{CBM-L}":NEXT
720 PRINTSPC(13)"{CYN}O{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}{CBM-Y}P":PRINTSPC(13)"{CBM-H}O{CBM-Y}P O{CBM-Y}P O{CBM-Y}P{CBM-N}"
725 PRINTSPC(13)"{CBM-H}{CBM-H}{RED}Q{CYN}{CBM-N} {CBM-H}{RED}Q{CYN}{CBM-N} {CBM-H}{RED}Q{CYN}{CBM-N}{CBM-N}"
730 PRINTSPC(13)"{CBM-H}L{CBM-P}{SHIFT-@} L{CBM-P}{SHIFT-@} L{CBM-P}{SHIFT-@}{CBM-N}":PRINTSPC(13)"L{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{CBM-P}{SHIFT-@}"
740 POKE214,18:PRINT:PRINTSPC(9)"{BLK}RICHTUNGEN : {WHT}"RI$(P):GOTO750
748 REM **** KOMMANDOAUSWERTUNG ****
749 FORI=1TO2500:NEXT
750 ZA=0:GOSUB7000
755 GOSUB860:INPUT"{YEL} KOMMANDO :";K$
760 IFLEFT$(K$,1)=" "THENK$=RIGHT$(K$,LEN(K$)-1):GOTO760
765 IFLEN(K$)=1THEN870
770 FORI=1TOLEN(K$):IFMID$(K$,I,1)=" "THEN780
772 NEXT
775 GOSUB860:PRINT"          UNKORREKTE EINGABE !":GOTO749
780 V$=LEFT$(K$,I-1):H$=RIGHT$(K$,LEN(K$)-I)
785 IFLEFT$(H$,1)=" "THENH$=RIGHT$(H$,LEN(H$)-1):GOTO785
790 IFV$="SAGE"THEN6500
795 FORI=1TO14:IFLEFT$(V$(I),LEN(V$))=V$THENX=I:GOTO805
800 NEXT:NV$=V$:GOTO815
805 FORI=1TO21:IFLEFT$(H$(I),LEN(H$))=H$THENY=I:GOTO825
810 NEXT:NV$=H$
815 GOSUB860:PRINT"{UP} TUT MIR LEID, ABER ICH VERSTEHE"
820 PRINT"{DOWN} '"NV$"' NICHT.":GOTO749
825 IFX>9THENX=X-5
830 FORI=1TOLEN(KB$(X)):IFMID$(KB$(X),I,1)=CHR$(Y+64)THEN950
835 NEXT:GOSUB860
840 PRINT"{UP} TUT MIR LEID, ABER ICH VERSTEHE IHREN"
845 PRINT"{DOWN} BEFEHL NICHT.":GOTO749
860 POKE214,20:PRINT:FORI=1TO3:PRINTEF$:NEXT:PRINT"{UP}{UP}{UP}":RETURN
869 REM **** BEWEGEN ****
870 FORI=1TO6:IFK$=MID$("NSWOHR",I,1)THEN880
875 NEXT:GOTO775
880 IFGS=1ORZB=1ORMR=1THEN7600
882 FORI=1TOLEN(RI$(P))STEP2
885 IFMID$(RI$(P),I,1)=K$THEN900
890 NEXT:GOSUB860
895 PRINT"   IN DIESE RICHTUNG GEHT ES NICHT":GOTO749
900 SC=0:ZI=VAL(MID$(ZI$(P),I,2))
905 IFP=16ANDZI=13THEN925
910 IFP=13ANDZI=9ANDG(8)=0ANDV=1THEN7660
915 IFZI=0THEN7680
920 P=ZI:GOTO180
925 IFG(8)=0THENONVGOTO930,7650,7670
930 GOSUB860:PRINT"{UP} HINTER IHNEN LOEST SICH EINE STEIN-"
935 PRINT"{DOWN} LAWINE, DIE DEN WEG VERSCHUETTET.":RI$(16)="W-O":ZI$(16)="1517"
940 FORI=1TO2500:NEXT:P=ZI:GOTO180
949 REM ** SPRUNG ZUR BEF.AUSFUEHRUNG **
950 IFX=4ANDY<13THEN7100
955 IFX=5THEN7200
960 ZN=1000+(X-1)*500+(Y-1)*20:ZN$=STR$(ZN)
965 FORI=1TO4:NZ=VAL(MID$(ZN$,I+1,1))
970 POKE(2060+I),(NZ+48)
975 NEXT
980 GOSUB860:GOTO1
998 REM *** BEFEHLSAUSFUEHRUNG ***
999 PRINT"  OK !":GOTO749
1000 IFY=1ORY=7ORY=16ORY=19ORY=20THENE$="E ":GOTO1002
1001 E$=" "
1002 PRINT" HIER IST KEIN"E$H$(Y):GOTO749
1020 IFG(Y)=0ORG(Y)=PTHEN999
1022 GOTO1000
1060 GOTO1020
1180 IFG(10)<>0ANDG(10)<>PTHEN1000
1182 IFV<>2THENPRINT" DER "H$(10)" LAESST SICH NICHT OEFFNEN":GOTO749
1184 IFG(9)<>21THEN999
1186 PRINT" IM "H$(10)" BEFINDET SICH EIN "H$(9)" !!":G(9)=P:GOTO749
1300 IFP=18ANDRI$(18)="H"ANDV=3THEN1306
1302 IFP=1ORP>13THEN1000
1304 PRINT" HIER IST KEINE GESCHLOSSENE TUER":GOTO749
1306 IFG(11)<>0THENPRINT" DIE TUER IST VERSCHLOSSEN !":GOTO749
1308 RI$(18)="O-H":ZI$(18)="1909":RI$(19)="S-W":ZI$(19)="1718":GOTO180
1340 IFP<>11THEN1000
1342 IFV=3ORG(10)<>21THEN999
1344 IFG(11)<>0THENPRINT" DER SCHRANK IST VERSCHLOSSEN":GOTO749
1346 PRINT"{UP}   IM SCHRANK BEFINDET SICH"
1348 PRINT"{DOWN}   EIN GOLDENER BALL":G(10)=11:GOTO749
1400 PRINT" DAS IST UNMOEGLICH !":GOTO749
1580 IFG(5)<>PTHEN1000
1582 IFP=18THENPRINT" ES IST ZU DUNKEL HIER !":GOTO749
1584 IFG(2)=0THEN1590
1586 PRINT"{UP} SIE BRAUCHEN EINEN BEHAELTER"
1588 PRINT"{DOWN} UM DEN FROSCH ZU FANGEN !":GOTO749
1590 IFGG=5THENPRINT" SIE TRAGEN SCHON 5 GEGENSTAENDE !":GOTO749
1592 G(5)=0:GG=GG+1:PRINT" OK !":GOTO749
1740 IFMR=1THEN7640
1742 IFV=3ANDP=9ANDMO=0THENPRINT" VON HIER AUS GEHT DAS NICHT !":GOTO749
1744 GOTO1000
1780 IFGS<>1THEN1000
1782 PRINT"  DER GEIST MAG DAS NICHT !!":FORI=1TO2000:NEXT:GOTO7630
2080 IFG(5)=PTHENPRINT" DER FROSCH LAESST SICH NICHT KUESSEN":GOTO749
2082 IFG(5)<>0THEN1000
2084 IFV<>1ORP<>6THEN999
2086 PRINT" ES KNALLT UND STINKT...":FORI=1TO2000:NEXT:GOTO7800
2240 IFMR<>1THEN1000
2242 PRINT"{UP} DAS MONSTER SCHLAEGT WILD UM SICH"
2244 PRINT"{DOWN} UND SCHLEUDERT SIE AUS DEM BRUNNEN":MR=0
2246 FORI=1TO3000:NEXT:P=9:GOTO180
2260 IFZB<>1THEN1000
2262 FORI=0TO15:POKE53281,I:POKE53280,I:FORJ=1TO100:NEXT:NEXT:ZB=0
2264 POKE53280,5:POKE53281,5:ZK=ZK+1:IFZK=3THEN7620
2266 P=2+INT(13*RND(TI)):GOTO180
2280 IFGS<>1THEN1000
2282 IFTI$<"001000"THEN7630
2284 GE=1:GS=0:FORI=832TO892STEP3:POKEI,0:POKEI+1,0:POKEI+2,0:NEXT
2286 PRINT"{UP}  DER GEIST HAT IHREN MUNDGERUCH":PRINT"{DOWN}  NICHT VERTRAGEN !"
2288 GOTO749
2400 IFP<>10ORFA=1THEN1000
2402 IFV<>1THEN999
2404 IFV=1THENFA=1:RI$(10)="N-W-R":ZI$(10)="070919"
2406 RI$(19)="S-H":ZI$(19)="1710":GOTO180
2820 IFP<>3ORLEN(RI$(3))=5THEN1000
2822 PRINT" DAS GEHT NICHT !":GOTO749
2840 IFP<>11THEN1000
2842 PRINT" DER SCHRANK IST ZU SCHWER !":GOTO749
2900 IFP<>10ORFA=1THEN1000
2902 GOTO2822
3560 IFGS=1THEN7630
3562 IFG(4)<>0THENPRINT" ICH HABE DAS BUCH NICHT !":GOTO749
3564 IFV=3THEN3568
3566 PRINT" ZAUBERWORT : "Z1$:GOTO749
3568 PRINT"{UP} ZAUBERWOERTER : "Z1$:PRINT"{DOWN}"SPC(17)Z2$:GOTO749
3660 IFG(9)<>0THENPRINT" ICH HABE KEINEN ZETTEL":GOTO749
3662 PRINT" ZAUBERWORT: "Z2$:FORI=1TO2500:NEXT
3664 PRINT"{UP} DER ZETTEL VERSCHWINDET !!!":GG=GG-1:G(9)=21:GOTO749
3860 IFP<>12ORSC=0THEN1000
3862 PRINT"{UP}  TUT MIR LEID, ABER ES SCHEINT"
3864 PRINT"{DOWN}  EINE FREMDE SPRACHE ZU SEIN":GOTO749
4300 IFP<12OR(V=3ANDP=18)THENPRINT" OK...NICHTS PASSIERT !":GOTO749
4302 GOTO1000
4320 IFP<>3ORLEN(RI$(3))=5THEN1000
4322 PRINT"{UP} HINTER DEM "H$(17)
4324 PRINT"{DOWN} SCHEINT EIN HOHLRAUM ZU SEIN !":GOTO749
4380 IFV=2ANDP=12ANDLEN(RI$(12))=3THEN4388
4382 IFP=19ANDG(8)=21THEN4392
4384 IFP>14ANDP<18THEN1000
4386 PRINT" OK...NICHTS BESONDERES FESTZUSTELLEN !":GOTO749
4388 POKE214,11:PRINT:PRINTSPC(18)"{YEL}{RVON}U{CBM-Q}/W":GOSUB860
4390 PRINT" EINE SCHRIFT ERSCHEINT...":SC=1:GOTO749
4392 G(8)=19:PRINT" AN DER WAND HAENGT EIN SCHWERT !":GOTO749
4400 IFP=10ANDFA=0THENPRINT" KLINGT HOHL...":GOTO749
4402 GOTO1000
4580 IFG(5)<>0ANDG(5)<>PTHEN1000
4582 PRINT" DER FROSCH ENTWISCHT IHNEN !"
4584 IFG(5)<>18ORV<>1THEN4588
4586 GOTO749
4588 IFG(5)=0THENGG=GG-1
4590 G(5)=1+INT(19*RND(1)):GOTO749
4740 IFMR=0THEN1000
4742 PRINT" GUTE IDEE, ABER DAS GEHT NICHT !":GOTO749
4760 IFZB=0THEN1000
4762 GOSUB4770:IFY=0ORG(Y)<>0THEN749
4764 IFY=8ANDZA=2THEN7820
4766 GOTO7620
4770 INPUT" WOMIT ";WM$
4771 FORI=1TOLEN(WM$):IFMID$(WM$,I,1)=" "THEN4773
4772 NEXT:GOTO4774
4773 WM$=RIGHT$(WM$,LEN(WM$)-I)
4774 FORY=1TO12:IFWM$=LEFT$(H$(Y),LEN(WM$))THEN4776
4775 NEXT:GOSUB860:PRINT" ICH VERSTEHE '"WM$"' NICHT !":Y=0:RETURN
4776 IFG(Y)<>0THENGOSUB860:PRINT" ICH HABE DIESEN GEGENSTAND NICHT !"
4777 RETURN
4780 IFGS=0THEN1000
4782 GOSUB4770:IFY=0ORG(Y)<>0THEN749
4784 GOSUB860:ONVGOTO4785,4790,4794
4785 IFG(8)=0THENPRINT" SIE SIND GESTOLPERT, UND...":FORI=1TO2000:NEXT:GOTO7630
4786 IFZA=1ANDY=7THEN4798
4788 GOTO7630
4790 IFZA=1ANDY=3THEN4798
4792 GOTO7630
4794 IFY=1THENPRINT" DER GEIST IST TOT, ABER...":FORI=1TO2000:NEXT
4796 GOTO7630
4798 POKEVI+21,0:GS=0:GE=1:PRINT" OK...DER GEIST IST TOT !":GOTO749
5000 IFG(1)<>0ANDG(1)<>PTHEN1000
5002 PRINT" WOZU SOLLTE DAS GUT SEIN ?":GOTO749
5020 IFG(2)<>0ANDG(2)<>PTHEN1000
5022 PRINT"{UP} SO GEHT MAN NICHT MIT FREMDEM"
5024 PRINT"{DOWN} EIGENTUM UM !":GOTO749
5300 IFP=18ANDRI$(18)="H"ANDV=3THEN5306
5302 IFP=1ORP>13THEN1000
5304 PRINT"WIESO, HIER IST KEINE VERSCHLOSSENE TUER":GOTO749
5306 PRINT" DIE TUER IST ZU STABIL...":GOTO749
5320 GOSUB4770:IFY=0ORG(Y)<>0THEN749
5322 IF(V=1ANDY=8)OR(V=3ANDZA=2ANDY=8)THEN7247
5324 GOSUB860:PRINT" DAS GEHT NICHT !":GOTO749
5340 IFP<>11THEN1000
5342 PRINT"{UP} DER SCHRANK LAESST SICH NICHT"
5344 PRINT"{DOWN} MIT GEWALT OEFFNEN !":GOTO749
5380 IFP>14ANDP<18THEN1000
5382 PRINT"SIE KOENNEN DIE WAND NICHT ZERSTOEREN !":GOTO749
6499 REM *** BEFEHL 'SAGE' ***
6500 GOSUB860:IFH$=Z1$THENZA=1:GOTO6506
6502 IFH$=Z2$THENZA=2:GOTO6506
6504 GOTO7690
6506 ONVGOTO6508,6512,6520
6508 IFZA=1ANDG(7)=0ANDGS=1ANDG(4)=0THENPRINT" DIE KEULE VIBRIERT...":GOTO6528
6510 GOTO6526
6512 IFZA=1ANDP=12ANDSC=1THENRI$(12)="N-W-O":ZI$(12)="080013":GOTO415
6514 IFZA=1ANDG(3)=0ANDGS=1THENPOKEVI+39,0:GOTO755
6516 IFZA=2ANDG(8)=0ANDZB=1ANDG(4)=0THENPRINT" DAS SCHWERT LEUCHTET...":GOTO6528
6518 GOTO6526
6520 IFZA=1ANDG(3)=0ANDMO=0ANDP=9THENPRINT" DAS AMULETT LEUCHTET...":GOTO6528
6522 IFZA<>2ORG(4)<>0ORP<>3ORRI$(3)<>"W-O"THEN6526
6524 PRINT" DER SPIEGEL LEUCHTET...":GOTO6528
6526 PRINT" OK...ES TUT SICH NICHTS !":GOTO749
6528 FORI=1TO2000:NEXT:GOTO755
6999 REM * BESITZ/GEGENSTD ANZEIGEN *
7000 PRINT"{HOME}";:FORI=1TO20:PRINT"        ":NEXT
7002 PRINT"{HOME}{BLK}{DOWN}SIE":PRINT"{DOWN}BESITZEN":SP=0
7005 FORI=1TO12:IFG(I)=0THENGOSUB7050
7010 NEXT
7020 PRINT"{HOME}";:FORI=1TO20:PRINTSPC(32)"       ":NEXT
7022 J=0:PRINT"{HOME}{BLK}{DOWN}"SPC(32)"ES GIBT":PRINTSPC(32)"{DOWN}HIER :":SP=32
7025 FORI=1TO12:IFG(I)=PTHENGOSUB7050:J=J+1:IFJ=6THENRETURN
7030 NEXT:RETURN
7050 IFI=1THENPRINTSPC(SP)"{DOWN}GLAS-":PRINTSPC(SP)"KUGEL":RETURN
7055 IFI=11THENPRINTSPC(SP)"{DOWN}SCHLUES":PRINTSPC(SP)"SEL":RETURN
7060 IFI=12THENPRINTSPC(SP)"{DOWN}KETTEN-":PRINTSPC(SP)"HEMD":RETURN
7065 PRINTSPC(SP)"{DOWN}"G$(I):RETURN
7099 REM *** BEFEHL 'NIMM' ***
7100 IFG(Y)=PTHEN7110
7105 GOSUB860:PRINT"  DIESER GEGENSTAND IST NICHT HIER !":GOTO749
7110 IFGG<5THEN7120
7115 GOSUB860:PRINT"  SIE HABEN BEREITS 5 GEGENSTAENDE !":GOTO749
7120 IFY=5THENGOSUB860:PRINT"    DER FROSCH HUEPFT IHNEN DAVON !":GOTO749
7125 G(Y)=0:GG=GG+1:GOSUB860:PRINT"   OK !":FORI=1TO1000:NEXT:GOTO750
7199 REM *** BEFEHL 'WIRF' ***
7200 IFG(Y)<>0THENGOSUB860:PRINT" ICH HABE DIESEN GEGENSTAND NICHT !":GOTO749
7205 GOSUB860:INPUT"  WOHIN ";WH$
7210 FORI=1TOLEN(WH$):IFMID$(WH$,I,1)=" "THEN7220
7215 NEXT:GOTO7225
7220 WH$=RIGHT$(WH$,LEN(WH$)-I)
7225 IFWH$="BODEN"ORWH$="WEG"ORWH$="EGAL"ORWH$="WAND"THEN7235
7230 ONVGOTO7240,7270,7330
7235 GOSUB860:PRINT"  OK !":G(Y)=P:GG=GG-1:GOTO749
7240 IFY<>8ORWH$<>"SPIEGEL"ORP<>3ORLEN(RI$(3))=5THEN7255
7245 G(Y)=3:GG=GG-1
7247 RI$(3)="N-W-O":ZI$(3)="010204":GOSUB860
7250 PRINT"  DER SPIEGEL ZERBRICHT !":FORI=1TO2000:NEXT:GOTO180
7255 IFY<>10ORWH$<>"BRUNNEN"ORP<>9ORG(5)<>18THEN7380
7260 GOSUB860:PRINT"{UP}  AUS DEM BRUNNEN KOMMT EIN FROSCH"
7265 PRINT"{DOWN}  MIT DEM BALL IM MUND.":G(10)=9:G(5)=9:GG=GG-1:GOTO749
7270 IFY<>3ORWH$<>"GEIST"ORGS<>1THEN7290
7275 IFZA<>1THEN7630
7280 POKEVI+21,0:GOSUB860:PRINT"    SIE HABEN DEN GEIST BESIEGT !"
7285 GE=1:GS=0:G(Y)=7:GG=GG-1:GOTO749
7290 IFY<>6ORP<>10ORWH$<>"QUADRAT"ORFA=1THEN7305
7295 FA=1:G(6)=10:GG=GG-1:RI$(10)="N-W-R":ZI$(10)="070919"
7300 RI$(19)="S-H":ZI$(19)="1710":GOTO180
7305 IFY<>4ORP<>3ORWH$<>"SPIEGEL"ORLEN(RI$(3))=5THEN7315
7310 GOTO7245
7315 IFY<>8ORZB<>1ORWH$<>"ZAUBERER"THEN7380
7320 IFZA<>2THEN7620
7325 POKEVI+21,0:FORI=1TO2000:NEXT:GOTO7820
7330 IFY<>1ORWH$<>"GEIST"ORGS<>1THEN7340
7335 GOTO7280
7340 IFY<>3ORWH$<>"BRUNNEN"ORP<>9THEN7360
7345 G(3)=18:GG=GG-1:GOSUB860:IFMO=1THENPRINT"  OK !":GOTO749
7350 IFZA=1THENPRINT" AUS DEM BRUNNEN ERTOENT EIN SCHREI...":MO=1:GOTO749
7355 G(3)=9:PRINT" DAS AMULETT KOMMT ZURUECKGEFLOGEN !":GOTO749
7360 IFY<>8ORWH$<>"SPIEGEL"ORP<>3ORLEN(RI$(3))=5THEN7380
7365 IFZA=2THEN7245
7370 GOTO7235
7380 IFWH$="BRUNNEN"ANDP=9THENG(Y)=18:GOTO7415
7382 IFWH$="QUADRAT"ANDP=10ANDFA=0THENG(Y)=10:GOTO7415
7385 IFWH$="HOEHLE"ANDP=17THENG(Y)=19:GOTO7415
7390 IFWH$="GEIST"ANDGS=1THEN7630
7395 IFWH$="ZAUBERER"ANDZB=1THEN7620
7400 IFWH$="SPIEGEL"ANDP=3ANDLEN(RI$(3))=3THEN7235
7405 GOSUB860:PRINT" ICH VERSTEHE SIE NICHT..."
7410 FORI=1TO2000:NEXT:GOTO7205
7415 GG=GG-1:GOSUB860:PRINT"  OK !":GOTO749
7499 REM *** ERKLAERUNG ***
7500 PRINT"{CLR}{DOWN} SIE MUESSEN VERSUCHEN, EINE AUFGABE ZU"
7505 PRINT"{DOWN} LOESEN, INDEM SIE DEM COMPUTER SAGEN,"
7510 PRINT"{DOWN} WAS ER TUN SOLL."
7515 PRINT"{DOWN} ZUR  FORTBEWEGUNG  GEBEN  SIE NUR"
7520 PRINT"{DOWN} ABKUERZUNGEN EIN (N-S-W-O-H-R = NORD -":REM N-S-W-O-H-R = NORD -
7525 PRINT"{DOWN} SUED - WEST - OST - HOCH - RUNTER)"
7526 REM SUED - WEST - OST - HOCH - RUNTER
7530 PRINT"{DOWN} ANSONSTEN MUESSEN DIE BEFEHLE AUS ZWEI"
7535 PRINT"{DOWN} WOERTERN BESTEHEN (VERB/HAUPTWORT)"
7540 PRINT"{DOWN} DER COMPUTER VERSTEHT DIE VERBEN :"
7545 PRINT"{DOWN} OEFFNE,FANGE,KUESSE,NIMM,LIES,WIRF,"
7550 PRINT"{DOWN} BERUEHRE/KLOPFE,ERSCHLAGE/TOETE,SAGE"
7555 PRINT"{DOWN} ZERBRECHE/ZERSTOERE   -  FERTIG ???"
7560 POKE198,0:WAIT198,1:GOTO100
7599 REM **** VERLOREN ****
7600 IFZB=1THEN7620
7605 IFGS=1THEN7630
7610 IFMR=1THEN7640
7620 PRINT"{CLR}{BLK}{DOWN} DER ZAUBERER HAT SIE IN EINE KROETE"
7625 PRINT"{DOWN} VERWANDELT.":GOTO7700
7630 PRINT"{CLR}{BLK}{DOWN}     DER GEIST HAT SIE GEBISSEN.":GOTO7700
7640 PRINT"{CLR}{BLK}{DOWN} DAS MONSTER HAT BESCHLOSSEN, SIE"
7645 PRINT"{DOWN} ZU HEIRATEN.":GOTO7700
7650 PRINT"{CLR}{BLK}{DOWN} SIE SIND UNTERWEGS GESTOLPERT UND"
7655 PRINT"{DOWN} HABEN SICH DABEI DEN KLEINEN FINGER":PRINT"{DOWN} VERSTAUCHT.":GOTO7700
7660 PRINT"{CLR}{BLK}{DOWN} AUS DEM TORBOGEN HAT SICH EIN STEIN"
7665 PRINT"{DOWN} GELOEST UND IST IHNEN AUF DEN FUSS":PRINT"{DOWN} GEFALLEN.":GOTO7700
7670 PRINT"{CLR}{BLK}{DOWN} SIE SIND IN EINEN ERDRUTSCH GERATEN"
7675 PRINT"{DOWN} UND HABEN SICH IHRE NEUE HOSE DRECKIG":PRINT"{DOWN} GEMACHT.":GOTO7700
7680 PRINT"{CLR}{BLK}{DOWN} SIE SIND VOM BERG GEFALLEN UND HABEN"
7685 PRINT"{DOWN} DABEI IHRE BRILLE ZERBROCHEN.":GOTO7700
7690 PRINT"{CLR}{BLK}{DOWN}        SIE HABEN SICH IN EINE"
7695 PRINT"{DOWN}          BANANE VERWANDELT !"
7700 PRINT"{DOWN} DESHALB MUESSEN SIE LEIDER AUFGEBEN."
7705 PRINT"{DOWN}{DOWN}{DOWN}{YEL} MOECHTEN SIE ES NOCH EINMAL PROBIEREN ?"
7710 GETI$:IFI$="J"THENRUN5
7715 IFI$<>"N"THEN7710
7720 END
7799 REM **** GEWONNEN ****
7800 PRINT"{CLR}{YEL}{DOWN}        ... UND  DER  "H$(5)" ...":FORI=1TO1500:NEXT
7805 PRINT"{UP}         ... VERWANDELT  SICH ...":FORI=1TO1500:NEXT
7810 PRINT"{UP}... IN EINE WUNDERSCHOENE PRINZESSIN ...":FORI=1TO2000:NEXT
7815 PRINT"{CLR}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}":GOTO7840
7820 POKEVI+21,0:PRINT"{CLR}{YEL}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}    SIE HABEN DEN ZAUBERER BESIEGT !!!":GOTO7840
7830 PRINT"{CLR}{YEL}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}{DOWN}    SIE HABEN DEN SCHATZ GEFUNDEN !!!"
7840 PRINTSPC(13)"{DOWN}{DOWN}GRATULIERE,":PRINT"{DOWN}     SIE HABEN IHR ZIEL ERREICHT !!"
7850 PRINT"{DOWN}{DOWN}{DOWN}      WOLLEN SIE WEITER SPIELEN ?"
7860 GETI$:IFI$="J"THENRUN5
7870 IFI$<>"N"THEN7860
7880 END
8000 DATAOEFFNE,BDJPRU,FANGE,EMO,KUESSE,EMNOU,NIMM,ABCDEFGHIJKLQRU
8005 DATAWIRF,ABCDEFGHIJKL,LIES,DIS,BERUEHRE,PQTU,TOETE,EMNO
8010 DATAZERSTOERE,ABPQRT,WERFE,LESE,KLOPFE,ERSCHLAGE,ZERBRECHE
8015 DATAGLASKUGEL,KAEFIG,AMULETT,BUCH,FROSCH,HELM,KEULE,SCHWERT,ZETTEL,BALL
8020 DATASCHLUESSEL,KETTENHEMD,MONSTER,ZAUBERER,GEIST,TUER,SPIEGEL,SCHRANK
8025 DATASCHRIFT,WAND,QUADRAT
8030 DATAS,03,S-O,0503,W-O,0204,W-H,0311,N-S,0208,O,07,S-W,1006,N-O,0509
8035 DATAS-W-O-R,13081018,N-W,0709,R,04,W-O,0013,N-W-O,091214,W-O,1300,O,16
8040 DATAW-O-H,151713,N-W,1916,H,09,S,17
8045 DATA20,20,20,20,18,2,21,21,21,21,6,5
8050 DATASILBER,BLUME,GOLD,RING,KUPFER,HORN,PLATIN,STERN
8054 REM SPRITE 1 - GEIST
8055 DATA0,127,0,0,255,192,1,206,96,3,255,240,1,255,240,1,245,248,1,255,248
8060 DATA3,255,248,7,255,248,15,255,252,31,255,254,3,255,240,3,255,224
8065 DATA3,255,224,7,255,224,7,255,224,7,255,240,15,255,240,15,255,248
8070 DATA31,255,252,59,187,110
8074 REM SPRITE 2 - MONSTER
8075 DATA12,0,48,30,0,120,3,0,192,1,129,128,3,195,192,15,231,240,62,126,124
8080 DATA124,60,62,248,60,31,254,126,127,255,255,255,127,255,254,127,255,254
8085 DATA63,90,252,30,165,120,31,255,248,7,255,224,4,126,32,4,60,32
8090 DATA31,0,248,42,129,84
8094 REM SPRITE 3 - ZAUBERER
8095 DATA0,15,224,0,63,128,0,127,128,0,219,0,1,255,0,3,255,128,31,255,240
8100 DATA127,255,252,127,255,252,111,255,236,111,255,236,103,255,204,98,170,140
8105 DATA99,255,140,71,255,196,7,199,192,15,131,224,15,1,224,14,0,224
8110 DATA28,0,112,60,0,120
8200 REM
8201 REM **********************
8202 REM *   CASTLE OF DOOM   *
8203 REM * VON B. WEISSBECKER *
8204 REM *  MAERZ/APRIL 1984  *
8205 REM **********************
</pre>
            <figcaption>Listing des Monats: Castle of Doom (Fortsetzung)</figcaption>
        </figure>

    </article>

</body>

</html>

<!--
    Der Autor heißt Bernhard Weißbecker. Im Fließtext steht es richtig;
    in der Autorenangabe falsch.
 -->
