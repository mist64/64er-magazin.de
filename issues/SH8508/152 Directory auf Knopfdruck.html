<!DOCTYPE html>
<html lang="de">

<head>
    <title>Directory auf Knopfdruck</title>
    <meta charset="UTF-8">
    <link rel="stylesheet" href="../style.css">
    <meta name="author" content="ah">
    <meta name="64er.issue" content="Sonderheft 8/85">
    <meta name="64er.pages" content="152-153">
    <meta name="64er.toc_category" content="Tips und Tricks">
    <!-- <meta name="64er.toc_title" content="XXX"> -->
    <meta name="64er.id" content="directory">
</head>

<body>
    <article>
        <h1>Directory auf Knopfdruck</h1>
        <p class="intro">Häufig steht man vor dem Problem, Programme direkt, also ohne SYS-Aufrufe, über einen Tastencode zu aktivieren. Wir zeigen Ihnen, wie man ein Directory auf dem Bildschirm ausgibt, ohne ein vorhandenes Basic-Programm zu zerstören.</p>

        <p>Das Programm »Directory auf Knopfdruck« (siehe Listing 1) kann, nachdem es mit Hypra-Assassembliert wurde, mit dem Befehl SYS 49152 aktiviert werden.</p>

        <p>Den erzeugten Maschinencode zeigt Listing 2, das auch direkt mit dem MSE eingegeben werden kann. Solange kein Reset ausgelöst oder die RUN/STOP-Taste gedrückt wird, erscheint bei der Tastenkombination »CTRL-D« das Directory auf dem Bildschirm, ohne ein eventuell im RAM stehendes Basic-Programm zu zerstören.</p>

        <p>Der hier vorgestellte Lösungsweg, ein beliebiges Programm auf Tastendruck aufzurufen, hat den Vorteil, daß er allgemeingültig ist und nicht nur auf die Ausgabe des Directorys beschränkt ist.</p>

        <p>Im allgemeinen müßte das Betriebssystem geändert werden, um eine zusätzliche Tastaturabfrage einzubinden. Aber wir haben Glück, denn einige Routinen im Betriebssystem werden über einen sogenannten Sprungvektor aufgerufen. Alle zugänglichen Sprungvektoren, die sich ändern lassen, befinden sich in der dritten Page, also im Bereich von $311 bis $333, jeweils im Low-/High-Byte-Format. Der zum Einbinden einer Tastaturabfrage geeignetste Vektor ist der Interruptvektor, denn diese sogenannte Interrupt-Routine wird 60mal pro Sekunde angesprungen. Ein Sprungvektor, der in diese Routine verzweigt, befindet sich in den Speicherzellen $314/$315. Die Speicherzellen enthalten im Normalzustand den Wert $31/$EA. Das bedeutet, daß immer, wenn ein Interrupt ausgelöst wird, zu einer Routine verzweigt wird, die bei $EA31 liegt. Schreibt man nun in die Speicherzellen $314/ $315 einen anderen Wert, wird bei einem Interrupt nicht mehr zur Adresse $EA31, sondern zu der Adresse verzweigt, die in den Speicherzellen $314/315 steht, wobei die Adresse $314das Low- und die Speicherzelle $315 das High-Byte der anzuspringenden, neuen Interrupt-Routine enthält. Damit sich der Vektor aber verändern läßt, muß zuerst der normale Interrupt gesperrt werden, denn sonst könnte folgendes passieren: Angenommen, es wird ein Interrupt ausgelöst, nachdem erst das Low-Byte in die Adresse $314 geschrieben wurde, dann nimmt der Computer an, daß die neue Interrupt-Routine bei $EAOO plus dem in Adresse $314 stehendem Low-Byte liegt, verzweigt an diese Adresse und stürzt ab.</p>

        <p>Mit dem Maschinenbefehl »SEI« für SEt Interruptflag läßt sich nun der prozessorinterne Interrupt ausschalten. Dies wird in der Zeile 230 im Listing 1 realisiert. Die Zeilen 240 bis 270 sorgen dafür, daß die Startadresse der neuen Interrupt-Routine in die entsprechenden Adressen kommt. Eine Bemerkung noch zu den Zeilen 240 und 270. Durch das »kleiner«- beziehungsweise »größer«-Zeichen läßt sich eine 16-Bit-Adresse (in diesem Fall »INTER«) in zwei 8-Bit-Zahlen splitten. Durch das »kleiner«-Zeichen erhält man das Low- und entsprechend für das »größer«-Zeichen das High-Byte.</p>

        <p>Der folgende »CLI«-Befehl (CLear Interruptflag) läßt nun den normalen Interrupt wieder zu. Nur wird jetzt, wenn ein Interruptstattfindet, nicht mehr zur Adresse $EA31, sondern zur Routine »INTER« verzweigt. Die Aufgabe des Teilprogramms »INTER« ist es, die »CTRL«- und »D«-Taste abzufragen. Sind beide Tasten gleichzeitig gedrückt, wird nach »DIR« verzweigt, ansonsten in der Zeile 420 zum normalen Interrupt nach $EA31. Die Zeilen 370 und 380 beziehungsweise 400 und 410 sperren auch den Interrupt, aber auf eine etwas andere Art.</p>

        <p>Sie werden sich vielleicht fragen, warum der Interrupt überhaupt gesperrt werden muß. Die Antwort auf diese Frage ist leicht zu geben. Die Bearbeitung der Routine »DIR« dauert länger als 1/60 Sekunde. Dadurch würde bereits ein neuer Interrupt ausgelöst, bevorderalteabgearbeitetwäre. Die Art, wie der Interrupt abgeschaltet wird, wurde mit Absicht so gewählt, da einige Routinen im Betriebssystem den Interrupt durch den CLI-Befehl wieder zulassen, so auch die Routine »CHROUT«, die ein Zeichen auf einem Gerät, hier dem Bildschirm, ausgibt. Durch die Befehle »LDA #TIMERAUS« und »STA ICR« wird eigentlich nicht der Interrupt abgeschaltet, sondern der Timer gestoppt, der den Interrupt auslöst. Doch nun zum eigentlichen »DIR«-Programm.</p>

        <p>Die Zeilen 450 bis 550 entsprechen dem Basic-Befehl »OPEN 1,8,0,"$"«. Dabei liegt das Augenmerk auf der Sekundäradresse »0« und dem Dollarzeichen. Das Dollarzeichen als Filename bewirkt, daß das Directory eingelesen wird und zwar einschließlich Track 18 Sektor 0. Durch die »0« als Sekundäradresse wird das Directory schon soweit aufbereitet, daß nur noch die Zeichen, die sich nach einem »LOAD"$",8« tatsächlich im RAM befinden, zum Computer gesendet werden, inklusive Filetyp, Steuerzeichen, Leerzeichen und Anzahl der Blöcke pro File. Leider enthält ein vom Floppy-Laufwerk gesendeter Directory-Eintrag auch noch Platzhalter für die Zeilenlinkadressen, die wir natürlich nicht brauchen und folglich ausblenden beziehungsweise überlesen müssen. Eine vom Floppy-Laufwerk gesendete Directory-Zeile hat folgendes Format:</p>

        <p>LI LI AB SP "filename SP" SP FT</p>

        <p>LI = Linkbyte; AB = Anzahl der Blöcke pro File im Low/ High-Byte-Format; SP = Leerzeichen; FT = Filetyp im Klartext.</p>

        <p>Das einzige, was zu tun bleibt, ist die Ausgabe »RETURN« am Ende jeder Directory-Zeile (Zeile 811/812). Außerdem muß die Information, die die Anzahl der Blöcke pro File enthält, in dezimale Schreibweise umgerechnet und auf dem Bildschirm ausgegeben werden. Das ist aber kein Problem, denn das Betriebssystem stellt eine solche Routine »INTOUT=$BDCD« zur Verfügung. Wenn sie aufgerufen wird, muß der Akku das High- und das X-Register das Low-Byte der auszugebenden Zahl enthalten. Das Low-Byte wird in Zeile 590 an das X-Register übergeben. Das High-Byte dagegen wird in Zeile 630 durch das Unterprogramm »CHRIN« eingelesen und steht automatisch im Akku.</p>

        <h3>Zum Programmablauf</h3>

        <p>Die Zeilen 570 bis 620 überlesen 4 Byte beim ersten Durchlauf. Bei allen weiteren Durchläufen werden immer nur die ersten 2 Byte überlesen, die die Zeilenlinkadresse enthalten. Beim ersten Durchlauf wird neben der Zeilenlinkadresse auch die Startadresse ($0801) gesendet, die ebenfalls ausgeblendet werden muß, darum 4 Byte. Bei einem Sprung auf das Label »ROWOUT« enthält das Y-Register die Anzahl der Byte, die überlesen werden sollen plus 1. Wird die Schleife in Zeile 620 verlassen, enthält das X-Register das Low-Byte der Zahl, die die Anzahl der Blöcke pro File kennzeichnet. Das High-Byte wird in Zeile 630 eingelesen und an den Akku übergeben. Die nächste Schleife, die bei dem Label »NEXTCHR« beginnt, gibt den Rest eines Directory-Eintrags auf dem Bildschirm aus. Der »CMP«-Befehl in Zeile 730 überprüft, ob eine »0«, die das Ende des Eintrags kennzeichnet, gesendet wurde oder nicht. Bei einer »0« wird nach »LASTCHR« verzweigt, ein »RETURN« ausgegeben und zum Label »ROWOUT« gesprungen.</p>

        <address class="author">(ah)</address>


        <figure>
            <pre>
10  -.EQ IRQVEKLO = $314
20  -.EQ IRQVEKHI = $315
30  -.EQ SHIFTFLAG = $28D
40  -.EQ TIMERAUS = $80
50  -.EQ TIMEREIN = 129
60  -.EQ ICR = $DC0E
70  -.EQ INTERRUPTALT = $EA31
80  -.EQ SETNAM = $FFBD
90  -.EQ SETLFS = $FFBA
100  -.EQ OPEN = $FFC0
110  -.EQ CHKIN = $FFC6
120  -.EQ CHRIN = $FFCF
130  -.EQ INTOUT = $BDCD
140  -.EQ CHROUT = $FFD2
150  -.EQ CLOSE = $FFC3
151  -.EQ CLRCHN = $FFCC
160  -.EQ STATUS = $90
170  -.EQ RETTEN = $FB
180  -.EQ TASTENCODE = $C5
190  -;
200  -;
210  -.BA $C000
220  -;
221  -;********************************
222  -;* INTERRUPTVEKTOR NEU SETZEN   *
223  -;********************************
224  -;
230  -          SEI
240  -          LDA #&lt;(INTER)
250  -          STA IRQVEKLO
260  -          LDA #&gt;(INTER)
270  -          STA IRQVEKHI
280  -          CLI
290  -          RTS
300  -;
301  -;********************************
302  -;*    NEUE INTERRUPTROUTINE     *
303  -;********************************
304  -;
310  -INTER     LDA TASTENCODE
320  -          CMP #18        ;IST DIE TASTE &gt;&gt;D&lt;&lt; GEDRUECKT?
330  -          BNE INTERE     ;WENN NEIN DANN ZUM NORMALEN INTERRUPT
340  -          LDA SHIFTFLAG
350  -          CMP #4         ;IST &gt;&gt;CTRL&lt;&lt; GEDRUECKT?
360  -          BNE INTERE     ;WENN NEIN DANN ZUM NORMALEN INTERRUPT
370  -          LDA #TIMERAUS  ;TIMER AUSSCHALTEN/INTERRUPT SPERREN
380  -          STA ICR
390  -          JSR DIR        ;UND ZUM UNTERPROGRAMM DIR
400  -          LDA #TIMEREIN  ;TIMER EINSCHALTEN/INTERRUPT ZULASSEN
410  -          STA ICR
420  -INTERE    JMP INTERRUPTALT
430  -;
440  -;
441  -;********************************
442  -;*         DIRECTORY            *
443  -;*          AUSGEBEN            *
444  -;********************************
445  -;
450  -DIR       LDA #1         ;LAENGE DES FILENAMENS
460  -          LDY #&gt;(NAME)
470  -          LDX #&lt;(NAME)
480  -          JSR SETNAM     ;FILENAMENPARAMETERSETZEN
490  -          LDA #1         ;LOGISCHE FILENUMMER = 1
500  -          LDX #8         ;GERAETEADRESSE = 8
510  -          LDY #0         ;SEKUNDAERADRESSE = 0
520  -          JSR SETLFS     ;LOGISCHE FILENUMMER UND SEKUNDAERADR. SETZEN
530  -          JSR OPEN
540  -          LDX #1         ;LOGISCHE FILENUMMER
550  -          JSR CHKIN      ;EINGABEGERAET SETZEN
560  -          LDY #5         ;DIE ERSTEN 5 ZEICHEN UEBERLESEN
570  -ROWOUT    STY RETTEN
580  -          JSR CHRIN      ;EINGABE EINES ZEICHENS
590  -          TAX            ;LO-BYTE BLOCKANZAHL INS X-REGISTER
600  -          LDY RETTEN
610  -          DEY
620  -          BNE ROWOUT
630  -          JSR CHRIN      ;HI-BYTE BLOCKANZAHL IN DEN AKKU
640  -          LDY STATUS     ;IST FILEENDE SCHON ERREICHT?
650  -          BNE ENDE       ;WENN JA DANN FERTIG
670  -          JSR INTOUT     ;ANZAHL DER BLOECKE AUSGEBEN (X/AKKU)
690  -          LDA #" "       ;BLANK AUSGEBEN
700  -          JSR CHROUT
710  -NEXTCHR   JSR CHRIN      ;NAECHSTES ZEICHEN HOLEN
720  -          STA RETTEN     ;UND RETTEN
730  -          CMP #0         ;IST FILEEINTRAG AUSGEGEBEN?
740  -          BEQ LASTCHR    ;WENN JA DANN RETURN AUSGEBEN
790  -          LDA RETTEN     ;ANSONSTEN MUSS NOCH DAS LETZTE ZEICHEN
800  -          JSR CHROUT     ;AUSGEGEBEN WERDEN
810  -          JMP NEXTCHR
811  -LASTCHR   LDA #13        ;&gt;&gt;RETURN&lt;&lt; AUSGEBEN
812  -          JSR CHROUT
813  -          LDY #$3        ;3 ZEICHEN UEBERLESEN
814  -          JMP ROWOUT
820  -ENDE      JSR CLRCHN     ;EIN- UND AUSGABEKANAELE
830  -          LDA #1         ;UND LOGISCHES FILE SCHLIESSEN
840  -          JSR CLOSE
850  -          RTS
860  -;
870  -NAME      .TX "$"
</pre>
            <figcaption>YYYYYYYYYYYYY</figcaption>
        </figure>
        <div class="binary_download" data-filename="dir.b3.prg" data-name="XXXXXXXXXXXXXXX"></div>


        <!-- 0    "----------------" del  -->

        <!-- makro.sh8 -->
        <figure>
            <pre>
10  -;********************************
20  -;*   WEITERE VERSCHIEBEBEFEHLE  *
30  -;********************************
40  -;
50  -; X -&gt; Y
60  -.MA TXY
70  -          PHA
80  -          TXA
90  -          TAY
100  -          PLA
110  -.RT
120  -;
130  -; Y -&gt; X
140  -.MA TYX
150  -          PHA
160  -          TYA
170  -          TAX
180  -          PLA
190  -.RT
200  -;
210  -; X-REGISTER AUF DEN STACK
220  -.MA PHX
230  -.EQ RETTEN = $FC
240  -          STA RETTEN
250  -          TXA
260  -          PHA
270  -          LDA RETTEN
280  -.RT
290  -;
300  -; Y-REGISTER AUF DEN STACK
310  -.MA PHY
320  -.EQ RETTEN = $FC
330  -          STA RETTEN
340  -          TYA
350  -          PHA
360  -          LDA RETTEN
370  -.RT
380  -;
390  -; X-REGISTER VOM STACK HOLEN
400  -.MA PLX
410  -.EQ RETTEN = $FC
420  -          STA RETTEN
430  -          PLA
440  -          TAX
450  -          LDA RETTEN
460  -.RT
470  -;
480  -; Y-REGISTER VOM STACK HOLEN
490  -.MA PLY
500  -.EQ RETTEN = $FC
510  -          STA RETTEN
520  -          PLA
530  -          TAY
540  -          LDA RETTEN
550  -.RT
560  -;
570  -; DEN AKKU AUF USERSTACK
580  -.MA PUSHA
590  -          ... PHY
600  -          LDY #0
610  -          STA (USER),Y
620  -          ... DECW(USER)
630  -          ... PLY
640  -.RT
650  -;
660  -;AKKU UND Y-REGISTER AUF USERSTACK
670  -.MA PUSHAY
680  -          PHA
690  -          TYA
700  -          ... PUSHA
710  -          PLA
720  -          ... PUSHA
730  -.RT
740  -;
750  -; AKKU VON USERSTACK
760  -.MA PULLA
770  -          ... INCW(USER)
780  -          ... PHY
790  -          LDY #0
800  -          LDA (USER),Y
810  -          ... PLY
820  -.RT
830  -;
840  -;AKKU UND Y-REGISTER VON USERSTACK
850  -.MA PULLAY
860  -          ... PULLA
870  -          TAY
880  -          ... PULLA
890  -.RT
900  -;
910  -;********************************
920  -;*      16-BIT BEFEHLE          *
930  -;********************************
940  -;
950  -; A/Y + ADRESSE = A/Y AKKU=LO
960  -.MA ADW (ADRESSE)
970  -          CLC
980  -          ADC ADRESSE
990  -          PHA
1000  -          TYA
1010  -          ADC ADRESSE+1
1020  -          TAY
1030  -          PLA
1040  -.RT
1050  -;
1060  -; ADIERE ADR1 + ADR2 = SUMME
1070  -.MA ADMW (ADR1,ADR2,SUMME)
1080  -          PHA
1090  -          CLC
1100  -          LDA ADR1
1110  -          ADC ADR2
1120  -          STA SUMME
1130  -          LDA ADR1+1
1140  -          ADC ADR2+1
1150  -          STA SUMME+1
1160  -          PLA
1170  -.RT
1180  -;
1190  -; A/Y - ADRESSE = A/Y AKKU=LO
1200  -.MA SBCW (ADRESS)
1210  -          SEC
1220  -          SBC ADRESSE
1230  -          PHA
1240  -          TYA
1250  -          SBC ADRESSE+1
1260  -          TAY
1270  -          PLA
1280  -.RT
1290  -;
1300  -; ADR1 - ADR2 = DIFFERENZ
1310  -.MA SBCMW (ADR1,ADR2,DIFF)
1320  -          PHA
1330  -          SEC
1340  -          LDA ADR1
1350  -          SBC ADR2
1360  -          STA DIFF
1370  -          LDA ADR1+1
1380  -          SBC ADR2+1
1390  -          STA DIFF+1
1400  -          PLA
1410  -.RT
1420  -;
1430  -; ADRESSE = ADRESSE + 1
1440  -.MA INCW (ADRESSE)
1450  -          PHA
1460  -          LDA ADRESSE
1470  -          CLC
1480  -          ADC #1
1490  -          STA ADRESSE
1500  -          LDA ADRESSE+1
1510  -          ADC #0
1520  -          STA ADRESSE+1
1530  -          PLA
1540  -.RT
1550  -;
1560  -; ADRESSE = ADRESSE - 1
1570  -.MA DECW (ADRESSE)
1580  -          PHA
1590  -          LDA ADRESSE
1600  -          SEC
1610  -          SBC #1
1620  -          STA ADRESSE
1630  -          LDA ADRESSE+1
1640  -          SBC #0
1650  -          STA ADRESSE+1
1660  -          PLA
1670  -.RT
1680  -;
1690  -; ADRESSE -&gt; A/Y AKKU=LO
1700  -.MA LDAY (ADRESSE)
1710  -          LDY ADRESSE+1
1720  -          LDA ADRESSE
1730  -.RT
1740  -;
1750  -; A/Y -&gt; ADRESSE AKKU=LO
1760  -.MA STAY (ADRESSE)
1770  -          STA ADRESSE
1780  -          STY ADRESSE+1
1790  -.RT
1800  -;
1810  -; WERT=16BIT -&gt; A/Y
1820  -.MA LDAYI (WERT)
1830  -          LDA #&lt;(WERT)
1840  -          LDY #&gt;(WERT)
1850  -.RT
1860  -;********************************
1870  -;*  BEFEHLE ZUR STRUKTURIERTEN  *
1880  -;*       PROGRAMMIERUNG         *
1890  -;********************************
1900  -;
1910  -.MA REPEAT
1920  -ACE1      .GL ACE0=ACE1
1930  -.RT
1940  -;
1950  -.MA EXITREPEAT
1960  -          JMP BCE0
1970  -.RT
1980  -;
1990  -.MA UNTIL (UEBERGABE,BEDINGUNG)
2000  -          PHA
2010  -          LDA UEBERGABE
2020  -          CMP #BEDINGUNG
2030  -          BEQ LBL1
2040  -          PLA
2050  -          JMP ACE0
2060  -LBL1      PLA
2070  -.GL BCE0=LBL1
2080  -.RT
2090  -;
2100  -.MA WHILE (UEBERGABE,BEDINGUNG)
2110  -CCE0      .GL CCE0=CCE0
2120  -          PHA
2130  -          LDA UEBERGABE
2140  -          CMP #BEDINGUNG
2150  -          BNE LBL1
2160  -          JMP CCE1
2170  -LBL1      PLA
2180  -.RT
2190  -;
2200  -.MA EXITWHILE
2210  -          PHA
2220  -          JMP CCE1
2230  -.RT
2240  -;
2250  -.MA ENDWHILE
2260  -          JMP CCE0
2270  -CCE1      .GL CCE1=CCE1
2280  -          PLA
2290  -.RT
</pre>
            <figcaption>Listing 1. »Directory auf Knopfdruck«. Der Quelltext wurde mit Hypra-Ass erstellt.</figcaption>
        </figure>
        <div class="binary_download" data-filename="makro.sh8.prg" data-name="Directory auf Knopfdruck (Assembler-Quelltext)"></div>
    </article>
</body>

        <figure>
            <pre data-filename="direktory.obj.prg" data-name="Directory auf Knopfdruck" data-mse=mse1></pre>
            <figcaption>Listing 2. »Directory auf Knopfdruck« muß mit dem MSE eingegeben werden</figcaption>
        </figure>
        <div class="binary_download" data-filename="direktory.obj.prg" data-name="Directory auf Knopfdruck"></div>

</html>
