;Diskontieren ein.prg ==0801==
;Quelle: 1984-08-09-10.d64

    8 poke45,peek(174):poke46,peek(175):clr
    9 clr:dima$(15),a(15):goto1000
   10 z1=3:z2=9:s=13:rem * maske loeschen *
   20 forz=z1toz2:gosub90:print"                          ":next:ifc=0thenreturn
   30 z1=12:z2=20:c=0:goto20
   50 return
   70 a$(12)=str$(int(a(12)*100+.5)):rem * runden
   75 ab$=left$(a$(12),len(a$(12))-2)+"."+right$(a$(12),2):return
   80 load"menue",8:rem *load menue
   90 poke214,z:poke211,s:sys58732:return:rem * cursorposition
   95 a=55869:fori=1597to1622:pokei,100:pokea,1:a=a+1:next:return
  100 rem * auswahlroutine *
  110 gosub90:print"{rvon}           {rvof}":fori=1to75:nexti
  120 gosub90:print"{rvon}"a$(y)"{rvof}":fori=1to125:next
  130 getg$:ifg$<>chr$(133)andg$<>chr$(134)andg$<>chr$(135)andg$<>chr$(13)then110
  140 ifg$=chr$(133)andcthenc=0:return
  150 ifg$=chr$(13)thengosub90:print"{rvon}>{rvof}":return
  160 ifg$=chr$(135)then190
  170 z=z+sw:y=y+1:ifz>z2thenz=z1:y=y1
  180 goto110
  190 z=z-sw:y=y-1:ifz<z1thenz=z2:y=y2
  195 goto110
  200 rem * wahl-fusszeile *:s=0
  210 z=23:gosub90:print"{rvon}          {rvof}";:fori=1to75:next
  220 gosub90:print"{rvon}"ba$(y)"{rvof}";:fori=1to175:next
  230 getg$:ifg$<>chr$(134)andg$<>chr$(135)andg$<>chr$(13)then210
  240 ifg$=chr$(13)thengosub90:print"{rvon}>{rvof}";:return
  250 ifg$=chr$(135)then280
  260 s=s+10:y=y+1:ifs>31thens=0:y=1
  270 goto210
  280 s=s-10:y=y-1:ifs<0thens=30:y=4
  290 goto210
  300 rem * eingaberoutine *
  310 gosub90
  320 in$=""
  330 print"{CBM-I}{left}";
  340 getg$:ifg$=""then340
  350 g=asc(g$):ifg=13then460
  360 ifg=13thenreturn
  370 ifg$<"0"org$>"9"then420
  380 in$=in$+g$
  390 printg$;
  400 iflen(in$)>nthenfori=1tolen(in$):printchr$(20);:next:goto320
  410 goto330
  420 ifg=46then380
  425 ifg$=chr$(133)then9
  430 ifg<>20then330
  440 iflen(in$)<1then330
  450 in$=left$(in$,len(in$)-1):goto390
  460 pn$="":fori=1to(10-len(in$)):pn$=pn$+".":next:printpn$
  470 iflen(in$)<1then310
  480 return
  999 :
 1000 rem ** dikontieren **
 1010 gosub5000:printma$
 1020 a=55388:fori=1076to1906step40:pokei,101:pokea,1:a=a+40:next
 1030 a=55736:fori=1464to1474:pokei,100:pokea,1:a=a+1:next
 1040 a=55749:fori=1477to1502:pokei,100:pokea,1:a=a+1:next
 1050 z=23:s=0:gosub90:printmb$
 1060 z=3:gosub90:print"Auswahl :":print"{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}"
 1070 z=5:fori=1to5:gosub90:print"{rvon}"a$(i)"{rvof}":z=z+1:next
 1080 z=5:z1=z:z2=9:s=0:y=1:y1=y:y2=5:sw=1:c=0:gosub100:f=y:iff=5then80
 1150 iff=1thena$(9)=kn$:a$(10)=p$:a$(11)=ja$:k=1
 1160 iff=2thena$(9)=kb$:a$(10)=p$:a$(11)=ja$:k=1
 1170 iff=3thena$(9)=kn$:a$(10)=kb$:a$(11)=ja$:k=0
 1180 iff=4thena$(9)=kn$:a$(10)=kb$:a$(11)=p$:k=2
 2000 rem * eingabemaske *
 2010 z=3:s=13:gosub90:print"Eingabe :":z=4:gosub90:print"{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}"
 2020 z=5:fori=9to11:s=13:gosub90:print"{rvon}"a$(i)"{rvof} : ";pm$
 2030 z=z+2:next:z=23:s=0:gosub90:printmc$
 2040 z=5:s=27:n=10:gosub300:a(9)=val(in$)
 2050 z=7:gosub300:a(10)=val(in$)
 2060 z=9:gosub300:a(11)=val(in$)
 2070 z=13:s=13:gosub90:print"Eingabe korrekt ? (j/n):":gosub95
 2080 z=13:s=38:gosub90:print"{CBM-I}{left}";
 2090 getg$:ifg$<>"j"andg$<>"n"then2090
 2100 printg$:ifg$="j"theny=1
 2110 ifg$="n"theny=0
 2120 geti$:ifi$<>chr$(13)andi$<>chr$(20)then2120
 2130 ifi$=chr$(20)then2080
 2140 c=1:gosub10:ify=0then2010
 3000 rem * rechnung *
 3010 iff=1thena(12)=a(9)*(1/(1+(a(10)/100))^a(11))
 3020 iff=2thena(12)=a(9)/(1/(1+(a(10)/100))^a(11))
 3030 iff=3thena(12)=((a(9)/a(10))^(1/a(11))-1)*100
 3040 iff=4thena(12)=log(a(9)/a(10))/log(1+(a(11)/100))
 3500 rem * bildschirm
 3510 z=3:s=13:gosub90:print"Eingabe :":z=4:gosub90:print"{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}"
 3520 z=5:fori=9to11:gosub90:printa$(i)" :"a(i):z=z+2:next
 3530 z=13:gosub90:print"Resultat :":z=14:gosub90:print"{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}":gosub70
 3540 z=16:gosub90:print"{rvon}"a$(f)"{rvof} :";:printtab(38-len(ab$));ab$
 3550 printtab(39-len(ab$));:fori=1tolen(ab$)-1:print"=";:next
 3560 z=20:gosub90:ifk=1thenprint"Ausdruck in DM{$a0}!":goto3580
 3570 ifk=0thenprint"Ausdruck in Prozent !"
 3580 z=23:s=0:gosub90:fori=1to4:print"{rvon}"ba$(i)"{rvof}";:next
 3590 y=1:gosub200:ify=4then80
 3600 ify=3then1010
 3610 ify=2then4500
 4000 rem * aenderung *
 4010 c=1:gosub10
 4020 z=3:s=13:gosub90:print"Aenderung :":z=4:gosub90:print"{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}{CBM-T}"
 4030 z=5:s=13:fori=9to11:gosub90:print"{rvon}"a$(i)"{rvof} :";a(i):z=z+2:next
 4040 z=23:s=0:gosub90:printmd$;
 4050 z=5:z1=z:z2=9:s=13:y=9:y1=y:y2=11:sw=2:c=1:gosub100
 4060 ifc=0thengosub10:goto3000
 4070 z=13:s=13:gosub90:print"{rvon}"a$(y)"{rvof} : "pm$:gosub95
 4080 z=23:s=0:gosub90:printmc$;:z=13:s=27:gosub300:a(y)=val(in$):goto4010
 4500 rem * ausdruck *
 4505 ta$=chr$(10):tb$=chr$(16)
 4510 open4,4,7:print#4,chr$(14);tb$"10DISKONTIEREN EINES{$a0}KAPITALS"
 4520 print#4,tb$"10---------------------------"ta$
 4530 fori=9to11:print#4,chr$(15);tb$"10*** "a$(i)" : ";a(i):next
 4540 gosub70:print#4,ta$;tb$"14"a$(f)" : "ab$
 4550 print#4,tb$"15";:fori=1to2+len(a$(f)+ab$):print#4,"=";:next
 4560 print#4,ta$;ta$;ta$
 4570 print#4,chr$(13);tb$"10***   Endkapital und Barwert werden";
 4580 print#4," in ' DM ' ausgedruckt,"
 4590 print#4,tb$"16der Zinssatz in ' % ' !"
 4600 print#4:fori=1to40:print#4,"==";:next:print#4,ta$;ta$;ta$;ta$
 4610 close4:fori=56176to56215:pokei,0:next:goto3580
 5000 rem * variablenliste *
 5010 ma$="{clr}{rvon}      DISKONTIEREN{$a0}EINES{$a0}KAPITALS      {rvof}"
 5020 mb$="{rvon} F3 u. F5 - Wahl ! 'RETURN' - Eingabe !{rvof}"
 5030 mc$="{rvon}  F1 - Neu !      Geben Sie ein !      {rvof}"
 5040 md$="{rvon} F1 - Resultat !     F3 u. F5 - Wahl ! {rvof}"
 5050 kn$=" Endkapital":p$="  Zinssatz ":ja$="  Jahre    ":pm$=".........."
 5060 kb$="  Barwert  "
 5110 ba$(1)="  Aendern "
 5120 ba$(2)="  Kopie   "
 5130 ba$(3)="  Neu     "
 5140 ba$(4)=" Menue    "
 5510 a$(1)="  Barwert  "
 5520 a$(2)=" Endkapital"
 5530 a$(3)="  Zinssatz "
 5540 a$(4)="  Laufzeit "
 5550 a$(5)="  Menue    "
 5590 a$(13)="  Aendern  "
 5600 a$(14)="  Resultat "
 5610 a$(15)="  Menue    ":return

