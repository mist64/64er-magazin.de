<!DOCTYPE html>
<html lang="de">

<head>
    <title>Epson-Support</title>
    <meta charset="UTF-8">
    <link rel="stylesheet" href="../style.css">
    <meta name="author" content="Alexander Del Pino, gk">
    <meta name="64er.issue" content="Sonderheft 4/85">
    <meta name="64er.pages" content="26-30">
    <meta name="64er.toc_category" content="Basic-Erweiterung">
    <meta name="64er.id" content="epson_support">
</head>

<body>
    <article>
        <h1>Epson-Support</h1>
        <p class="intro">Epson-Support läßt die Herzen eines jeden FX/RX-80-Besitzers höher schlagen. Mit einfachen Befehlen und kurzen Programmen können Sie jetzt komplizierte Grafiken auf Ihrem Drucker erstellen. Und das mit einer Auflösung, die fast siebenmal so hoch ist wie die des C 64, nämlich bis zu 800 mal 552 Punkten.</p>

        <p>Epson-Support ist eine Basic-Erweiterung und belegt den Speicherbereich ab $C000 (49152). Der Programmstart erfolgt durch SYS 49152; jetzt stehen einige leistungsfähige Befehle zur Verfügung, um Grafiken zu erstellen und auf den Epson FX/RX-Druckern auszugeben. Die Grafiken liegen im Speicher des Commodore 64 ab $2000 (8192). Wegen des enormen Speicherplatzbedarfs wird auch das RAM unter dem Basic, dem Betriebssystem und unter dem Zeichen-ROM verwendet. Die Bitmap wird über Koordinaten adressiert, dabei liegt der Punkt (0,0) in der linken Ecke oben wie bei den normalen HiRes-Grafiken auf dem Bildschirm. Die X-Werte laufen von 0 bis 799, die Y-Werte von 0 bis 551, so daß 800x 552 = 441600 Dots adressiert werden können. Die Bitmap ist so organisiert, daß der Ausdruck relativ schnell geht.</p>

        <p>Die Ausgabe mit dem Drucker läuft über ein User-Port-Kabel, die nötige Treibersoftware ist im Epson-Support schon enthalten. Der Ausdruck erfolgt in der sogenannten doppelten Zeichendichte, also mit 960 Punkten pro Zeile, aus Speichermangel können aber nur 800 Punkte pro Zeile verwendet werden. Listing 1 und Listing 2 sind zwei Beispielprogramme. Als Ergebnis liefern sie Bild 1 und Bild 2. Bevor Sie diese Beispiele probieren, muß das Listing 3 mit dem MSE (siehe Seite 7) eingegeben werden und mit SYS 49152 initialisiert werden.</p>

        <figure>
            <img src="26-1.png" alt="">
            <figcaption>Bild 1. Listing 1 liefert dieses Bild (hier verkleinert)</figcaption>
        </figure>
        <figure>
            <img src="26-2.png" alt="">
            <figcaption>Bild 2. Listing 2 erzeugt dieses Bild (hier verkleinert)</figcaption>
        </figure>

        <h3>Epson-Support Bedienungsanleitung</h3>

        <p>Das Programm Epson-Support wird absolut geladen mit LOAD"EPSON-SUPPORT",8,1 und durch SYS 49152 gestartet. Dem Basic-Programmierer stehen dann einige neue Befehle zur Verfügung, um die grafischen Fähigkeiten des Epson-Druckers besser auszunutzen, denn im Speicher des C 64 wird eine Grafik-Bitmap von 800 x 552 Punkten verwaltet. Die Auflösung ist also wesentlich höher als bei normalen Bildschirm-Grafiken.</p>

        <p>Die einzelnen Befehle werden im folgenden erläutert:</p>

        <h2>&amp;CLEAR**</h2>

        <p>Damit wird die Grafik-Bitmap gelöscht. Dieser Befehl sollte unbedingt am Anfang eines jeden Programms stehen, sonst stehen in der Bitmap wirre Muster.</p>

        <p><strong>&amp;DOTX,Y</strong></p>

        <p>Dieser Befehl setzt einen Punkt an der Position (X,Y). Der X-Wert liegt zwischen 0 (links) und799 (rechts), der Y-Wert zwischen 0(oben) und551 (unten). Für X und Y kann ein beliebiger numerischer Basic-Ausdruck stehen;</p>

        <p><strong>&amp;RESETDOTX,Y</strong></p>

        <p>Dieser Befehl löscht einen Punkt an der Position (X,Y). Der X-Wert liegt zwischen 0 (links) und 799 (rechts), der Y-Wert zwischen 0 (oben) und 551 (unten). Für X und Y kann ein beliebiger numerischer Basic-Ausdruck stehen.</p>

        <p><strong>&amp;INVDOT X,Y</strong></p>

        <p>Dieser Befehl invertiert einen Punkt an der Position (X,Y). Der X-Wert liegt zwischen 0 (links) und 799 (rechts), der Y-Wert zwischen 0 (oben) und 551 (unten). Für X und Y kann ein beliebiger numerischer Basic-Ausdruck stehen.</p>

        <p><strong>&amp;LINE X1,Y1,X2,Y2</strong></p>

        <p>Dieser Befehl zieht eine Linie zwischen den Punkten (X1 ,Y1) und (X2,Y2). Der Wertebereich der Koordinaten ist der gleiche wie bei den Dot-Befehlen. Für die Parameter können beliebige Basic-Ausdrücke stehen.</p>

        <p><strong>&amp;RESETLINE X1,Y1,X2,Y2</strong></p>

        <p>Dieser Befehl löscht eine Linie zwischen den Punkten (X1,Y1) und (X2,Y2). Der Wertbereich der Koordination ist der gleiche wie bei den Dot-Befehlen. Für die Parameter können beliebige Basic-Ausdrücke stehen.</p>

        <p><strong>&amp;INVLINE X1,Y1,X2,Y2</strong></p>

        <p>Dieser Befehl invertiert die Punkte einer Linie zwischen den Punkten (X1,Y1) und (X2,Y2). Der Wertebereich der Koordinaten ist wiederum der gleiche wie bei den Dot-Befehlen. Für die Parameter können beliebige Basic-Ausdrücke stehen.</p>

        <p><strong>&amp;SCANLINE X1,Y1,X2,Y2</strong></p>

        <p>Dieser Befehl setzt eine Linie zwischen den Punkten (X1,Y1) und (X2,Y2). Falls jedoch auf einen schon gesetzten Punkt auf dem Linienzug getroffen wird, so wird nicht mehr weitergezeichnet. Der Wertebereich der Koordinaten ist der gleiche wie bei den Dot-Befehlen. Für die Parameter können beliebige Basic-Ausdrücke stehen.</p>

        <p><strong>&amp;PRINT</strong></p>

        <p>Durch diesen Befehl wird die HiRes-Bitmap auf dem Drucker ausgegeben. Das Programm ist für Epson FX/RX-80-Drucker ausgelegt. Bei Verwendung eines anderen Druckers müssen einige Stellen im Assemblerlisting (Listing 4) beim &amp;PRINT-Befehl geändert werden. Siehe dazu auch die Kommentare im Listing bei diesem Befehl.</p>

        <p><strong>&amp;EXIT</strong></p>

        <p>Führt zum Verlassen von Epson-Support und bewirkt danach einen Basic-Neustart. Basic-Programme sollten vorher unbedingt abgespeichert sein, weil sie sonst gelöscht werden.</p>

        <p><strong>&amp;TEXT X,Y,</strong>"blablabla&hellip;"</p>

        <p>Dieser Befehl setzt den Text, welcher in Anführungszeichen steht, in die Bitmap an der Position (X,Y). Diese Position gibt die Koordinaten der linken unteren Ecke des ersten Buchstabens an.</p>

        <p><strong>&amp;RESETTEXT, X,Y,</strong>"blablabla&hellip;"</p>

        <p>Dieser Befehl löscht den Text, welcher in Anführungszeichen steht, in die Bitmap an der Position (X,Y). Diese Position gibt die Koordinaten der linken unteren Ecke des ersten Buchstabens an.</p>

        <p><strong>&amp;INVTEXT X,Y,</strong>"blablabla&hellip;"</p>

        <p>Dieser Befehl invertiert den Text, welcher in Anführungszeichen steht, in die Bitmap an der Position (X,Y). Diese Position gibt die Koordinaten der linken unteren Ecke des ersten Buchstabens an.</p>

        <address class="author">(Alexander Del Pino/gk)</address>

        <figure>
            <pre data-filename="epson-sup.demo1" data-name="Demo-Programm »Schnecke«"></pre>
            <figcaption>Listing 1. Demo-Programm »Schnecke«</figcaption>
        </figure>

        <figure>
            <pre data-filename="epson-sup.demo2" data-name="Demo-Programm »Interferrenz«"></pre>
            <figcaption>Listing 2. Demo-Programm »Interferrenz«</figcaption>
        </figure>

        <figure>
            <pre data-filename="epson support.prg" data-name="Epson-Support" data-mse=mse1></pre>
            <figcaption>Listing 3. Epson-Support muß mit dem MSE eingegeben werden.</figcaption>
        </figure>
        <div class="binary_download" data-filename="epson support.prg" data-name="Epson-Support"></div>

        <figure>
            <pre>          .BA $C000 ;PC SETZEN
          ;***********************************
          ; E P S O N - S U P P O R T
          ;
          ;VERSION 1.7 (C)1984 BY A.DEL PINO
          ;
          ;***********************************
          ;
          ;VARIABLEN & SYSTEMADRESSEN
          ;
HELP      = $033C
YVALUE    = $14
XVALUE    = $033C
X1        = $0350
Y1        = X1+2
X2        = X1+4
Y2        = X1+6
XSTEP     = X1+8
YSTEP     = X1+10
XDIFF     = X1+12
YDIFF     = X1+14
SUM       = X1+16
XMAX      = 800
YMAX      = 552
DISABLE   = $34 ;ROM AUSBLENDEN
ENABLE    = 55 ;ROM EINBLENDEN
FLAG      = X1+18
BITPOS    = X1+19
FLAG1     = X1+23
FLAG2     = X1+24
FLAG3     = X1+25
LENGH     = X1+26
LINETYP   = X1+27
          ;
          ;PROGRAMMSTART, ZEIGER VERBIEGEN
          ;
          LDA #&lt;ESUP
          LDY #&gt;ESUP
          STA $0308
          STY $0309
          LDA #&lt;8190
          LDY #&gt;8190
          STA $33
          STY $34
          STA $37
          STY $38
          ;
          ;PROMPT AUSGEBEN
          ;
          LDA #&lt;PROMPT
          LDY #&gt;PROMPT
          JMP $AB1E
          ;
          ;BASIC-TOKEN ERKENNEN
          ;
ESUP      JSR $73
          CMP #'&'
          BEQ SUPPORT
          JSR $79
          JMP $A7E7
SUPPORT   LDX #$FF
          LDY #0
          STY FLAG
SLOOP     INX
          INY
          LDA @STMTAB,X
          BEQ NEWSTMT
          CMP ($7A),Y
          BEQ SLOOP
NXT       INX
          LDA @STMTAB,X
          BNE NXT
          INC FLAG
          LDY #0
          LDA @STMTAB+1,X
          BNE SLOOP
          JMP $AF08
NEWSTMT   DEY
          CLC
          TYA
          ADC $7A
          STA $7A
          BCC L1
          INC $7B
          ;
          ;RETURN-ADRESSE AUF DEN STACK
          ;
L1        LDA #$A7
          PHA
          LDA #$E9
          PHA
          LDA FLAG
          ASL
          TAX
          LDA @ENTRY+1,X
          ;
          ;ADRESSE DES NEUEN BEFEHLS AUF DEN STACK
          PHA
          LDA @ENTRY,X
          PHA
          JMP $73
          ;
          ;HIER SIND DIE NEUEN BEFEHLE...
          ;
          ;**************************
          ;
          ; &CLEAR
          ;
          ;**************************
CLEAR     LDA #&lt;8192
          LDY #&gt;8192
          STA $FB
          STY $FC
          SEI
          LDA #DISABLE
          STA 1
          LDY #0
          TYA
CL1       STA ($FB),Y
          INY
          BNE CL1
          INC $FC
          LDX $FC
          CPX #$C0
          BNE CL1
          LDX #$C8
          STX $FC
CL2       STA ($FB),Y
          INY
          BNE CL2
          INC $FC
          BNE CL2
          LDA #ENABLE
          STA 1
          CLI
          RTS
          ;**************************
          ;
          ; DOT - BEFEHLE
          ;
          ;**************************
DOT       LDA #0
          STA FLAG
          JSR VALUE+3
          STA XVALUE
          STY XVALUE+1
          JSR VALUE
WORKDOT   CPY #&gt;YMAX
          BCC SE1
          CMP #&lt;YMAX
          BCC SE1
          RTS
          ;
          ;ADRESSE DES PUNKTES BERECHNEN
          ;
SE1       STA @HELP+2
          STY @HELP+3
          LDY #3
SE2       CLC
          LSR @HELP+3
          ROR @HELP+2
          DEY
          BNE SE2
          CLC
          LDA @HELP+2
          ADC #&gt;8192
          STA $FC
          LDA @HELP+2
          STA $FB
          ASL
          ADC $FC
          STA $FC
          LDA #0
          ASL $FB
          ROL
          ASL $FB
          ROL
          ASL $FB
          ROL
          ASL $FB
          ROL
          ASL $FB
          ROL
          ADC $FC
          STA $FC
SE4       LDA $14
          AND #7
          TAX
          LDA @BIT,X
          STA BITPOS
          LDA XVALUE
          LDY XVALUE+1
          CPY #&gt;XMAX
          BCC SE5
          CMP #&lt;XMAX
          BCC SE5
          RTS
SE5       ADC $FB
          STA $FB
          TYA
          ADC $FC
          CMP #$C0
          BCC SE6
          ADC #7
SE6       STA $FC
          SEI
          ;
          ;ROM AUSBLENDEN
          ;
          LDA #DISABLE
          STA 1
          LDY #0
          BIT FLAG
          BPL DOT0X
          BVC INVERT
          ;
          ;PUNKT TESTEN 'BITTEST'
          ;
          LDA BITPOS
          LDX #0
          AND ($FB),Y
          BEQ TS1
          DEX
TS1       STX FLAG
          JMP ENDDOT
          ;
          ;PUNKT INVERTIEREN
          ;
INVERT    LDA BITPOS
          EOR ($FB),Y
          STA ($FB),Y
          JMP ENDDOT
DOT0X     BVC SET
          ;
          ;PUNKT ZURUECKSETZEN
          ;
          LDA BITPOS
          EOR #$FF
          AND ($FB),Y
          STA ($FB),Y
          JMP ENDDOT
          ;
          ;PUNKT SETZEN
          ;
SET       LDA BITPOS
          ORA ($FB),Y
          STA ($FB),Y
ENDDOT    LDA #ENABLE
          STA 1
          CLI
          RTS
          ;**************************
          ;
          ; &PRINT
          ;
          ;**************************
          ;USERPORT-INTERFACE DURCH GEEIGNTES
          ;SETZEN DER PORTBAUSTEIN-REGISTER
          ;INITIALISIEREN. BEI DRUCKERN AM SERIELLEN BUS MUSS
          ;HIER EIN FILE AUF DEN DRUCKER EROEFFNET WERDEN.
HARDCOPY  SEI
          LDA #$FF
          STA $DD03
          LDA $DD02
          ORA #4
          STA $DD02
          LDA $DD00
          ORA #4
          STA $DD00
          LDA #$10
          STA $DD0D
          LDA $DD0D
          CLI
          ;STEUERCODES FUER EPSON RX-80 AUSGEBEN
          LDX #5
HC0       LDA @TABLE1,X
          JSR CWROUT
          DEX
          BPL HC0
          LDA #&lt;8192
          LDY #&gt;8192
          STA $FB
          STY $FC
          LDA #0
          STA $FD
          ;STEUERCODES FUER NEUE GRAFIKZEILE AUSGEBEN
HC1       LDX #0
          LDA @TABLE2,X
          JSR CWROUT
          INX
          CPX #14
          BNE HC1+2
          ;EINE GRAFIKZEILE (8*800 DOTS) AUSGEBEN
          LDX #0
HC2       JSR WROUT
          INX
          BNE HC2
HC3       JSR WROUT
          INX
          BNE HC3
HC4       JSR WROUT
          INX
          BNE HC4
HC5       JSR WROUT
          INX
          CPX #&lt;XMAX
          BNE HC5
          INC $FD
          LDA $FD
          CMP #69
          BNE HC1
          RTS
          ;ROM AUSBLENDEN
WROUT     SEI
          LDA #DISABLE
          STA 1
          ;ADRESSE DES AUSZUGEBENDEN PUNKTES BERECHNEN
          CLC
          LDA $FB
          ADC #&lt;$800
          STA $FE
          LDA $FC
          ADC #&gt;$800
          STA $FF
          LDY #0
          LDA $FC
          CMP #$C0
          BCS WR1
          LDA ($FB),Y
          .BY $2C
WR1       LDA ($FE),Y
          INC $FB
          BNE WR2
          INC $FC
          ;ROM WIEDER EINBLENDEN
WR2       LDY #ENABLE
          STY 1
          CLI
          ;GEBE ASCII-ZEICHEN IM ACCU AUS
          ;BEI DRUCKERN MIT SERIELLER SCHNITTSTELLE
          ;MUSS HIER 'JMP $FFD2' STEHEN
          ;UND DANACH AUCH EIN 'RTS'
CWROUT    STA $DD01
          LDA $DD00
          AND #$FB
          STA $DD00
          ORA #4
          STA $DD00
WAIT      LDA $DD0D
          AND #$10
          BEQ WAIT
          CLC
          RTS
          ;**************************
          ;
          ; LINE-BEFEHLE
          ;
          ;**************************
LINE      LDA #0
          STA FLAG
          LDA #0
          STA LINETYP
          JSR VALUE+3
          STA X1
          STY X1+1
          JSR VALUE
          STA Y1
          STY Y1+1
          JSR VALUE
          STA X2
          STY X2+1
          JSR VALUE
          STA Y2
          STY Y2+1
          LDX #1
          LDY #0
          SEC
          LDA X2
          SBC X1
          STA XDIFF
          LDA X2+1
          SBC X1+1
          STA XDIFF+1
          BPL LI1
          DEX
          DEX
          DEY
          EOR #$FF
          STA XDIFF+1
          LDA XDIFF
          EOR #$FF
          STA XDIFF
          INC XDIFF
          BNE LI1
          INC XDIFF+1
LI1       STX XSTEP
          STY XSTEP+1
          LDX #1
          LDY #0
          SEC
          LDA Y2
          SBC Y1
          STA YDIFF
          LDA Y2+1
          SBC Y1+1
          STA YDIFF+1
          BPL LI2
          DEX
          DEX
          DEY
          EOR #$FF
          STA YDIFF+1
          LDA YDIFF
          EOR #$FF
          STA YDIFF
          INC YDIFF
          BNE LI2
          INC YDIFF+1
LI2       STX YSTEP
          STY YSTEP+1
          LDY #0
          LDA XDIFF
          ORA XDIFF+1
          BNE LI3
          DEY
LI3       STY SUM
          STY SUM+1
LI4       LDA X1
          LDY X1+1
          STA XVALUE
          STY XVALUE+1
          LDA Y1
          LDY Y1+1
          STA YVALUE
          STY YVALUE+1
          JSR SCANN
          JSR WORKDOT
          LDA X1
          CMP X2
          BNE LI5
          LDA X1+1
          CMP X2+1
          BNE LI5
          LDA Y1
          CMP Y2
          BNE LI5
          LDA Y1+1
          CMP Y2+1
          BNE LI5
          RTS
LI5       BIT SUM+1
          BPL LI6
          CLC
          LDA Y1
          ADC YSTEP
          STA Y1
          LDA Y1+1
          ADC YSTEP+1
          STA Y1+1
          CLC
          LDA SUM
          ADC XDIFF
          STA SUM
          LDA SUM+1
          ADC XDIFF+1
          STA SUM+1
          JMP LI4
LI6       CLC
          LDA X1
          ADC XSTEP
          STA X1
          LDA X1+1
          ADC XSTEP+1
          STA X1+1
          SEC
          LDA SUM
          SBC YDIFF
          STA SUM
          LDA SUM+1
          SBC YDIFF+1
          STA SUM+1
          JMP LI4
VALUE     JSR $AEFD
          JSR $AD8A
          JSR $B7F7
          LDA $14
          LDY $15
          RTS
          ;FLAG SETZEN FUER MODIFIZIERTEN PUNKT- UND LINIEN-FUNKTIONEN
RESETDOT  LDA #$40
          JMP DOT+2
INVERTDOT LDA #$80
          JMP DOT+2
RESETLINE LDA #$40
          JMP LINE+2
INVERTLINELDA #$80
          JMP LINE+2
          ;**************************
          ;
          ; &EXIT
          ;
          ;**************************
          ;ZEIGER WIEDER 'GERADEBIEGEN'
EXIT      LDY #$A7
          LDA #$E4
          STA $0308
          STY $0309
          SEC
          JSR $FF99
          STX $37
          STY $38
          JMP $E39A ;SYSTEM AUFRUFEN
          ; TEXT-BEFEHLE, FLAG SETZEN
INVTEXT   LDA #$80
          .BY $2C
RESTEXT   LDA #$40
          .BY $2C
TEXT      LDA #0
          STA FLAG
          JSR VALUE+3
          STA X1
          STY X1+1
          JSR VALUE
          STA Y1
          STY Y1+1
          JSR STRING
          BNE TXT1
          RTS
TXT1      STA LENGH
          STY FLAG3
TXT2      LDY FLAG3
          LDA ($64),Y
          CMP #$20
          BCC TXT3
          CMP #$40
          BCC TXT3+2
          CMP #$60
          BCS TXT3
          AND #$1F
          .BY $2C
TXT3      LDA #$20
          STA 2
          LDA #$1A
          ASL 2
          ROL
          ASL 2
          ROL
          ASL 2
          ROL
          STA 3
          SEI
          LDA #$31
          STA 1
          LDY #7
TXT4      LDA (2),Y
          STA $200,Y
          DEY
          BPL TXT4
          LDA #ENABLE
          STA 1
          CLI
          LDA #0
          STA FLAG1
TXT5      LDA #7
          STA FLAG2
TXT6      LDX FLAG2
          LDA $200,X
          LDX FLAG1
          AND @BIT,X
          BEQ TXT8
          LDA X1
          LDY X1+1
          STA XVALUE
          STY XVALUE+1
          LDY Y1+1
          LDA FLAG2
          CLC
          ADC Y1
          STA $14
          BCC TXT7
          INY
TXT7      JSR WORKDOT
TXT8      DEC FLAG2
          BPL TXT6
          INC X1
          BNE TXT9
          INC X1+1
TXT9      INC FLAG1
          LDX FLAG1
          CPX #8
          BNE TXT5
          INC FLAG3
          LDA FLAG3
          CMP LENGH
          BNE TXT10
          RTS
TXT10     JMP TXT2
STRING    JSR $AEFD
          JSR $AD9E
          JSR $B6A3
          LDY #0
          LDA ($64),Y
          STA $63
          INY
          LDA ($64),Y
          TAX
          INY
          LDA ($64),Y
          STA $65
          STX $64
          LDY #0
          LDA $63
          RTS
SCANN     BIT LINETYP
          BMI SCA1
          RTS
SCA1      LDX #$F0
          STX FLAG
          JSR WORKDOT
          LDA Y1
          LDY Y1+1
          STA $14
          BIT FLAG
          BPL SCA2
          PLA
          PLA
          RTS
SCA2      LDX #0
          STX FLAG
          RTS
SCANNLN   LDA #$FF
          JMP LINE+7
          ;**************************
          ;
          ; D A T E N B E R E I C H
          ;
          ;**************************
BIT       .BY 128 64 32 16 8 4 2 1
TABLE1    .BY 13 8 'A' 27 '8' 27
TABLE2    .BY 10 13 '       ' 27 '*' 1 XMAX
PROMPT    .BY 'EPSON-SUPPORT  V1.7  ***' 13
          .BY '(C)1984 BY ALEX DEL PINO' 13 0
STMTAB    .BY 'CLEAR' 0
          .BY 'DOT' 0
          .BY 'RESETDOT' 0
          .BY 'INVDOT' 0
          .BY 'LINE' 0
          .BY 'RESETLINE' 0
          .BY 'INVLINE' 0
          .BY 153 0
          .BY 'EXIT' 0
          .BY 'TEXT' 0
          .BY 'INVTEXT' 0
          .BY 'RESETTEXT' 0
          .BY 'SCANLINE' 0 0
ENTRY     .BY CLEAR-1 DOT-1 RESETDOT-1 INVERTDOT-1
          .BY LINE-1 RESETLINE-1 INVERTLINE-1 HARDCOPY-1 EXIT-1
          .BY TEXT-1 INVTEXT-1 RESTEXT-1 SCANNLN-1
          .EN ;END OF SOURCE-FILE

READY.</pre>
            <figcaption>Listing 4. Der Source-Code zum Epson-Support</figcaption>
        </figure>

    </article>
</body>

</html>
