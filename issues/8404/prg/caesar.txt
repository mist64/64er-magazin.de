;caesar.prg ==0801==
;Die Datei CAESAR in 84XX/8404.D64 auf der 64'er CD-ROM hat noch die
;REM-Statements in Zeilen in Zeilen 7450 und 7800 (siehe Fehlerteu-
;felchen). Dies ist hier gefixt.
;Die Credits ab Zeile 61000 fehlen im gedruckten Magazin.

  100 dimf(19,12):dimd(2):t1=1229
  120 print"{clr}"
  140 sd=1
  160 si=54272:fl=si:fh=si+1:tl=si+2:th=si+3
  180 w9=si+4:a=si+5:h=si+6:l=si+24
  200 f(3,3)=5:f(4,3)=5:t2=1231
  220 f(15,9)=6:f(16,9)=6:t3=56005
  240 d(1)=25:d(2)=25:t4=56007
  420 poke53280,0:poke53281,0
  440 print"{down}{down}{down}{down}{down}{down}{down}{blk}"
  460 print"  {rvon}{SHIFT-POUND}   {CBM-*}{rvof} {rvon}{SHIFT-POUND}   {CBM-*}{rvof} {rvon}{SHIFT-POUND}   {CBM-*}{rvof} {rvon}{SHIFT-POUND}   {CBM-*}{rvof} {rvon}{SHIFT-POUND}   {CBM-*}{rvof} {rvon}{SHIFT-POUND}   {CBM-*}{rvof}"
  480 print"  {rvon} {rvof}     {rvon} {rvof}   {rvon} {rvof} {rvon} {rvof}     {rvon} {rvof}     {rvon} {rvof}   {rvon} {rvof} {rvon} {rvof}   {rvon} {rvof}"
  500 print"  {rvon} {rvof}     {rvon}     {rvof} {rvon}    {rvof}  {rvon}     {rvof} {rvon}     {rvof} {rvon}     {rvof}"
  520 print"  {rvon} {rvof}     {rvon} {rvof}   {rvon} {rvof} {rvon} {rvof}         {rvon} {rvof} {rvon} {rvof}   {rvon} {rvof} {rvon} {rvof}  {CBM-*}"
  540 print"  {CBM-*}{rvon}   {rvof}{SHIFT-POUND} {rvon} {rvof}   {rvon} {rvof} {CBM-*}{rvon}   {rvof}{SHIFT-POUND} {CBM-*}{rvon}   {rvof}{SHIFT-POUND} {rvon} {rvof}   {rvon} {rvof} {CBM-*}   {CBM-*} "
  580 poke53281,1:fort=1to1500:nextt
  600 data10,89,200,16,109,200,27,160,200
  620 data34,207,200,-1,-1,0
  640 data15,129,70,15,129,70,13,10,70
  660 data29,69,100,-1,-1,0
  680 data5,123,200,17,103,200,14,162,200
  700 data87,182,270,-1,-1,0
  720 data16,109,130,16,109,130
  740 data16,109,150,3,116,270,-1,-1,0
  760 data17,103,200,21,237,200,26,20
  780 data330,21,237,80,26,20,200,29
  800 data69,200,26,20,200,0,0,200,21
  820 data237,200,26,20,200,29,69,600
  840 data26,20,200,0,0,200
  860 data-1,-1,0
  880 gosub51000
  900 print"{clr}"
 1000 gosub40000
 1100 fori=1to2:gosub10000:gosub15000:nexti:poke53280,14:poke53281,8
 1200 print"{clr}{down}{down}{down}{down}{down}{down}{down}{down}{down}    ok. die legionaere sind postiert."
 1300 print"{down}{down}         ich werde mal aufdecken."
 1400 gosub54000:fort=1to1000:nextt
 1500 print"{clr}";
 1600 gosub10000
 1700 gosub7000
 1800 gosub50000
 1900 ti$="000000"
 2000 fori=1to2:rr=0
 2005 ifi=1thenprint"{lblu}";
 2010 ifi=2thenprint"{lred}";
 2020 gosub9000:print" "sn$(i)" position ziehender legionaer{home}":gosub50000
 2040 gosub12000:gosub9000
 2060 iff(x,y)<>ithengosub9000:print"falsche eingabe";:gosub9700:goto2040
 2100 forp=0to9
 2120 poke1863+(x*2)-((11-y)*80),224
 2140 poke56135+(x*2)-((11-y)*80),9
 2160 gosub56000
 2180 poke56135+(x*2)-((11-y)*80),1
 2190 poke1863+(x*2)-((11-y)*80),86
 2195 nextp
 2200 gosub9000
 2240 print" "sn$(i)" welche richtung?{home}"
 2280 getr$:ifr$=""then2280
 2300 ifr$=chr$(81)thenzx=-1:zy=-1:goto2450
 2310 ifr$=chr$(83)then25000
 2320 ifr$=chr$(87)thenzx=0:zy=-1:goto2450
 2330 ifr$=chr$(77)then34000
 2340 ifr$=chr$(69)thenzx=1:zy=-1:goto2450
 2360 ifr$=chr$(65)thenzx=-1:zy=0:goto2450
 2380 ifr$=chr$(68)thenzx=1:zy=0:goto2450
 2400 ifr$=chr$(90)thenzx=-1:zy=1:goto2450
 2420 ifr$=chr$(88)thenzx=0:zy=1:goto2450
 2440 ifr$=chr$(67)thenzx=1:zy=1:goto2450
 2445 goto2280
 2450 ifx+(zx)<1orx+(zx)>18ory+(zy)<1ory+(zy)>11then22000
 2500 w=f(x+(zx),y+(zy)):gosub55000
 2520 w2=f(x+(zx*2),y+(zy*2))
 2530 ifrr=1then2570
 2540 ifw=i+4then22000
 2560 ifw=0thenf(x,y)=0:gosub20000:f(x+(zx),y+(zy))=i:bx=x+zx:by=y+zy
 2565 ifw=0thenf(x,y)=0:gosub21000:goto6800
 2566 ifw>4thengosub20000:bx=x+zx:by=y+zy:goto6800
 2570 ifw2=i+4orw=i+4then22000
 2575 ifx+(zx*2)<1orx+(zx*2)>18ory+(zy*2)<1ory+(zy*2)>11then22000
 2580 ifw=iandw2=0orw=iandw2>3thenf(x,y)=0:gosub20000
 2600 ifw=iandw2=0orw=iandw2>3thenf(x+(zx*2),y+(zy*2))=i
 2620 ifw=iandw2=0orw=iandw2>3thenbx=x+(zx*2):by=y+(zy*2):gosub21000:goto6000
 2640 rem
 2660 ifw=0andw2=0then22000
 2680 ifw<3andw2=0orw<3andw2>3thenf(x,y)=0:gosub20000:f(x+(zx),y+(zy))=0
 2700 ifw<3andw2=0orw<3andw2>3thenbx=x+zx:by=y+zy:gosub23000
 2720 ifw<3andw2=0orw<3andw2>3thenf(x+(zx*2),y+(zy*2))=i:bx=x+(zx*2):by=y+(zy*2)
 2740 ifw<3andw2=0orw<3andw2>3thengosub21000:gosub53000:goto6000
 2760 ifw>2goto6800
 2800 goto24000
 6000 iff(bx,by)>2then6800
 6200 gosub9000:print"  weiterspringen? ja=f1 nein=f7";
 6500 getss$:ifss$=""then6500
 6520 ifss$=chr$(133)thenx=x+(zx*2):y=y+(zy*2):zx=0:zy=0:rr=1:goto2100
 6540 ifss$=chr$(134)then6800
 6560 ifss$=chr$(135)then6800
 6580 ifss$=chr$(136)then6800
 6600 goto6500
 6800 ifbx=3andby=3andi=2thenpoke55501,14:poket1,88:f(3,3)=3:l2=l2+1:d(2)=d(2)-1
 6820 ifbx=4andby=3andi=2thenpoke55503,14:poket2,88:f(4,3)=3:l2=l2+1:d(2)=d(2)-1
 6840 ifbx=15andby=9andi=1thenpoket3,10:poke1733,88:f(15,9)=3:l1=l1+1:d(1)=d(1)-1
 6860 ifbx=16andby=9andi=1thenpoket4,10:poke1735,88:f(16,9)=3:l1=l1+1:d(1)=d(1)-1
 6880 ifl1=2orl2=2then35000
 6900 ifd(1)=0ord(2)=0then38000
 6920 gosub9000:nexti
 6940 rz=rz+1:i=0:goto2000
 7000 forxx=1to18
 7200 foryy=1to11
 7300 iff(xx,yy)=1thengosub7600
 7400 iff(xx,yy)=2thengosub7700
 7450 iff(xx,yy)=3thengosub7800
 7500 nextyy,xx:xx=0:yy=0:return
 7600 poke1863+(xx*2)-((11-yy)*80),224:poke56135+(xx*2)-((11-yy)*80),14:return
 7700 poke1863+(xx*2)-((11-yy)*80),224:poke56135+(xx*2)-((11-yy)*80),10:return
 7800 poke1863+(xx*2)-((11-yy)*80),224:poke56135+(xx*2)-((11-yy)*80),9:return
 8000 ifi=2then8200
 8100 poke1863+(x*2)-((11-y)*80),224:poke56135+(x*2)-((11-y)*80),14:return
 8200 poke1863+(x*2)-((11-y)*80),224:poke56135+(x*2)-((11-y)*80),10:return
 9000 print"{home}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}                                       ";
 9100 print"{home}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}";
 9200 return
 9500 gosub9000
 9520 print"{yel}        nicht moeglicher zug{wht}";
 9540 gosub9700:gosub9000:goto2120
 9600 gosub9000
 9620 print"{yel}        u n m o e g l i c h {wht}";
 9640 gosub9700:gosub9000:goto2120
 9700 forhr=1to1500:nexthr
 9800 return
 10000 poke53281,9:poke53280,0
 10100 print"{home}{blk}{CBM-A}C{CBM-R}C{CBM-R}C{CBM-R}C{CBM-R}C{CBM-R}C{CBM-R}C{CBM-R}C{CBM-R}C{CBM-R}C{CBM-R}C{CBM-R}C{CBM-R}C{CBM-R}C{CBM-R}C{CBM-R}C{CBM-R}C{CBM-R}C{CBM-S}"
 10200 b1$="{SHIFT--}{$a0}{SHIFT--}{$a0}{SHIFT--}{$a0}{SHIFT--}{$a0}{SHIFT--}{$a0}{SHIFT--}{$a0}{SHIFT--}{$a0}{SHIFT--}{$a0}{SHIFT--}{$a0}{SHIFT--}{$a0}{SHIFT--}{$a0}{SHIFT--}{$a0}{SHIFT--}{$a0}{SHIFT--}{$a0}{SHIFT--}{$a0}{SHIFT--}{$a0}{SHIFT--}{$a0}{SHIFT--}{$a0}{SHIFT--}"
 10300 b2$="{CBM-Q}C{SHIFT-+}C{SHIFT-+}C{SHIFT-+}C{SHIFT-+}C{SHIFT-+}C{SHIFT-+}C{SHIFT-+}C{SHIFT-+}C{SHIFT-+}C{SHIFT-+}C{SHIFT-+}C{SHIFT-+}C{SHIFT-+}C{SHIFT-+}C{SHIFT-+}C{SHIFT-+}C{SHIFT-+}C{CBM-W}"
 10400 forb=1to10:printb1$:printb2$:nextb
 10500 printb1$:print"{CBM-Z}C{CBM-E}C{CBM-E}C{CBM-E}C{CBM-E}C{CBM-E}C{CBM-E}C{CBM-E}C{CBM-E}C{CBM-E}C{CBM-E}C{CBM-E}C{CBM-E}C{CBM-E}C{CBM-E}C{CBM-E}C{CBM-E}C{CBM-E}C{CBM-X}"
 10600 print"{home}":forb=1to11:printtab(37)b;:print:nextb
 10700 poke55501,14:poke55503,14:poke1229,86:poke1231,86
 10800 poke56005,10:poke56007,10:poke1733,86:poke1735,86
 10900 print" 1 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8":gosub11000
 11000 forb=56216to56255:pokeb,13:nextb
 11100 forb=55334to56294step40:pokeb,13:pokeb+1,13:nextb
 11200 return
 12000 getx$:ifx$>chr$(47)andx$<chr$(58)then12200
 12100 goto12000
 12200 x=val(x$):poke56215+(x*2),0
 12300 gosub9000:ifx>1then13000
 12400 getx$:ifx$>chr$(47)andx$<chr$(57)then12600
 12450 ifx$=chr$(13)then13000
 12500 goto12400
 12600 x=val(x$):x=x+10:poke56215+(x*2),0
 12700 rem
 13000 gety$:ify$>chr$(47)andy$<chr$(58)then13200
 13100 goto13000
 13200 y=val(y$):poke55294+(y*80),0:poke55295+(y*80),0
 13300 ify>1then13900
 13400 gety$:ify$>chr$(47)andy$<chr$(50)then13600
 13450 ify$=chr$(13)then13900
 13500 goto13400
 13600 poke55375,0:y=val(y$):y=y+10:poke55294+(y*80),0:poke55295+(y*80),0
 13900 fort=1to500:nextt:gosub11000
 14000 return
 15000 gosub9000
 15100 print" "sn$(i)" gebe deine figuren jetzt ein";:gosub9700
 15300 fors=1to25:gosub9000:print"{wht} "s". figur";
 15400 gosub12000:gosub9000
 15600 iff(x,y)>0thengosub9000:print"{yel} unmoeglich";:goto15400
 15700 ifi=1andy>5thengosub9000:print"{yel} unmoeglich";:goto15400
 15800 ifi=2andy<7thengosub9000:print"{yel} unmoeglich";:goto15400
 15900 ifx=15andy=9thengosub9000:print"{wht} das ist das zielfeld";:goto15400
 16000 ifx=3andy=3thengosub9000:print"{wht} das ist das zielfeld";:goto15400
 16100 ifx=16andy=9thengosub9000:print"{wht}{rght}das ist das zielfeld";:goto15400
 16200 ifx=4andy=3thengosub9000:print"{wht} das ist das zielfeld";:goto15400
 16300 gosub9000
 16400 f(x,y)=i
 16500 gosub8000
 16600 nexts:ifi=2then17300
 16700 fort=1to800:nextt
 16800 gosub50000
 16900 print"{clr}{down}{down}{down}{down}{down}{down}{down}{down}{down}caesar wuenscht jetzt die heeresstellung{down}"
 17000 printtab(11)"von heeresfuehrer{down}{down}"
 17050 printtab(14)sn$(2)
 17100 fort=1to3500:nextt
 17300 fort=1to750:nextt
 17400 print"{clr}";:return
 20000 poke1863+(x*2)-((11-y)*80),224:poke56135+(x*2)-((11-y)*80),9:return
 21000 ifi=1then21300
 21100 ifi=2then21500
 21300 poke1863+(bx*2)-((11-by)*80),224:poke56135+(bx*2)-((11-by)*80),14:return
 21400 return
 21500 poke1863+(bx*2)-((11-by)*80),224:poke56135+(bx*2)-((11-by)*80),10:return
 21600 return
 22000 gosub9000
 22100 print"      caesar erlaubt dies nicht.";
 22200 gosub9700
 22300 gosub9000
 22320 ifi=1then22400
 22340 ifi=2then22600
 22400 poke1863+(x*2)-((11-y)*80),224:poke56135+(x*2)-((11-y)*80),14:goto6800
 22600 poke1863+(x*2)-((11-y)*80),224:poke56135+(x*2)-((11-y)*80),10:goto6800
 23000 poke1863+(bx*2)-((11-by)*80),224:poke56135+(bx*2)-((11-by)*80),9:return
 24000 gosub9000
 24100 print" caesar duldet keine meuchelmoerder.";:goto22200
 25000 gosub9700
 25100 gosub9000
 25200 ifi=1then25400
 25300 ifi=2then25500
 25400 poke1863+(x*2)-((11-y)*80),224:poke56135+(x*2)-((11-y)*80),14:goto6800
 25500 poke1863+(x*2)-((11-y)*80),224:poke56135+(x*2)-((11-y)*80),10:goto6800
 30000 print"{clr}{down}{down}{down}{down}{down}{down}"spc(8)"soll ich die steuerungs-"
 30100 printtab(10)"funktionen naeher er-"
 30200 printtab(15)"laeutern?"
 30400 gety3$:ify3$=""then30400
 30500 ify3$="j"then30700
 30600 return
 30700 print"{clr}{yel}{down}{down}":poke53281,2:poke53280,0
 30800 print"die ansteuerung jedes einzelnen soldaten";
 30900 print"  erfolgt ueber eingabe der einzelnen"
 31000 print"koordinaten desselben. wenn sie zb. den"
 31100 print" soldaten mit der koordinate 4 / 6 an-"
 31200 print" steuern wollen druecken sie 4; dann 6."
 31300 print"bei einem soldaten mit den koordinaten"
 31400 print"1 / 11 sieht die sache etwas anders aus;";
 31500 print"hier geben sie ein: 1 - return - 1 - 1."
 31600 print"  nachdem sie die position eingegeben"
 31700 print" haben muessen sie noch die zugrichtung"
 31800 print"  eingeben.  das geschieht wie folgt:{blk}{down}"
 31900 print"          links  oben   rechts"
 32000 print"                M  {SHIFT--}  N       "
 32100 print"                 {wht}q{blk} {wht}w{blk} {wht}e{blk}        "
 32200 print"                  M{SHIFT--}N         "
 32300 print"          links C{wht}a{blk}CQC{wht}d{blk}C rechts"
 32400 print"                  N{SHIFT--}M         "
 32500 print"                 {wht}z{blk} {wht}x{blk} {wht}c{blk}        "
 32600 print"                N  {SHIFT--}{$a0} M       "
 32700 print"          links  unten  rechts"
 32750 getpq$:ifpq$=""then32750
 32800 print"{lgrn}{down}{down}{down}{down}{down}wenn sie zwischenzeitig aufhoeren wollen";
 32900 print"druecken sie 'm' wenn sie einen zug, der";
 33000 print"bereits mit koordinaten eingegeben wurde";
 33100 print"nicht machen wollen, druecken sie 's'."
 33200 print"beachten sie: sie duerfen nicht mit eig-";
 33300 print"enen legionaeren ins eigene lager. sie"
 33400 print"sollen nicht versuchen aus dem schlacht-";
 33500 print"feld herauszuziehen und weiterhin nicht"
 33600 print"auf soldaten springen - all dies be-"
 33700 print"straft caeser dadurch indem der naechste";
 33800 print"heerfuehrer an die reihe kommt."
 33900 print"{down}{down}{down}{rght}{rght}{rght}{rght}{rght}{rght}{blk}v{yel}i{blk}e{yel}l {blk}s {yel}p {blk}a {yel}s {blk}s {down}{down}{down}{down}{down}":goto30400
 34000 gosub52000:gosub52000
 34050 print"{clr}{down}{down}{down}{down}{down}{down}{yel}":poke53281,0:poke53280,2
 34100 print"  caesar bewundert diese strategische"
 34150 print"  leistung nicht besonders. schon nach"
 34200 print"  "rz" zuegen aufzuhoeren ist nicht"
 34300 print"  eines caesars heerfuehrer angemessen."
 34350 print"  vielleicht solltet ihr es ein paar"
 34400 print"  jahrhunderte spaeter bei dem ehrvol-"
 34450 print"  len kaiser wilhelm noch einmal ver-"
 34500 print"  suchen; oder ihr koenntet es bei mir"
 34550 print"  in einer meiner arenen probieren."
 34600 fort=1to4000:nextt:goto39000
 35000 fort=1to900:nextt
 35100 print"{home}{down}{down}{down}{down}{down}{down}{down}{down}{down}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{blk}UCCCCCCCCCCCCCCCCCCCCCCI"
 35150 print"{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{blk}{SHIFT--}der sieger steht fest!{SHIFT--}"
 35200 print"{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{blk}JCCCCCCCCCCCCCCCCCCCCCCK"
 35250 gosub54000
 35300 print"{clr}{yel}{down}{down}{down}":poke53280,0:poke53281,2
 35350 ifl1=2theng$=sn$(1)
 35370 ifl2=2theng$=sn$(2)
 35400 ifrz<27then38000
 35500 ifrz<65then36300
 35550 gosub52000:gosub52000
 35600 print" meinen glueckwunsch "g$";"
 35650 print" deine leistung haette zwar wesentlich"
 35700 print" besser sein koennen, da du "rz/2
 35750 print" verschiebungen deiner heeresformation"
 35800 print" gebraucht hast um zu gewinnen."
 35850 print" mit ein bisschen uebung solltst du "
 35900 print" deine leistung aber noch steigern"
 35950 print" koennen. zur zeit kann ich dich aber"
 36000 print" nur als vorkoster in meinem stab ge-"
 36050 print" brauchen; da der letzte, merkwuerdiger-";
 36100 print" weise,nachdem brutus sein gastgeschenk;";
 36150 print" eine sachertorte; vorbeibrachte; an"
 36200 print" magenkraempfen von uns gegangen ist."
 36250 fort=1to4000:nextt:goto39000
 36300 gosub54000:print"   meinen glueckwunsch, "g$:gosub52000
 36350 print"{down}   deine art der heeresfuehrung"
 36400 print"   erinnert an die von nero den"
 36450 print"   ich zwar nicht so mag, aber ich"
 36500 print"   gratuliere dir trotzdem. deine"
 36550 print"   legionaere finden, das du"
 36600 print"   ein barscher, unliebsamer"
 36650 print"   fuehrer bist, und, offen"
 36700 print"   gesagt, sie hassen dich."
 36750 print"   zwar kann ich dir zur zeit"
 36800 print"   keinen posten als heeres-"
 36850 print"   fuehrer in meiner legion"
 36900 print"   anbieten, aber ein leib-"
 36950 print"   eigener bei cleopatra"
 37000 print"   waere ja auch nicht von"
 37050 print"   der hand zu weisen."
 37100 fort=1to4000:nextt:goto39000
 38000 gosub54000
 38050 poke53280,1
 38100 print"{clr}{down}{down}{down}{down}{lgrn}"
 38150 print"   caesar beglueckwuenscht den sieger"
 38200 print:printtab(16)g$
 38250 print:gosub54000
 38300 print"   eine hervorragende strategische"
 38350 print"   leistung. jefferson, karl der"
 38400 print"   grosse und ich zusammen"
 38450 print"   haetten es nicht besser machen"
 38500 print"   koennen. du wirst ab heute in"
 38550 print"   meinen engsten fuehrungsstab"
 38600 print"   uebernommen und bekommst den"
 38650 print"   honorigen auftrag diese merk-"
 38700 print"   wuerdigen gallier aus diesem"
 38750 print"   dorf zu vertreiben.":gosub54000
 38800 fort=1to4000:nextt:goto39000
 39000 print"{down}{down}{wht}   noch einmal?"
 39100 getne$:ifne$=""then39100
 39200 ifne$="j"thenclr:goto100
 39300 print"{clr}{red}":poke53280,1:poke53281,1
 39400 print"{down}{down}{down}{down}{down}{down}{rght}{rght}{rght}{rght}{rght}{rght}bis"
 39500 print"{down}{down}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}bald"
 39600 print"{down}{down}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}bei"
 39700 print"{down}{down}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}c a e s a r"
 39800 print"{down}{down}{down}{down}{down}{down}":end
 40000 print"{clr}{orng}"
 40100 poke53280,11:poke53281,11
 40200 print"hallo, hier ist caesar. ich hoerte, das"
 40300 print"ihr strategisch was drauf habt - soso -"
 40400 print"na, das werden wir ja gleich sehen. ich"
 40450 print"habe mir fuer euch ein kleines spiel ein";
 40500 print"-fallen lassen. mein gedachtes schlacht-";
 40600 print"feld hat eine groesse von achtzehn  mal"
 40700 print"elf feldern und jeder heeresfuehrer hat"
 40800 print"fuenfundzwanzig legionaere.jeder heeres-";
 40900 print"fuehrer stellt  seine eigene formation "
 41000 print"auf. daher sollte der gegner bei der je-";
 41100 print"weiligen eingabe nicht auf den schirm"
 41200 print"schauen.  eure legionaere springen oder"
 41300 print"ziehen  gerade  oder  schraeg  in  alle"
 41400 print"richtungen. mit einem legionaer kann so"
 41500 print"weit gesprungen werden,  wie es die je-"
 41600 print"weilige stellung erlaubt. beim springen"
 41700 print"bleiben eigene legionaere stehen,  geg-"
 41800 print"nerische werden getoetet. ziel ist es"
 41900 print"die zwei lagerfelder ( VV ) des gegners"
 42000 print"mit eigenen legionaeren zu besetzen."
 42060 print"{down}{down}     bitte eine taste druecken."
 42100 getb$:ifb$=""then42100
 42150 gosub30000
 42200 print"{clr}{down}{down}{down}{down}{down}{wht}caesar wuenscht noch die namen der zwei"
 42300 print"heeresfuehrer kennenzulernen."
 42400 print"{down}{down}"
 42500 input"1. heeresfuehrer";s1$
 42600 iflen(s1$)>9thenprint"{down}beim zeus, das ist zu lang{down}":goto42500
 42700 print:input"2. heeresfuehrer";s2$
 42800 iflen(s2$)>9thenprint"{down}beim zeus, das ist zu lang{down}":goto42700
 42900 w=int(rnd(1)*2)+1
 43000 ifw=1thensn$(1)=s1$:sn$(2)=s2$
 43100 ifw=2thensn$(1)=s2$:sn$(2)=s1$
 43150 print"{down}   ton          ?"
 43170 getsd$:ifsd$=""then43170
 43190 ifsd$<>"j"thensd=0
 43200 fort=1to200:nextt
 43300 print"{down}{down}"sn$(1)" faengt an."
 43400 print"{down}{down}{lblu}die goetter moegen euch beistehen."
 43500 gosub9700
 43600 print"{clr}"
 43700 return
 50000 ifsd=0thenreturn
 50050 fort=1to5:readmx:readmy:readmd:nextt
 50100 pokel,15:poketh,13:poketl,15
 50200 pokea,3*16+15:pokeh,9
 50300 readmx:readmy:readmd
 50400 ifmx=-1thenrestore:return
 50500 pokefh,mx:pokefl,my:pokew9,65
 50600 fort=1tomd:nextt
 50700 pokew9,0:goto50300
 51000 pokel,15:poketh,13:poketl,15
 51100 pokea,3*16+15:pokeh,9
 51200 readmx:readmy:readmd
 51300 ifmx=-1thenrestore:return
 51400 pokefh,mx:pokefl,my:pokew9,65
 51500 fort=1tomd:nextt
 51600 pokew9,0:goto51200
 52000 ifsd=0thenreturn
 52050 fort=1to10:readmx:readmy:readmd:nextt
 52100 pokel,15:poketh,13:poketl,15
 52200 pokea,3*16+15:pokeh,9
 52300 readmx:readmy:readmd
 52400 ifmx=-1thenrestore:return
 52500 pokefh,mx:pokefl,my:pokew9,65
 52600 fort=1tomd:nextt
 52700 pokew9,0:goto52300
 53000 ifsd=0then53700
 53050 fort=1to15:readmx:readmy:readmd:nextt
 53100 pokea,3*16+15:pokeh,9
 53200 readmx:readmy:readmd
 53300 ifmx=-1thenrestore:goto53700
 53400 pokefh,mx:pokefl,my:pokew9,33
 53500 fort=1tomd:nextt
 53600 pokew9,0:goto53200
 53700 ifi=1thend(2)=d(2)-1:return
 53800 ifi=2thend(1)=d(1)-1:return
 54000 ifsd=0thenreturn
 54050 fort=1to20:readmx:readmy:readmd:nextt:pokel,10
 54100 pokea,3*16+15:pokeh,9
 54200 readmx:readmy:readmd
 54300 ifmx=-1thenrestore:return
 54400 pokefh,mx:pokefl,my:pokew9,33
 54500 fort=1tomd:nextt
 54600 pokew9,0:goto54200
 55000 ifsd=0thenreturn
 55100 pokel,15:pokea,16+9:pokeh,64
 55200 pokefh,29:pokefl,69
 55300 pokew9,129
 55400 fort=1to200:nextt
 55600 pokew9,0:pokea,0:pokeh,0
 55700 return
 56000 ifsd=0thenfort=1to60:nextt:return
 56100 ifkj=0thenkj=1:goto56300
 56200 ifkj=1thenkj=0:goto56600
 56300 pokefh,20:pokefl,178:pokew9,17
 56400 fort=1to25:nextt
 56500 pokew9,0:return
 56600 pokefh,12:pokefl,78:pokew9,17
 56700 fort=1to25:nextt
 56800 pokew9,0:return
 61000 rem
 62000 rem *****************************
 62100 rem *                           *
 62200 rem *      geschrieben  von     *
 62300 rem *                           *
 62400 rem *    andreas groebe         *
 62500 rem *                           *
 62600 rem *                           *
 62700 rem *                           *
 62800 rem *                           *
 62900 rem *     fuer vc-64  5/1983    *
 63000 rem *                           *
 63200 rem *****************************

