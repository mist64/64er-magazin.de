

;euler.polygon.prg ==0801==
  100 rem eulersches polygonverfahren
  110 :
  130 print"{clr}{rvon}     eulersches polygonzugverfahren     {rvof}"
  140 print"dieses verfahren hat keine fehler-"
  150 print"abschaetzung. numerisch ist dieses "
  160 print"verfahren recht schnell, aber bei zu"
  170 print"grosser schrittweite sehr ungenau, wobei"
  180 print"{up}der fehler mit wachsendem x immer "
  190 print"groesser wird. schrittweiten von 10^-2"
  200 print"und kleiner sind schon einigermassen"
  210 print"genau.{down}""
  220 :
  230 input"{down}anfangswerte : x,y ";x1,y1
  240 input"{down}intervallobergrenze";b:a=x1
  250 input"{down}schrittweite       ";h
  260 :
  280 print"{clr}y'=y*x-2*x{down}"
  290 deffnf(x)=int(x*10e6+.5)/10e6
  300 :
  310 y=y1
  320 print" x   ","  y"
  330 forx=atobsteph
  340 gosub410:m=f:rem steigung der naeherungsgeraden
  350 t=y-m*(x+a):rem verschub der geraden
  360 printfnf(x),fnf(y)
  370 y=m*(a+x+h)+t:rem naechster y-wert
  380 nextx
  390 end
  400 :
  410 f=y*x-2*x:rem differentialgleichung
  420 return

