

;zauberschloss 2.prg ==0801==
    1 rem +++ zauberschloss - 2. teil +++
    2 rem +++ ein adventure von       +++
    3 rem +++ josef busin             +++
    4 rem +++ sudetenstr. 37          +++
    5 rem +++ 7321 eschenbach         +++
    6 rem +++ tel.: 07161/42817       +++
    8 rem +++ hauptprogramm           +++
   15 ifa=0thena=1:load"zauber-sprites 2",8,1
   20 data120,169,0,141,18,208,173,17,208,41,127,141,17,208,169,129,141,26
   25 data208,169,91,160,3,141,20,3,140,21,3,88,96,173,25,208,141,25,208,41
   30 data1,208,3,76,144,3,173,18,208,201,200,176,21,169,200,141,18,208,169
   35 data4,160,0,141,32,208,140,33,208,104,168,104,170,104,64,169,0,141,18
   40 data208,169,4,160,4,76,120,3,165,197,201,64,208,14,169,0,141,4,212,141
   45 data5,212,141,6,212,76,49,234,141,1,212,73,255,141,0,212,169,15,141,24
   50 data212,169,25,141,5,212,169,68,141,6,212,169,17,141,4,212,76,49,234
   55 data120,169,51,133,1,169,0,133,95,133,90,133,88,169,208,133,96,169,240
   60 data133,89,169,224,133,91,32,191,163,169,55,133,1,88,96
   65 data169,240,160,3,141,2,3,140,3,3,96,32,94,166,76,131,164
   66 data162,0,160,0,138,133,251,169,216,133,252,138,145,251,200,208,251,232,230
   67 data252,224,3,208,244,162,0,157,0,219,232,224,200,208,248,96
   70 sys58451:forx=828to1013:reada:a5=a5+a:pokex,a:next
   72 forx=704to738:reada:a5=a5+a:pokex,a:next
   73 ifa5<>26829thenprint"{clr}{down}{down}{rvon}fehler in datas!":stop
   75 sys963:poke981,160:sys963:poke56576,peek(56576)and252:poke53272,8:poke648,192
   80 sys828:sys997
  200 v=53248:pokev+21,0:pokev+29,0:pokev+23,0:printchr$(142)chr$(8)
  215 print"{clr}{brn}":gosub25000
  216 print"{down}{blk}{rvof}zauberschloss - 2. teil"
  217 print"{down}{down}ein adventure von josef busin"
  227 forx=49152to49352step11:pokex,46:nextx:forx=55296to55496step11:pokex,1:nextx
  232 pokev+21,3:poke50168,192:poke50169,192:pokev,180:pokev+1,177:pokev+39,1
  233 pokev+27,3:pokev+40,1:pokev+29,3:forx=1to1000:nextx
  234 forx=177to20step-1:pokev,180+z:z=z+.3:pokev+1,x
  235 pokev+2,180-z1:z1=z1+1.1:pokev+3,x+1:fory=0to45:nexty:nextx
  240 print"{home}{rvof}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{blk}die geister haben das schloss verlassen.";
  245 print"ihre stunde ist gekommen. toeten sie den";
  247 print"zauberer und retten sie damit endgueltig";
  248 print"das volk vor dem tyrannen.        "
  250 geta$:ifa$=""then250
  255 print"{clr}{yel}"chr$(14):print"kennen sie die spielregeln (j/n) ?"
  260 geta$:ifa$=""then260
  265 ifa$="j"then307
  270 print"{clr}Dieses Adventure ist die Fortsetzung"
  272 print"von ZAUBERSCHLOSS.{down}"
  274 print"Obwohl der Zauberer (hoffentlich!) von"
  276 print"Dir in eine Maus verwandelt wurde, ist"
  278 print"er jetzt wieder zurueckgekehrt."
  280 print"{down}Abermals wird er versuchen, das Volk zu"
  282 print"unterjochen. Als Koenig hast Du die"
  284 print"Pflicht, Dein Volk und Deinen Thron vor"
  292 print"dem boesen Zauberer zu retten."
  296 print"{down}{down}Folgende Befehle werden akzeptiert:{down}"
  297 geta$:ifa$=""then297
  298 print"{clr}{down}{down}{down}{down}"
  299 print"nimm...verliere...oeffne...zerschlage.."
  300 print".bekaempfe...trinke...esse...lese..."
  301 print"benutze...besteche...sage...kuesse..."
  302 print"fange...list"
  305 geta$:ifa$=""then305
  307 print"{clr}"chr$(142)
  310 data,,,63,255,255,255,255,,,28,255,255,255,255,255,,,,128,193,255,255,255
  320 data,,,96,243,255,255,255
  330 forx=0to7:reada:poke57344+x,a:nextx:forx=0to23:reada:poke58032+x,a:nextx
  340 data,,,1,3,3,3,3,60,126,255,255,255,255,24,24,,,,128,192,192,192,192
  350 data3,3,3,3,1,,,,24,255,231,231,255,255,129,129,192,192,192,192,128,,,
  360 data,,32,112,255,255,112,32,129,255,126,,255,255,,,,,4,14,255,255,14,4
  400 forx=0to15:reada:poke57584+x,a:next:forx=0to55:reada:poke57624+x,a:next
  900 goto1100
 1010 print"{clr}{down}{down}{down}{down}{down}{gry1}":forx=1to10:print"@VWX";:nextx
 1020 forx=1to160:print"{rvon} ";:nextx:print"{yel}";
 1030 forx=1to320:print"{rvon} ";:nextx
 1035 pokev+27,0:poke50168,193:poke50169,193
 1040 pokev+21,3:pokev+39,0:pokev,250:pokev+1,150:pokev+23,1:pokev+29,1
 1050 pokev+40,0:pokev+2,50:pokev+3,120
 1055 forx=49152to49370step11:pokex,46:nextx
 1060 forx=55296to55514step11:pokex,1:nextx
 1080 return
 1100 gosub1010
 1105 ifns=0andwo=0then1107
 1106 goto1140
 1107 iffl=1then1140
 1110 poke50170,206:pokev+21,7:pokev+41,0:pokev+4,180:pokev+5,150
 1120 print"{down}{rvof}{blk}du befindest dich in einer oeden gegend"
 1130 print"du siehst eine flasche{up}":goto1150
 1140 print"{down}{rvof}{blk}du befindest dich in einer oeden gegend"
 1150 print"{down}moegliche richtungen: n-s-w-o"
 1160 input"{down}kommando";k$
 1165 ifk$="n"thenns=ns+1:goto1200
 1170 ifk$="s"thenns=ns-1:goto1200
 1175 ifk$="w"thenwo=wo-1:goto1200
 1180 ifk$="o"thenwo=wo+1:goto1200
 1185 ifk$="nimm flasche"andns=0andwo=0thenfl=1:pokev+21,3:goto1200
 1186 ifk$="oeffne flasche"andfl=1then1195
 1187 ifk$="verliere flasche"andfl=1then1197
 1190 ifk$="zerschlage flasche"andfl=1then20000
 1195 print"{clr}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{rvon}unmoeglich!":forx=1to2000:nextx
 1196 goto1100
 1197 pokev+21,0:print"{clr}{down}{down}{wht}die flasche hat sich in rauch aufge-"
 1198 print"loest.":print"somit ist das abenteuer zuende, bevor es";
 1199 print"erst richtig angefangen hat.":end
 1200 goto1100
 2100 poke53269,0:poke876,210:sys828:poke880,210:sys828
 2105 dimf1(3,4,4),f2(3,4,4),b1(3,4,4),b2(3,4,4),g$(39),b$(14),a(11)
 2110 forx1=0to3:forx2=0to4:forx3=0to4:readf1(x1,x2,x3):next:next:next
 2112 data8,14,12,30,4,128,13,14,7,2,8,14,15,5,4097,1034,133,11,14,22,1,8,133,128,1
 2115 data10,14,14,38,2,,11,13,15,5,9,15,30,15,4,10,13,13,15,38,137,8204,4,,5
 2117 data130,8,12,6,2,25,12,14,12,16391,8,14,47,12,7,2,131,11,12,7,,1,2057,4,17
 2120 data8,12,140,14,6,34,520,6,66,1,139,4,387,11,6,9,12,15,5,3,8,12,141,4,33
 2125 forx1=0to3:forx2=0to4:forx3=0to4:readf2(x1,x2,x3):next:next:next
 2130 data,32,,,,2,,,,,,,2048,,,,,,,,,1,,2,,,,,,,16,,,,4096,,,,,16,32,,,,,,,8,16
 2135 data,,,32,,,,,,512,,,,,,64,,,,,,1024,4,,,,,256,,,,,,128,,64,,,,,,,,,,,16,,,,
 2140 forx1=0to3:forx2=0to4:forx3=0to4:readb1(x1,x2,x3):next:next:next
 2145 data1024,,,,,,,,,1,4,,,,,,,8,,,,4,,,4096,,,,,512,,,,,,,,,,,,,,,,,,,,,256
 2150 data,,128,2,,,,,,512,,,,,8192,,,,,,4,,16384,,16,,,,2048,,,,,,,64,,,4
 2155 data,,,,,,,,4,
 2160 forx1=0to3:forx2=0to4:forx3=0to4:readb2(x1,x2,x3):next:next:next
 2165 data,,,,1,,,256,,,,,,,,,,,,,4,,,,,,,,,,,,1,,,32,,,1024,,,512,,,,,,,,,,128
 2170 data,,,,,,,,,,,,,,,,,,,,,,,,,,,,,2,,,,,,,,,,8,,64,,,,,,
 2180 forx=1to14:readb$(x):next
 2185 databekaempfe,benutze,besteche,esse,fange,kuesse,lese,list,nimm,oeffne
 2190 datasage,trinke,verliere,zerschlage
 2195 forx=1to39:readg$(x):next
 2196 forx=1to38:fory=x+1to39:ifg$(x)<g$(y)then2198
 2197 v$=g$(x):g$(x)=g$(y):g$(y)=v$
 2198 nexty:nextx
 2200 datahammelkeule,schlangen,hoellenstein,wache,wachen,kobold,zauberstein
 2205 datagold,bogenschuetze,ghoul,vampir,honig,zaubertrank,wein,speer,schwert
 2210 dataschild,peitsche,ungeheuer,tontafel,zauberring,kroete,floete,tuer,baer
 2215 datasilberdolch,werwolf,spiegel,flasche,shadow,netz,zauberer,zepter
 2220 datasteinerne,saeure,schemel,gegengift,flaschengeist,holzpflock
 2225 x1=1:x2=0:x3=1
 2250 gosub21000:ifg>0theng=g+1:ifg=5then23000
 2252 ifzt>0thenzt=zt+1:ifzt=20thengosub24000
 2260 za=za+1:gosub11000:gosub10000:gosub9900:sys704
 2263 iff1(x1,x2,x3)>127thenj1=1
 2265 ifj1=1thengosub30000
 2270 print"{home}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}";:input"kommando";k$
 2290 ifk$="n"and(fgand1)=1thenx2=x2-1:goto2400
 2300 ifk$="s"and(fgand2)=2thenx2=x2+1:goto2400
 2310 ifk$="w"and(fgand4)=4thenx3=x3-1:goto2400
 2320 ifk$="o"and(fgand8)=8thenx3=x3+1:goto2400
 2330 ifk$="h"and(fgand16)=16thenx1=x1+1:goto2400
 2340 ifk$="r"and(fgand32)=32thenx1=x1-1:goto2400
 2345 gosub3000:iffe=0then2400
 2350 print"{rvon}nicht moeglich!":forx=1to2000:nextx:goto2250
 2400 gosub12000:gosub26000:goto2250
 3000 fe=0:ifk$="list"then7000
 3005 x0=0:forx=1tolen(k$):ifmid$(k$,x,1)=" "thenx0=x
 3010 nextx:ifx0=0thenfe=1:return
 3015 k1$=left$(k$,x0-1):k2$=mid$(k$,x0+1,len(k$)-x0)
 3020 x0=0:forx=1to39:ifk2$=g$(x)ork2$="sesamoopera"thenx0=x
 3030 nextx:ifx0=0thenfe=1:return
 3040 x0=0:forx=1to14:ifk1$=b$(x)thenx0=x
 3050 nextx:ifx0=0thenfe=1:return
 3060 ifx0>8then3090
 3070 onx0gosub3500,4000,4500,5000,5500,6000,6500
 3080 return
 3090 x0=x0-8:onx0gosub7500,8000,8500,9000,9500,13000
 3100 return
 3500 fe=0:s=0:gosub26000
 3510 ifk2$=g$(1)and(f1(x1,x2,x3)and16384)=16384then3530
 3511 ifk2$=g$(2)and(f2(x1,x2,x3)and2048)=2048then3545
 3512 ifk2$=g$(4)and(b1(x1,x2,x3)and32)=32then3555
 3513 ifk2$=g$(13)and(b2(x1,x2,x3)and256)=256then3570
 3514 ifk2$=g$(20)and(f2(x1,x2,x3)and32)=32then3610
 3515 ifk2$=g$(22)and(f2(x1,x2,x3)and256)=256then3630
 3516 ifk2$=g$(29)and(f1(x1,x2,x3)and8192)=8192then3670
 3517 ifk2$=g$(30)and(f1(x1,x2,x3)and1024)=1024then3705
 3518 ifk2$=g$(31)ork2$=g$(32)and(f1(x1,x2,x3)and128)=128then3745
 3519 ifk2$=g$(34)and(f1(x1,x2,x3)and2048)=2048then3860
 3521 ifk2$=g$(7)and(f1(x1,x2,x3)and4096)=4096then3925
 3522 ifk2$=g$(26)and(f2(x1,x2,x3)and128)=128then3960
 3525 fe=1:return
 3530 gosub3990:pokev+21,0:print"{clr}{down}{down}der baer hat sich auf keinen langen"
 3540 print"kampf eingelassen."
 3542 print"mit einem einzigen prankenhieb hat er"
 3543 print"dich ins jenseits befoerdert.":goto55000
 3545 gosub3990:pokev+21,0:print"{clr}{down}{down}":x0=0:forx=1to4:iftr$(x)=g$(19)thenx0=x
 3546 nextx:ifx0=0then3550
 3547 print"der pfeil des bogenschuetzen hat nur den schild getroffen."
 3548 forx=1to1500:nextx:return
 3550 print"leider war der unheimliche bogenschuetze";:print"schneller."
 3552 print"sein magischer pfeil hat dich getroffen.":goto55000
 3555 gosub3990:pokev+21,0:print"{clr}{down}{down}"
 3556 print"da du den ";g$(4);" gereizt hast,"
 3560 print"ist es nur selbstverstaendlich, dass er"
 3565 print"dich wie ein laestiges insekt in der"
 3566 print"luft zerrissen hat.":goto55000
 3570 gosub3990:pokev+21,0:print"{clr}{down}{down}der kobold hat es vorgezogen zu ver-":print"schwinden."
 3575 forx=1to4:iftr$(x)=g$(8)then3590
 3580 next
 3581 ifx1=0thenb2(1,2,1)=b2(1,2,1)+256
 3582 ifx1=1thenb2(2,3,3)=b2(2,3,3)+256
 3583 ifx1=2thenb2(0,1,2)=b2(0,1,2)+256
 3586 b2(x1,x2,x3)=b2(x1,x2,x3)-256:forx=1to2000:nextx:return
 3590 print"leider hat er dein gold mitgenommen."
 3591 ifx1=0thenb2(1,2,1)=b2(1,2,1)+256
 3592 ifx1=1thenb2(2,3,3)=b2(2,3,3)+256
 3593 ifx1=2thenb2(0,1,2)=b2(0,1,2)+256
 3595 forx=1to4:iftr$(x)=g$(8)thentr$(x)=""
 3600 nextx:x0=x:gosub23200:goto3586
 3610 gosub3990:pokev+21,0:print"{clr}{down}{down}"
 3611 print"die schlangen haben dich gebissen."
 3615 print"du bist vergiftet.":print"wenn du nicht bald etwas dagegen"
 3620 print"unternimmst, ist es aus.":g=1
 3625 forx=1to2500:nextx:return
 3630 gosub3990:pokev+21,0:ifk$=g$(37)then3655
 3635 print"{clr}{down}{down}der geheimnisvolle shadow hat dir"
 3640 print"saemtliche lebensenergie genommen."
 3645 print"um deine leblose huelle werden sich die"
 3650 print"ratten kuemmern.":goto55000
 3655 x0=0:forx=1to4:iftr$(x)=g$(37)thenx0=x
 3656 nextx:ifx0=0then3635
 3657 f2(x1,x2,x3)=f2(x1,x2,x3)-256:print"{clr}{down}{down}du hast den geheimnisvollen shadow"
 3660 print"vernichtet."
 3661 print"leider ist dabei der ";g$(37);" auch"
 3662 print"vernichtet worden.":gosub23200:forx=1to3000:next:return
 3670 gosub3990:pokev+21,0:print"{clr}{down}{down}":ifk$=g$(39)then3685
 3675 print"das schreckliche ungeheuer hat dich mit"
 3680 print"haut und haaren gefressen.":goto55000
 3685 x0=0:forx=1to4:iftr$(x)=g$(39)thenx0=x
 3686 nextx:ifx0=0then3675
 3688 f1(x1,x2,x3)=f1(x1,x2,x3)-8192
 3690 print"das ";g$(39);" hat das schreckliche"
 3695 print"ungeheuer vernichtet.":forx=1to2000:nextx:return
 3705 gosub3990:pokev+21,0:print"{clr}{down}{down}":ifk$=g$(11)then3725
 3710 print"der blutruenstige vampir hat dich gebis-";:print"sen...{down}"
 3715 print"da du nun ebenfalls zum vampir wirst,"
 3720 print"kannst du nicht weiterspielen.":goto55000
 3725 x0=0:forx=1to4:iftr$(x)=g$(11)thenx0=x
 3730 nextx:ifx0=0then3710
 3735 f1(x1,x2,x3)=f1(x1,x2,x3)-1024:print"du hast den vampir gepfaehlt."
 3740 print"er ist zu staub zerfallen.":forx=1to2000:nextx:return
 3745 gosub3990:pokev+21,0:print"{clr}{down}{down}":gosub33000
 3750 if(f1(x1,x2,x3)and256)=256then3815
 3755 if(k$=g$(21)ands=1)or(k$=g$(37)ands=2)or(k$=g$(24)ands=0)then3775
 3760 if(f2(x1,x2,x3)and2)=2thengosub23100:return
 3761 print"die wache hat dich gefangen und dich"
 3765 print"in den kerker gebracht.":x1=0:x2=1:x3=0:forx=1to2000:nextx:a(0)=-1
 3770 return
 3775 print"die wache hat es vorgezogen, zu ver-"
 3780 print"schwinden.":ifk$=g$(24)ands=0thengosub23400
 3781 ifk$=g$(37)ands=2thengosub29100
 3782 forx=0to3:if(f1(x,1,3)and128)=128then3784
 3783 f1(x,1,3)=f1(x,1,3)+128
 3784 nextx:f1(x1,x2,x3)=f1(x1,x2,x3)-128
 3785 ifx1=0andx2=1andx3=0then3795
 3790 forx=1to1500:nextx:return
 3795 print"{down}du hast die wache, die dir unter um-"
 3800 print"staenden haette helfen koennen, verjagt.";
 3805 print"nun musst du bis an dein lebensende im":print"kerker bleiben.":goto55000
 3815 ifk$=g$(37)ands=2then3825
 3820 goto3760
 3825 x0=0:forx=1to4:iftr$(x)=g$(37)thenx0=x
 3830 nextx:ifx0=0then3760
 3835 print"du hast die wachen verjagt."
 3840 print"dabei ist aber der ";g$(37);" zu"
 3845 print"staub zerbroeselt.":gosub23200:f1(x1,x2,x3)=f1(x1,x2,x3)-384
 3850 forx=1to3000:next:return
 3860 gosub3990:pokev+21,0:print"{clr}{down}{down}":ifk$=g$(23)then3875
 3870 print"der werwolf hat dich zerrissen.":goto55000
 3875 x0=0:forx=1to4:iftr$(x)=g$(23)thenx0=x
 3880 nextx:ifx0=0then3870
 3885 print"du hast den werwolf vernichtet.":print"dabei wurde aber auch der ";g$(23)
 3886 f1(x1,x2,x3)=f1(x1,x2,x3)-2048
 3890 print"zerstoert.":gosub23200:forx=1to3000:next:return
 3925 gosub3990:pokev+21,0:print"{clr}{down}{down}":ifk$=g$(23)then3940
 3930 print"leider war der ghoul hungrig, und hat"
 3935 print"dich somit verspeist.":goto55000
 3940 x0=0:forx=1to4:iftr$(x)=g$(23)thenx0=x
 3945 nextx:ifx0=0then3930
 3950 print"du hast den ghoul getoetet.":print"leider ist dabei auch der ";g$(23)
 3952 print"vernichtet worden.":gosub23200:f1(x1,x2,x3)=f1(x1,x2,x3)-4096
 3955 forx=1to3000:next:return
 3960 gosub3990:pokev+21,0:print"{clr}{down}{down}":ifk$=g$(17)then3970
 3965 print"der steinerne hat dich mit einem schlag"
 3967 print"seiner steinernen faust erschlagen.":goto55000
 3970 x0=0:forx=1to4:iftr$(x)=g$(17)thenx0=x
 3972 nextx:ifx0=0then3965
 3975 print"die ";g$(17);" hat den steinernen auf-"
 3977 print"geloest.":gosub23200:f2(x1,x2,x3)=f2(x1,x2,x3)-128
 3980 forx=1to3000:next:return
 3990 input"womit";k$:x0=0:forx=1to39:ifk$=g$(x)thenx0=x
 3995 nextx:ifx0=0then2350
 3999 return
 4000 pokev+21,0:print"{clr}{down}{down}":ifk2$=g$(5)then4040
 4010 ifk2$=g$(16)then4090
 4020 ifk2$=g$(36)then4190
 4030 fe=1:return
 4040 if(f2(x1,x2,x3)and32)=32then4060
 4050 goto4030
 4060 print"die musik hat die schlangen beruhigt."
 4070 print"sie werden dich nicht beissen.":a(7)=-4:forx=1to3000:next:return
 4090 x0=0:forx=1to4:iftr$(x)=g$(16)thenx0=x
 4100 nextx:ifx0=0thenfe=1:return
 4101 ifx1=3thenfe=1:return
 4102 if(f2(x1+1,x2,x3)and16)=0thenfe=1:return
 4110 print"die peitschenschnur hat sich an einem"
 4120 print"balken an der decke verfangen. du bist"
 4130 print"daran nach oben geklettert.":x1=x1+1
 4140 f2(x1,x2,x3)=f2(x1,x2,x3)-16:forx=1to3000:next:return
 4190 x0=0:forx=1to4:iftr$(x)=g$(36)thenx0=x
 4200 nextx:ifx0=0thenfe=1:return
 4210 print"um dich herum wird alles dunkel."
 4211 print"eine fremde macht reisst dich fort."
 4212 print"du bist entmaterialisiert.{down}"
 4220 print"leider hat sich dabei der ";g$(36):print"in rauch aufgeloest."
 4221 x1=int(rnd(1)*3+0):x2=int(rnd(2)*4+0):x3=int(rnd(3)*4+0):k=0
 4230 forx=1to5000:next:gosub23200:gosub26000:return
 4500 pokev+21,0:if(f2(x1,x2,x3)and2)=2and(f1(x1,x2,x3)and128)=128then4520
 4510 fe=1:return
 4520 x0=0:forx=1to4:iftr$(x)=g$(8)thenx0=x
 4530 nextx:ifx0=0then4590
 4540 print"{clr}{down}{down}die wache hat das gold genommen und hat":print"dich freigelassen."
 4550 gosub23200:k=0:ifx2=1thenx3=1
 4570 ifx2=4thenx2=3
 4580 forx=1to3000:nextx:return
 4590 print"{clr}{down}{down}du hast nichts, womit du die wache"
 4600 print"bestechen koenntest.":forx=1to3000:next:return
 5000 pokev+21,0:print"{clr}":ifk2$=g$(9)ork2$=g$(12)then5020
 5010 fe=1:return
 5020 x0=0:forx=1to4:iftr$(x)=g$(9)ortr$(x)=g$(12)thenx0=x
 5040 next:ifx0=0thenfe=1:return
 5050 ifk2$=g$(9)thene$="die "+g$(9):goto5070
 5060 ifk2$=g$(12)thene$="den "+g$(12)
 5070 gosub23200:print"{down}{down}du hast ";e$;" wie ein tier":print"verschlungen."
 5080 print"{down}{down}{down}pfui!!!":forx=1to3000:next:return
 5500 pokev+21,0:print"{clr}{down}{down}":ifk2$=g$(13)and(b2(x1,x2,x3)and256)=256then5530
 5520 fe=1:return
 5530 x0=0:forx=1to4:iftr$(x)=g$(15)thenx0=x
 5540 nextx:ifx0=0then5590
 5550 print"du hast den kobold gefangen, bevor er"
 5560 print"verschwinden konnte.":print"du hast ihn aber wieder freigelassen,"
 5570 print"da er dir gold gegeben hat.":b2(x1,x2,x3)=b2(x1,x2,x3)-256
 5580 b1(x1,x2,x3)=b1(x1,x2,x3)+4:forx=1to2000:nextx:gosub5620:return
 5590 print"du hattest nichts, womit du den flinken"
 5600 print"kobold haettest fangen koennen.":print"er ist verschwunden."
 5601 forx=1to4:iftr$(x)=g$(8)thengosub5700
 5602 next
 5610 b2(x1,x2,x3)=b2(x1,x2,x3)-256:forx=1to2000:nextx:gosub5620:return
 5620 ifx1=0thenb2(1,2,1)=b2(1,2,1)+256
 5630 ifx1=1thenb2(2,3,3)=b2(2,3,3)+256
 5640 ifx1=2thenb2(3,3,2)=b2(3,3,2)+256
 5650 ifx1=3thenb2(0,1,2)=b2(0,1,2)+256
 5660 return
 5700 print"{down}leider hat er dein gold migenommen.":x0=x:gosub23200:return
 6000 pokev+21,0:print"{clr}{down}{down}":ifk2$=g$(14)then6020
 6010 fe=1:return
 6020 x0=0:forx=1to4:iftr$(x)=g$(14)thenx0=x
 6030 nextx:ifx0=0then6010
 6040 print"leider hat sich die kroete in keine"
 6050 print"schoene prinzessin verwandelt."
 6060 print"{down}dafuer hat sie dich gebissen, und da es"
 6070 print"sich um eine giftige art handelte, hast"
 6080 print"du das zeitliche gesegnet.":goto55000
 6500 pokev+21,0:print"{clr}{down}{down}":ifk2$=g$(27)then6520
 6510 fe=1:return
 6520 x0=0:forx=1to4:iftr$(x)=g$(27)thenx0=x
 6530 nextx:ifx0=0then6510
 6540 print"{down}{down}{down}{down}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}s e s a m o o p e r a":forx=1to3000:nextx
 6550 print"{clr}{down}{down}die ";g$(27);" ist zu staub zer-"
 6560 print"fallen.":gosub23200:forx=1to3000:next:return
 7000 pokev+21,0:print"{clr}{down}{down}du traegst:":x0=0:forx=1to4:iftr$(x)<>""thenx0=x
 7010 nextx:ifx0=0thenprinttab(13)"{down}nichts":forx=1to2000:nextx:return
 7020 print"{down}":forx=1to4:printtab(13)tr$(x):nextx
 7030 geta$:ifa$=""then7030
 7040 return
 7500 pokev+21,0:print"{clr}":x0=0:forx=1to4:iftr$(x)=""thenx0=x
 7502 nextx:ifx0=0thenfe=1:return
 7505 b1=b1(x1,x2,x3):b2=b2(x1,x2,x3)
 7507 ifk2$=g$(10)and(b2and1)=1then23300
 7508 ifk2$=g$(21)and(b2and32)=32thengosub23500:goto7780
 7509 ifk2$=g$(8)and(b1and4)=4then29500
 7510 ifk2$=g$(37)and(b1and1)=1thenb1=b1-1:goto7770
 7520 ifk2$=g$(5)and(b1and2)=2thenb1=b1-2:goto7770
 7530 ifk2$=g$(8)and(b1and4)=4thenb1=b1-4:goto7770
 7540 ifk2$=g$(12)and(b1and8)=8thenb1=b1-8:goto7770
 7550 ifk2$=g$(3)and(b1and16)=16thenb1=b1-16:goto7770
 7560 ifk2$=g$(4)and(b1and32)=32thenb1=b1-32:goto7770
 7570 ifk2$=g$(17)and(b1and64)=64thenb1=b1-64:goto7770
 7580 ifk2$=g$(14)and(b1and128)=128thenb1=b1-128:goto7770
 7590 ifk2$=g$(27)and(b1and256)=256thenb1=b1-256:goto7770
 7600 ifk2$=g$(24)and(b1and512)=512thenb1=b1-512:goto7770
 7610 ifk2$=g$(9)and(b1and1024)=1024thenb1=b1-1024:goto7770
 7620 ifk2$=g$(15)and(b1and2048)=2048thenb1=b1-2048:goto7770
 7630 ifk2$=g$(33)and(b1and4096)=4096thenb1=b1-4096:goto7770
 7640 ifk2$=g$(23)and(b1and8192)=8192thenb1=b1-8192:goto7770
 7650 ifk2$=g$(25)and(b1and16384)=16384thenb1=b1-16384:goto7770
 7670 ifk2$=g$(39)and(b2and2)=2thenb2=b2-2:goto7780
 7680 ifk2$=g$(38)and(b2and4)=4thenb2=b2-4:goto7780
 7690 ifk2$=g$(18)and(b2and8)=8thenb2=b2-8:goto7780
 7700 ifk2$=g$(11)and(b2and16)=16thenb2=b2-16:goto7780
 7710 ifk2$=g$(21)and(b2and2048)=2048thenb2=b2-2048:goto7780
 7720 ifk2$=g$(6)and(b2and64)=64thenb2=b2-64:goto7780
 7730 ifk2$=g$(36)and(b2and128)=128thenb2=b2-128:goto7780
 7740 ifk2$=g$(16)and(b2and512)=512thenb2=b2-512:goto7780
 7750 ifk2$=g$(19)and(b2and1024)=1024thenb2=b2-1024:goto7780
 7760 fe=1:return
 7770 b1(x1,x2,x3)=b1:tr$(x0)=k2$:return
 7780 b2(x1,x2,x3)=b2:tr$(x0)=k2$:return
 8000 pokev+21,0:print"{clr}{down}{down}":ifk2$=g$(3)then8040
 8010 ifk2$=g$(28)then8190
 8030 fe=1:return
 8040 x0=0:forx=1to4:iftr$(x)=g$(3)thenx0=x
 8050 nextx:ifx0=0then8030
 8055 tr$(x0)="":gosub23200
 8060 if(f1(x1,x2,x3)and128)=128then8090
 8070 print"du hast die flasche geoeffnet.":print"leider befand sich ein flaschengeist"
 8080 print"darin, der dich gefressen hat.":goto55000
 8090 print"du hast die flasche geoeffnet.":if(f1(x1,x2,x3)and384)=384then8150
 8100 print"in der flasche befand sich ein flaschen-";
 8110 print"geist, der herauskam und die wache ver-":print"jagte."
 8120 print"{down}leider hat er immer noch hunger, und du"
 8130 print"bist das einzig essbare wesen in der":print"umgebung.":forx=1to5000:next:a(11)=0
 8140 b1(x1,x2,x3)=b1(x1,x2,x3)+32:f1(x1,x2,x3)=f1(x1,x2,x3)-128:a(0)=0:return
 8150 print"in der flasche befand sich ein geist,":print"der die wachen verjagte."
 8160 print"{down}leider hat er immer noch hunger, und du"
 8170 print"bist das einzig essbare wesen in der":print"umgebung.":forx=1to4000:next:a(11)=0
 8180 b1(x1,x2,x3)=b1(x1,x2,x3)+32:f1(x1,x2,x3)=f1(x1,x2,x3)-384:a(0)=0:return
 8190 if(f1(x1,x2,x3)and64)=64andtu=0thenprint"die tuer ist verschlossen.":goto8260
 8191 if(f1(x1,x2,x3)and64)=64andtu=1thenprint"o.k.":f1(x1,x2,x3)=f1(x1,x2,x3)-63:goto8260
 8200 if(f2(x1,x2,x3)and8)=8then22000
 8210 if(f2(x1,x2,x3)and64)=64then8240
 8220 if(f2(x1,x2,x3)and512)=512then8250
 8225 if(f2(x1,x2,x3)and4096)=4096then8270
 8230 fe=1:return
 8240 print"du bist in den wassergraben gestuerzt,":print"und ertrunken.":goto55000
 8250 print"die tuer ist zugemauert worden."
 8260 forx=1to3000:nextx:return
 8270 print"diese tuer fuehrte ins freie.":print"leider kam gerade ein rudel hungriger"
 8271 print"woelfe vorbei...":print"{down}dein ende war schrecklich.":goto55000
 8500 pokev+21,0:print"{clr}{down}{down}":ifk2$="sesamoopera"then8520
 8510 fe=1:return
 8520 if(f2(x1,x2,x3)and1024)=1024then8550
 8525 if(f1(x1,x2,x3)and64)=0then8510
 8530 print"der zauberspruch hat die magische sperre";
 8540 print"der tuer beseitigt.":tu=1:goto8260
 8550 print"durch den magischen spruch hat sich ein"
 8560 print"geheimgang geoeffnet.":f2(x1,x2,x3)=f2(x1,x2,x3)-1024
 8570 f1(x1,x2,x3)=f1(x1,x2,x3)+1:goto8260
 9000 pokev+21,0:print"{clr}{down}{down}":ifk2$=g$(6)then9050
 9020 ifk2$=g$(33)then9070
 9025 ifk2$=g$(17)then9110
 9030 ifk2$=g$(38)then9090
 9040 fe=1:return
 9050 tr$=k2$:gosub9400:ifg>0thenprint"du bist geheilt.":g=0:gosub23200:goto8260
 9070 tr$=k2$:gosub9400:print"du bist durch den wein eingeschlafen."
 9071 forx=0to11:ifa(x)>0then9430
 9072 next
 9080 gosub23200:print"die wache hat dich gefunden und dich":gosub3765:return
 9090 tr$=k2$:gosub9400:zt=1:gosub23200
 9100 print"du fuehlst dich sehr stark.":forx=1to2000:next:return
 9110 tr$=k2$:gosub9400:print"die ";g$(17);" schmeckte aetzend..."
 9120 print"{down}leider hat sich dein magen aufgeloest.":goto55000
 9400 x0=0:forx=1to4:iftr$(x)=tr$thenx0=x
 9410 nextx:ifx0=0then2350
 9420 return
 9430 forx=1to3000:next:if(f2(x1,x2,x3)and2)=2then31000
 9435 if(f2(x1,x2,x3)and32)=32then9450
 9440 return
 9450 print"{clr}{down}{down}da du eingeschlafen bist, konnten dich"
 9460 print"die schlangen in aller ruhe beissen...":goto55000
 9500 pokev+21,0:print"{clr}":x0=0:forx=1to39:ifg$(x)=k2$thenx0=x
 9510 nextx:ifx0=0thenfe=1:return
 9512 ifx7=8thenfe=1:return
 9515 ifk2$=g$(9)and(f1(x1,x2,x3)and2048)=2048thengosub12700:return
 9517 ifk2$=g$(33)and(b1(x1,x2,x3)and32)=32thengosub12800:return
 9520 b1=b1(x1,x2,x3):b2=b2(x1,x2,x3)
 9521 ifk2$=g$(8)and(b1and4)=4thenb1=b1-4
 9522 ifk2$=g$(24)and(b1and512)=512thenb1=b1-512
 9523 ifk2$=g$(9)and(f1(x1,x2,x3)and16384)=16384thengosub9800:gosub28100:goto9660
 9525 ifk2$=g$(12)and(f1(x1,x2,x3)and16384)=16384thengosub9800:gosub28000:goto9660
 9526 ifk2$=g$(8)and(f1(x1,x2,x3)and128)=128thengosub9800:gosub32000:return
 9527 ifk2$=g$(8)and(b2and256)=256thengosub9800:gosub32020:return
 9530 ifk2$=g$(37)thengosub9800:b1=b1+1:goto9660
 9535 ifk2$=g$(5)thengosub9800:b1=b1+2:goto9660
 9540 ifk2$=g$(8)thengosub9800:b1=b1+4:goto9660
 9545 ifk2$=g$(12)thengosub9800:b1=b1+8:goto9660
 9550 ifk2$=g$(3)thengosub9800:b1=b1+16:goto9660
 9555 ifk2$=g$(4)thengosub9800:b1=b1+32:goto9660
 9560 ifk2$=g$(17)thengosub9800:b1=b1+64:goto9660
 9565 ifk2$=g$(14)thengosub9800:b1=b1+128:goto9660
 9570 ifk2$=g$(27)thengosub9800:b1=b1+256:goto9660
 9575 ifk2$=g$(24)thengosub9800:b1=b1+512:goto9660
 9580 ifk2$=g$(9)thengosub9800:b1=b1+1024:goto9660
 9585 ifk2$=g$(15)thengosub9800:b1=b1+2048:goto9660
 9590 ifk2$=g$(33)thengosub9800:b1=b1+4096:goto9660
 9595 ifk2$=g$(23)thengosub9800:b1=b1+8192:goto9660
 9600 ifk2$=g$(25)thengosub9800:b1=b1+16384:goto9660
 9610 ifk2$=g$(39)thengosub9800:b2=b2+2:goto9670
 9615 ifk2$=g$(38)thengosub9800:b2=b2+4:goto9670
 9620 ifk2$=g$(18)thengosub9800:b2=b2+8:goto9670
 9625 ifk2$=g$(11)thengosub9800:b2=b2+16:goto9670
 9630 ifk2$=g$(21)thengosub9800:b2=b2+2048:goto9670
 9635 ifk2$=g$(6)thengosub9800:b2=b2+64:goto9670
 9640 ifk2$=g$(36)thengosub9800:b2=b2+128:goto9670
 9645 ifk2$=g$(16)thengosub9800:b2=b2+512:goto9670
 9650 ifk2$=g$(19)thengosub9800:b2=b2+1024:goto9670
 9655 fe=1:return
 9660 b1(x1,x2,x3)=b1:return
 9670 b2(x1,x2,x3)=b2:return
 9800 x0=0:forx=1to4:iftr$(x)=k2$thenx0=x
 9810 nextx:ifx0=0then2350
 9820 gosub23200:return
 9900 r$="moegliche richtungen: "
 9910 if(fgand1)=1thenr$=r$+"-n"
 9920 if(fgand2)=2thenr$=r$+"-s"
 9930 if(fgand4)=4thenr$=r$+"-w"
 9940 if(fgand8)=8thenr$=r$+"-o"
 9950 if(fgand16)=16thenr$=r$+"-h"
 9960 if(fgand32)=32thenr$=r$+"-r"
 9970 r$=r$+"-":print"{home}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}"i$:printr$:return
10000 print"{clr}":fg=f1(x1,x2,x3):sys704
10002 gosub10700
10003 if(fgand64)=64thengosub10100
10004 if(f2(x1,x2,x3)and512)=512thengosub10100
10005 if(fgand1)=1thengosub10100
10006 if(f2(x1,x2,x3)and64)=64thengosub10300
10007 if(f2(x1,x2,x3)and4096)=4096thengosub10300
10015 if(fgand4)=4thengosub10200
10020 if(fgand8)=8thengosub10300
10025 if(fgand16)=16thengosub10400
10030 if(fgand32)=32thengosub10500
10040 return
10100 forx=49678to49690:pokex,100:nextx:poke49678,76
10110 s=0:poke49690,122:forx=1to5:poke49650-s,103:s=s+40:nextx
10120 forx=49451to49453:pokex,100:nextx
10130 s=0:forx=1to5:poke49654-s,101:s=s+40:nextx
10140 poke49694,76:forx=49695to49705:pokex,100:nextx:poke49705,122
10150 return
10200 poke49717,78:poke49756,78:poke49873,78:poke49912,78
10210 s=0:forx=1to13:poke49833-s,103:s=s+40:nextx
10220 s=0:poke49354,77:poke49395,77:forx=1to9:poke49435+s,103:s=s+40:nextx
10230 poke49755,122:poke49754,100
10240 poke49435,80:poke49434,99
10250 return
10300 poke49746,77:poke49787,77:poke49910,77:poke49951,77
10310 s=0:forx=1to13:poke49870-s,101:s=s+40:nextx:poke49389,78:poke49428,78
10320 s=0:forx=1to9:poke49468+s,101:s=s+40:nextx
10330 poke49788,76:poke49789,100:poke49468,79:poke49469,99
10340 r=0:return
10400 forx=49202to49208:pokex,100:nextx
10410 poke49242,77:poke49283,77:poke49248,78:poke49287,78
10420 poke49243,77:poke49247,78
10430 s=0:forx=1to7:poke49244+s,93:poke49244+s+40,107:s=s+80:nextx
10440 s=0:forx=1to7:poke49246+s,93:poke49246+s+40,115:s=s+80:nextx
10450 s=0:forx=1to7:poke49245+s+40,67:s=s+80:nextx
10460 return
10500 forx=49937to49943:pokex,99:nextx
10510 poke49897,78:poke49858,78:poke49903,77:poke49862,77:poke49898,78:poke49902,77
10520 s=0:forx=1to2:poke49899-s,66:poke49899-s-40,107:s=s+80:nextx
10530 s=0:forx=1to2:poke49901-s,66:poke49901-s-40,115:s=s+80:nextx
10540 s=0:forx=1to2:poke49900-s-40,67:s=s+80:nextx
10550 return
10700 s=0:forx=1to6:poke49152+s,77:s=s+41:next
10702 s=0:forx=1to6:poke49191+s,78:s=s+39:nextx
10704 forx=49398to49425:pokex,99:nextx
10710 s=0:forx=1to8:poke49398+s,101:s=s+40:nextx
10715 s=0:forx=1to8:poke49425+s,103:s=s+40:nextx:poke49398,79:poke49425,80
10720 if(fgand1)=1then10730
10725 forx=49678to49705:pokex,100:nextx:poke49678,76:poke49705,122
10730 if(fgand4)=4then10740
10735 s=0:forx=1to6:poke49717+s,78:s=s+39:nextx
10740 if(fgand8)=8then10750
10745 s=0:forx=1to6:poke49746+s,77:s=s+41:nextx
10750 return
11000 pokev+21,0:fg=f1(x1,x2,x3):f2=f2(x1,x2,x3):b1=b1(x1,x2,x3):b2=b2(x1,x2,x3)
11001 pokev+23,0:pokev+29,0
11005 i$="":s7=50168:x7=0:m=0
11010 if(fgand64)=64theni$=i$+" "+g$(28)
11020 if(fgand128)=128theni$=i$+" "+g$(31):m=20:f=0:gosub11500:a(0)=a(0)+1
11030 if(fgand256)=256thenm=20:f=0:gosub11500
11040 if(fgand512)=512theni$=i$+" "+g$(35):m=22:f=0:gosub11530:goto29000
11050 if(fgand1024)=1024theni$=i$+" "+g$(30):m=18:f=0:gosub11530:a(2)=a(2)+1
11060 if(fgand2048)=2048theni$=i$+" "+g$(34):m=24:f=0:gosub11530:a(3)=a(3)+1
11070 if(fgand4096)=4096theni$=i$+" "+g$(7):m=26:f=7:gosub11500:a(4)=a(4)+1
11080 if(fgand8192)=8192theni$=i$+" "+g$(29):m=33:f=5:gosub11550:a(5)=a(5)+1
11090 if(fgand16384)=16384theni$=i$+" "+g$(1):m=31:f=0:gosub11500:a(6)=a(6)+1
11100 if(f2and1)=1theni$=i$+"koboldhoehle"+" "+g$(8):m=29:f=7:gosub11520:return
11110 if(f2and2)=2theni$=i$+" "+"kerker":k=k+1:ifk=4then31000
11120 if(f2and4)=4theni$=i$+"schatzkammer"+" "+g$(8):m=29:f=7:gosub11520:return
11130 if(f2and8)=8theni$=i$+" "+g$(28)
11140 if(f2and16)=16thenx1=x1-1:gosub27000:goto11005
11150 if(f2and32)=32theni$=i$+" "+g$(20):m=32:f=0:gosub11520:a(7)=a(7)+1
11160 if(f2and64)=64theni$=i$+" "+g$(28)
11170 if(f2and128)=128theni$=i$+" "+g$(26):m=41:f=0:gosub11530:a(8)=a(8)+1
11175 if(f2and256)=256theni$=i$+" "+g$(22):m=37:f=1:gosub11530:a(9)=a(9)+1
11180 if(f2and512)=512theni$=i$+" "+g$(28)
11190 if(f2and2048)=2048theni$=i$+" "+g$(2):m=35:f=5:gosub11500:a(10)=a(10)+1
11200 if(f2and4096)=4096theni$=i$+" "+g$(28):return
11210 if(b1and1)=1theni$=i$+" "+g$(37):m=36:f=11:gosub11580
11220 if(b1and2)=2theni$=i$+" "+g$(5):m=39:f=0:gosub11500
11230 if(b1and4)=4theni$=i$+" "+g$(8):m=29:f=7:gosub11500
11240 if(b1and8)=8theni$=i$+" "+g$(12):m=40:f=7:gosub11580
11245 if(b1and16)=16theni$=i$+" "+g$(3):m=30:f=1:gosub11580
11250 if(b1and32)=32theni$=i$+" "+g$(4):m=47:f=5:gosub11500:a(11)=a(11)+1
11260 if(b1and64)=64theni$=i$+" "+g$(17):m=46:f=0:gosub11500
11270 if(b1and128)=128theni$=i$+" "+g$(14):m=44:f=5:gosub11580
11280 if(b1and256)=256theni$=i$+" "+g$(27):m=58:f=11:gosub11580
11290 if(b1and512)=512theni$=i$+" "+g$(24):m=61:f=0:gosub11500
11300 if(b1and1024)=1024theni$=i$+" "+g$(9):m=56:f=0:gosub11500
11310 if(b1and2048)=2048theni$=i$+" "+g$(15):m=57:f=1:gosub11500
11320 if(b1and4096)=4096theni$=i$+" "+g$(33):m=28:f=2:gosub11500
11330 if(b1and8192)=8192theni$=i$+" "+g$(23):m=59:f=11:gosub11500
11340 if(b1and16384)=16384theni$=i$+" "+g$(25):m=49:f=7:gosub11500
11345 if(b2and1)=1theni$=i$+" "+g$(10):f=2:m=48:gosub11580
11350 if(b2and2)=2theni$=i$+" "+g$(39):m=50:f=1:gosub11500
11360 if(b2and4)=4theni$=i$+" "+g$(38):m=60:f=0:gosub11500
11370 if(b2and8)=8theni$=i$+" "+g$(18):m=51:f=0:gosub11500
11380 if(b2and16)=16theni$=i$+" "+g$(11):m=54:f=0:gosub11580
11390 if(b2and32)=32theni$=i$+" "+g$(21):m=45:f=11:gosub11500
11400 if(b2and64)=64theni$=i$+" "+g$(6):m=52:f=0:gosub11500
11410 if(b2and128)=128theni$=i$+" "+g$(36):m=53:f=1:gosub11580
11420 if(b2and256)=256theni$=i$+" "+g$(13):m=21:f=0:gosub11580
11430 if(b2and512)=512theni$=i$+" "+g$(16):m=43:f=0:gosub11500
11440 if(b2and1024)=1024theni$=i$+" "+g$(19):m=27:f=0:gosub11500
11445 if(b2and2048)=2048theni$=i$+" "+g$(21):m=55:f=0:gosub11500
11450 return
11500 pokev+29,peek(v+29)+2^x7:pokev+23,peek(v+29)
11501 pokes7+x7,m+176:pokev+21,peek(v+21)+2^x7:pokev+39+x7,f:x7=x7+1:return
11520 pokev+29,0:pokev+23,0:forzz=1to8:gosub11501:next:return
11530 pokes7+x7,m+1+176:pokes7+x7+1,m+176:pokev+29,3:pokev+23,3
11531 pokev+2,210:pokev+3,185:pokev,210:pokev+1,148
11535 pokev+21,peek(v+21)+2^x7+2^(x7+1):pokev+39+x7,f:pokev+39+x7+1,f
11540 pokev+x7+2,peek(v+x7):pokev+x7+3,peek(v+x7+1)-42:x7=x7+2:j1=1:return
11550 pokes7,m+176:pokes7+1,m+1+176:pokes7+2,238:pokes7+3,239:pokev+23,255:pokev+29,255
11560 pokev+21,15:forx=0to3:pokev+39+x,5:next:pokev+6,250:pokev+7,167
11570 pokev+4,202:pokev+5,167:pokev+2,154:pokev+3,167:pokev,154:pokev+1,125:return
11580 gosub11501:return
12000 x0=0:forx=0to11:ifa(x)>0thenx0=x+1
12010 nextx:ifx0=0thenreturn
12015 pokev+21,0:print"{clr}{down}{down}"
12020 onx0gosub12900,3910,3710,3870,3930,3675,12500,3611,3965,3635,12600,3556
12025 forx=0to11:a(x)=0:next:return
12500 print"der baer hat dich gefressen.":goto55000
12600 x0=0:forx=1to4:iftr$(x)=g$(19)thenx0=x
12610 next:ifx0=0then3550
12620 gosub3547:return
12700 x0=0:forx=1to4:iftr$(x)=k2$thenx0=x
12710 next:ifx0=0thenreturn
12720 print"{down}{down}der ";g$(34);" hat die ";g$(9);" ge-"
12730 print"fressen. einstweilen ist er satt.":print"er wird dich nicht behelligen."
12740 tr$(x0)="":forx=1to4:iftr$(x)=""thentr$(x)=tr$(x+1)
12750 next:a(3)=-4:forx=1to3000:next:return
12800 x0=0:forx=1to4:iftr$(x)=k2$thenx0=x
12810 next:ifx0=0thenreturn
12820 print"der flaschengeist hat den wein gerochen,"
12830 print"und ist in der flasche verschwunden."
12840 print"er ist zum weingeist geworden.":gosub23200
12850 print"{down}{down}nun ist er in asbach uralt anzufinden!"
12860 b1(x1,x2,x3)=b1(x1,x2,x3)-32:a(11)=-1:forx=1to4000:next:return
12900 if(x1=0andx2=1andx3=1)or(x1=0andx2=3andx3=3)thena(0)=0:return
12910 gosub3760:return
13000 pokev+21,0:print"{clr}{down}{down}":ifk2$=g$(18)then13025
13010 ifk2$=g$(25)then 13050
13015 ifk2$=g$(28)then13100
13020 fe=1:return
13025 x0=0:forx=1to4:iftr$(x)=g$(18)thenx0=x
13026 nextx:ifx0=0thenfe=1:return
13027 gosub23200
13030 print"du hast den ";g$(18);" zerschlagen.":b2(x1,x2,x3)=b2(x1,x2,x3)+16
13040 forx=1to3000:nextx:return
13050 x0=0:forx=1to4:iftr$(x)=g$(25)thenx0=x
13060 nextx:ifx0=0thenfe=1:return
13070 print"du hast den ";g$(25);" zerschlagen."
13080 gosub23200:forx=1to3000:next:return
13100 ifzt=0thenprint"dazu fehlt dir leider die noetige kraft.";:goto13080
13110 print"dank deiner staerke konntest du die":print"mauer zerschlagen."
13120 f2(x1,x2,x3)=f2(x1,x2,x3)-512:f1(x1,x2,x3)=f1(x1,x2,x3)+1:goto13080
15000 pokev+21,0:print"{clr}{down}{down}der magische blitz des zauberes ist von"
15010 print"deinem ";g$(25);" abgeprallt, und hat den"
15020 print"zauberer selber getroffen.":print"{down}ein fuerchterlicher schrei, eine grelle"
15030 print"lichexplosion...":print"{down}{down}der zauberer ist vernichtet!!!"
15040 f1(x1,x2,x3)=f1(x1,x2,x3)-512
15050 forx=1to8000:next:goto2250
20000 pokev+21,0:poke887,9:sys828:print"{clr}{blk}{down}{down}durch das zerschlagen der flasche hast"
20010 print"du eine magische kettenreaktion ausge-"
20020 print"loest."
20030 print"ein wirbelwind ergreift dich und traegt"
20040 print"dich fort."
20050 print"{down}{down}{down}{down}{rght}{rght}{rght}{rght}{rght}moege die macht mit dir sein...":goto2100
21000 pokev,255:pokev+1,165:pokev+2,205:pokev+3,165:pokev+4,155:pokev+5,165
21010 pokev+6,105:pokev+7,165:pokev+8,55:pokev+9,165:pokev+10,230:pokev+11,170
21020 pokev+12,180:pokev+13,170:pokev+14,130:pokev+15,170:return
22000 pokev+21,0:print"{clr}{blk}":gosub25000:forx=1to40:print"{rvon} ";:next:print"{rvof}{down}du bist im freien."
22005 forx=0to4000:next
22010 print"dichte nebel umwallen das schloss,"
22020 forx=1to1000:poke49152+int(rnd(1)*799+0),102:next
22030 print"die das alte gemaeuer zerfressen und in":print"rauch aufloesen."
22040 forx=1to1000:poke49152+int(rnd(1)*799+0),32:next:forx=1to1000:next:print"{clr}{down}{down}"
22050 print"{rvof}das adventure ist geloest."
22055 print"{down}{down}du hast dein volk gerettet und hast"
22056 print"dich als wahrer koenig erwiesen."
22060 print"{down}{down}es wurden"za"schritte benoetigt.":end
23000 pokev+21,0:print"{clr}{down}{down}das schlangengift hat dich getoetet.":goto55000
23100 ifk1$<>"bekaempfe"thenreturn
23105 print"vorsicht!":print"die wache ist sehr schlecht gelaunt."
23110 print"wenn du sie weiter so aergerst, waere es";
23120 print"moeglich, dass du in der folterkammer":print"landest."
23130 forx=1to4000:next:return
23200 tr$(x0)="":forx=1to3:fory=x+1to4:iftr$(x)>tr$(y)then23220
23210 v$=tr$(x):tr$(x)=tr$(y):tr$(y)=v$
23220 nexty:nextx:gosub23600:return
23300 print"{down}{down}die magischen kraefte des flammenden"
23310 printg$(10);:print"s haben dich vernichtet.":goto55000
23400 print"{down}leider zerbrach dabei auch der ";g$(24);".":gosub23200:return
23500 ifzt>0then23540
23510 print"{down}{down}das ";g$(21);" steckt zu tief.":k2$=""
23520 print"du bist zu schwach, um es rauszuziehen.":forx=1to3000:next:return
23540 b2=b2-32:print"dank deiner staerke konntest du das"
23550 printg$(21);:print" herausziehen.":forx=1to2500:next:return
23600 forx=1to3:fory=x+1to4:iftr$(x)=tr$(y)thentr$(y)=""
23610 next:next:return
24000 pokev+21,0:print"{clr}{down}{down}die wirkung des zaubertranks hat nachge-":print"lassen."
24010 print"du bist wieder schwach.":forx=1to3000:next:return
25000 print"{home}{down}{down}{down}{down}{down}"," {rvon}{SHIFT-POUND}{CBM-*}",,"{left}{rvon}{SHIFT-POUND}{CBM-*}":print,"{rvon}{SHIFT-POUND}  {CBM-*}"spc(14)"{SHIFT-POUND}  {CBM-*}"
25010 print,"{rvon}{CBM-K}  {CBM-L}"spc(14)"{rvon}{CBM-K}  {CBM-L}"
25020 print,"{rvon}{CBM-K} {CBM-K}{CBM-L}"spc(14)"{rvon}{CBM-K} {CBM-K}{CBM-L}"
25030 print,"{rvon}{CBM-K} {CBM-K}{CBM-L}"spc(14)"{rvon}{CBM-K} {CBM-K}{CBM-L}"
25035 print,"{rvon}{CBM-K}  {CBM-L}{rvof}{CBM-D}{CBM-F}{CBM-D}{CBM-F}{CBM-D}{CBM-F}{CBM-D}{CBM-F}{CBM-D}{CBM-F}{CBM-D}{CBM-F}{CBM-D}{CBM-F}{rvon}{CBM-K}  {CBM-L}"
25040 print,"{rvon}{CBM-K}                    {CBM-L}"
25045 print,"{rvon}{CBM-K}                    {CBM-L}"
25050 print,"{rvon}{CBM-K}                    {CBM-L}"
25055 print,"{rvon}{CBM-K}                    {CBM-L}"
25060 print,"{rvon}{CBM-K}         {CBM-L}{rght}{CBM-K}        {CBM-L}"
25065 print,"{rvon}{CBM-K}         {CBM-L}{rght}{CBM-K}        {CBM-L}"
25070 print,"{rvon}{CBM-K}         {CBM-L}{rght}{CBM-K}        {CBM-L}":forx=1to40:print"{rvon} ";:nextx:return
26000 forx=0to11:ifa(x)>0thena(x)=0
26010 next:return
27000 pokev+21,0:print"{clr}{down}{down}du bist auf eine falltuer getreten."
27010 print"zum glueck hast du den sturz gut ueber-":print"standen."
27020 fg=f1(x1,x2,x3):f2=f2(x1,x2,x3):forx=1to3000:next:return
28000 print"{down}{down}der baer hat sich ueber den honig her-":print"gemacht.":gosub23200
28010 print"er ist zu sehr mit dem honig beschaef-"
28020 print"tigt, als dass du fuer ihn interessant":print"waerst.":a(6)=-5
28030 forx=1to3500:next:return
28100 print"{down}{down}der baer hat die ";g$(9)
28110 print"gefressen.":print"{down}einstweilen ist er satt.":a(6)=-3:goto28030
29000 gosub10000:gosub9900
29001 forx=1to5000:next:x0=0:forx=1to4:iftr$(x)=g$(25)thenx0=x
29010 next:ifx0=0then29030
29020 goto15000
29030 pokev+21,0:print"{clr}{down}{down}der zauberer hat dich mit einem"
29040 print"magischen blitz getoetet.":goto55000
29100 print"{down}dabei ist aber der ";g$(37);" zu"
29110 print"staub zerbroeselt.":gosub23200:return
29500 if(f2(x1,x2,x3)and1)=1or(f2(x1,x2,x3)and4)=4then29520
29510 goto7510
29520 goto7770
30000 print"{home}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}";:print"^_#":print"{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}$%&"
30010 print"{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}'()":j1=0:print"{home}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{down}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}";
30020 print"^_#":print"{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}$%&":print"{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}{rght}'()":return
31000 pokev+21,0:poke50168,240:poke50169,241:poke50170,242:pokev+21,7:pokev+29,7:pokev+23,7
31010 pokev+2,150:pokev+3,165:pokev,150:pokev+1,123:pokev+4,198:pokev+5,165
31020 pokev+39,0:pokev+40,0:pokev+41,0
31030 print"{clr}{down}die wache hat dich dem henker ueberge-":print"ben.":goto55000
32000 pokev+21,0:print"{clr}{down}{down}die wache hat sich das gold unter den"
32010 print"nagel gerissen.":gosub26000:forx=1to3000:next:return
32020 pokev+21,0:print"{clr}{down}{down}der kobold hat das gold an sich genom-"
32030 print"men.":gosub26000:forx=1to3000:next:return
33000 x0=0:forx=1to4:iftr$(x)=g$(21)andk$=g$(21)thenx0=x:s=1
33010 next:ifx0<>0thenreturn
33020 x0=0:forx=1to4:iftr$(x)=g$(24)andk$=g$(24)thenx0=x:s=0
33030 next:ifx0<>0thenreturn
33040 x0=0:forx=1to4:iftr$(x)=g$(37)andk$=g$(37)thenx0=x:s=2
33050 next:ifx0<>0thenreturn
33060 goto2350
55000 print"{down}{down}{down}so schnell kann ein abenteuer enden.":end

