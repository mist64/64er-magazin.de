;cursorposition.prg ==0801==
;Eingetippt von Endurion

    1 rem sys-demo
    2 rem
    3 rem cursorposition setzen/lesen
    4 rem
    5 rem
   10 a=780:rem akku
   20 x=781:rem x-register
   30 y=782:rem y-register
   40 f=783:rem flag-register
   50 up=65520:rem adresse von plot
   60 print chr$(147);"zeile 10, spalte 5"
   70 :
  100 rem carry-bit loeschen
  105 rem also cursorpos. setzen
  110 pokef,peek(f) and 254
  120 pokex,10:rem zeile 10
  130 pokey,5:rem spalte 5
  140 sys up:rem aufruf cursor plot
  150 print"** cursor gesetzt";
  190 :
  200 rem carry setzen
  205 rem also cursorpos. lesen
  210 pokef,peek(f) or 1
  220 sys up:rem aufruf plot
  230 print:print:print"cursorposition war:"
  240 print"zeile:";peek(x)
  250 print"spalte:";peek(y)
  260 print:end

