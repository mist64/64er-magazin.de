

;maennchendemo.prg ==0801==
  100 rem *******************************
  110 rem *                             *
  120 rem *  m a e n n c h e n d e m o  *
  130 rem *  -------------------------  *
  140 rem *                             *
  150 rem *  by   tobias        nicol   *
  160 rem *                             *
  170 rem *       neuwiesenstrasse 20   *
  180 rem *                             *
  190 rem *       6000  frankfurt  71   *
  200 rem *                             *
  210 rem *******************************
  220  
  230 rem ******* initialisierung *******
  240 poke 53280,0 : poke 53281,0
  250 a = 1545 : b = 1582 : c = 54297
  260 d = 55776 : e = 40
  270 print "{clr}"
  280  
  290 rem ***** farbspeicher setzen *****
  300 for i = d to d+3*e
  310 poke i,1
  320 next i
  330  
  340  
  350 rem **** bewegung nach rechts  ****
  360 for i = a to b
  370 gosub 550
  380 poke i-1,32 : poke i+1+e,32
  390 poke i-1+e,32 : poke i-e,32
  400 next i
  410  
  420 rem ***** bewegung nach links *****
  430 for i = b to a step -1
  440 gosub 550
  450 poke i+1,32 : poke i+1+e,32
  460 poke i-1+e,32 : poke i-e,32
  470 next i
  480  
  490 rem *** ruecksprung zum anfang  ***
  500 goto 350
  510  
  520 rem *** ende des hauptprogramms ***
  530  
  540  
  550 rem ** unterprogramm "bewegung"  **
  560 poke i,160 : poke i+1,77
  570 poke i-1,78 : poke i-e,81
  580 poke i-1+e,103 : poke i+1+e,101
  590 for j = 1 to peek (c) : next j
  600 return
  610  
  620  
  630  
  640 rem *******************************
  650 rem *** aenderungen fuer vc-20  ***
  660 rem *******************************
  670 rem
  680 rem bitte diese zeilen ersetzen:
  690 rem
  700 rem 240 poke 36879,8
  710 rem 250 a=7901 : b=7920 : c=36872
  720 rem 260 d=38620 : e=22

