

;q_heapsort.prg ==0801==
   10 rem erstellen eines feldes zum
   20 rem sortieren.
   30 rem das erstellen kann zufaellig
   40 rem oder geziehlt (durch eingabe)
   50 rem erfolgen.
   60 rem
   70 rem sortieralgorithmen erhalten die
   80 rem zeilennummern von 10000 bis 50000
   90 rem sie benoetigen jeweils diesen
   99 rem vorspann zur ausfuehrung.
  100 rem herstellung eines arrays:
  110 rem arrayvariable      - a$
  120 rem schleifenvariablen - x, y, z
  130 rem hilfsvariablen     - b$, c$, d$
  140 rem dreiecktausch mit  - s$
  150 print"{clr}":clr
  160 print"soll von {rvon}h{rvof}and oder {rvon}z{rvof}ufaellig erstellt":print
  170 input"werden ";x$
  180 ifx$<>"h"andx$<>"z"then150
  190 ifx$="h"thengosub220:gosub1000:goto210
  200 gosub220:gosub2000
  210 goto4000: rem weitermachen
  220 rem anzahl der elemente bestimmen
  230 print:input"anzahl der elemente ";a
  240 if a>10000thenprint:print"zu viele elemente":goto230
  250 ifa<10thenprint:print"zu wenige elemente":goto230
  255 dim a$(a)
  260 input"{down}{down}{rvon}d{rvof}rucker oder {rvon}b{rvof}ildschirm ";y$
  270 ify$<>"d"andy$<>"b"then260
  280 ify$="d"thend=4:goto300
  290 d=3
  300 return
 1000 rem eingabe von hand
 1010 print"{clr}"
 1020 print:print"sie muessen jetzt"a" elemente eingeben!":print:print
 1030 print"jedes element besteht aus 3 zeichen.":print:print
 1040 forx=1toa
 1050 printx". ";:input"element";a$(x)
 1060 iflen(a$(x))<>3then1050
 1070 nextx
 1080 return
 2000 rem zufaellige eingabe
 2010 print"{clr}"
 2020 print:print"es werden jetzt"a" elemente zufaellig":print:print"ausgewaehlt"
 2030 print:print"jedes element besteht aus 3 zeichen.":print:print
 2040 forx=1toa
 2050 a$(x)=""
 2060 fory=1to3:a$(x)=a$(x)+chr$(int(rnd(ti)*25)+65):nexty
 2070 nextx
 2080 return
 3000 rem zwischenausgabe der elemente
 3010 for i=1toa-9step10
 3020 for j=itoi+9:print#1,a$(j)" ";:nextj
 3030 print#1:nexti
 3040 return
 4000 rem weitermachen
 4005 open1,d
 4010 print"{clr}ausgabe des erstellten feldes"
 4020 print
 4030 gosub3000
 4040 rem sortierung startet
 4050 rem
10040 lg=int(a/2)+1:rg=a
10050 ifrg<=1then50000
10060 iflg<=1then10110
10080 lg=lg-1
10090 i=lg:goto10140
10110 s$=a$(1):a$(1)=a$(rg):a$(rg)=s$
10120 rg=rg-1
10130 i=1
10140 x$=a$(i)
10150 p=0
10160 if2*i<=rgandp=0then10210
10170 a$(i)=x$
10180 goto10050
10210 j=2*i
10220 ifj<rgthenifa$(j)<a$(j+1)thenj=j+1
10230 ifx$>=a$(j)then10260
10240 a$(i)=a$(j)
10250 i=j:goto10160
10260 p=1:goto10160
50000 rem endebehandlung
50010 print#1
50020 gosub 3000
50030 print#1,a;" elemente"
50040 print#1:print#1:close1
50050 end

